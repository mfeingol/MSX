10 ' ЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫ
20 ' ЫЫ                                  ЫЫ
30 ' ЫЫ              "SIMON"             ЫЫ
40 ' ЫЫ              -------             ЫЫ
50 ' ЫЫ                                  ЫЫ
60 ' ЫЫ            POR  ALTAIR           ЫЫ
70 ' ЫЫ                                  ЫЫ
80 ' ЫЫ  (C) ILLUMINATUS SOFTWARE  1991  ЫЫ
90 ' ЫЫ                                  ЫЫ
100 ' ЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫ
110 '
120 ' ЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫ
130 ' ЫЫ SELECCIONAR HERTZ ЫЫ
140 ' ЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫ
150 ' PRINT"1- Monitor":PRINT"2- TV":PRINT"? ";
160 ' A$=INPUT$(1):A=VAL(A$):IFA<1ORA>2THEN160ELSEPRINTA:FORI=1TO20:NEXT:CLS:COLOR3,0,0:VDP(10)=A
170 ' ИИИЫЫИИИИИИИИИИИИИ
180 ' ЫЫ PRESENTACION ИЫ
190 ' ИЫИИИИИИИЫЫИИИИИИИ
200 CLEAR:SCREEN4,,0:COLOR3,0,0:CLS:ZZ=750:OPEN"GRP:"AS#1:COLOR=(3,0,0,0):FORA=1TO100:NEXT
210 FORA=44TO43STEP-1:PRESET(A,92):PRINT#1,"ILLUMINATUS  SOFTWARE":NEXT:GOSUB1860
220 REM FORA=96TO97:PRESET(A,92):PRINT#1,"PRESENTS":NEXT:GOSUB1860
230 ' ЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫ
240 ' ЫЫ PANTALLA DE PRESENTACION ЫЫ
250 ' ЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫ
260 '
270 ' ЫЫЫЫЫЫЫЫЫЫЫЫ
280 ' ЫЫ LETRAS ЫЫ
290 ' ЫЫЫЫЫЫЫЫЫЫЫЫ
300 COLOR3,0,0:SCREEN7:FORA=1TO9:COLOR=(A,0,0,0):NEXT
310 LINE(0,0)-(512,212),2,B:LINE(1,1)-(511,211),2,B:LINE(2,2)-(510,210),2,B:LINE(3,3)-(509,209),2,B
320 DRAW"C5BM96,15R54D8L39D15R39D31L54U8R39U15L39U31":PAINT(97,16),5'S
330 DRAW"C5BM160,15R54D8L19D38R19D8L54U8R20U38L20U8":PAINT(161,16),5'I
340 DRAW"C5BM224,15F27E27D54L15U22G12H12D22L15U54":PAINT(225,25),5'M
350 DRAW"C5BM286,15R54D54L54U54BR15BD8R24D38L24U38":PAINT(287,16),5'O
360 DRAW"C5BM350,15R15":LINE-(389,47),5:DRAW"U32R15D54L15":LINE-(365,37),5:DRAW"D32L15U54":PAINT(351,16),5'N
370 DRAW"C9BM103,19R40BL40D23R40D22L40BM167,19R40BL20D46L20R40BM231,30D35BU35F20E20D35BM293,19R40D46L40U46BM357,19D46BU46":LINE-(396,65),9:DRAW"U46"'RAYAS
380 ' ЫЫЫЫЫЫЫЫЫЫЫЫЫЫ
390 ' ЫЫ PIRAMIDE ЫЫ
400 ' ЫЫЫЫЫЫЫЫЫЫЫЫЫЫ
410 LINE(256,75)-(359,178),2:LINE-(153,178),2:LINE-(256,75),2:PAINT(256,76),2:LINE(216,107)-(296,113),0,BF
420 LINE(217,113)-(212,110),6:LINE-(142,172),6:LINE-(152,178),6:LINE-(217,113),6:PAINT(213,114),6:LINE(213,110)-(290,110),7:LINE-(295,113),7:LINE-(218,113),7:LINE-(214,110),7:PAINT(222,112),7
430 LINE(224,106)-(218,104),6:LINE-(255,75),6:LINE-(224,106),6:PAINT(223,102),6:LINE(255,75)-(221,105),6
440 B=212:FORA=118TO178STEP5:LINE(400,A)-(B,A),0:LINE(B,A)-(B-10,A-5),0:B=B-5:NEXT
450 CIRCLE(256,97),19,9,,,.25:PAINT(256,97),9:CIRCLE(256,97),9,5:PAINT(256,97),5:CIRCLE(256,97),3,2:PAINT(256,97),2
460 LINE(256,97)-(6,150),8:LINE-(6,170),8:LINE-(256,97),8:PAINT(8,161),8:LINE(256,97)-(506,150),8:LINE-(506,170),8:LINE-(256,97),8:PAINT(504,161),8:LINE(256,97)-(6,160),8:LINE(256,97)-(506,160),8
470 ' ЫЫЫЫЫЫЫЫЫЫЫЫЫЫ
480 ' ЫЫ MENSAJES ЫЫ
490 ' ЫЫЫЫЫЫЫЫЫЫЫЫЫЫ
500 A=180:FORI=97TO77STEP-4:READA$:PRESET(A,I):PRINT#1,A$:A=A+10:NEXT
510 A=272:FORI=75TO99STEP4:READA$:PRESET(A,I):PRINT#1,A$:A=A+10:NEXT
520 DATAA,N,N,U,I,T
530 DATAC,O,E,P,T,I,S
540 PRESET(172,183):PRINT#1,"PROGRAMADO POR ALTAIR":PRESET(140,196):PRINT#1,"(C) ILLUMINATUS SOFTWARE 1991"
550 ' ЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫ
560 ' ЫЫ APARECE LA PIRAMIDE ЫЫ
570 ' ЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫ
580 CLEAR:OPEN"GRP:"AS#1:COLOR3,4,4:G=1:FORA=1TO7
590 IFA<3THENB=B+1
600 IFA<4THENC=C+1
610 IFA<5THEND=D+1
620 IFA<6THENE=E+1
630 IFA<7THENF=F+1
640 COLOR=(2,A,0,0):COLOR=(3,C,A,C):COLOR=(4,0,0,B):COLOR=(5,0,0,E):COLOR=(6,D,G,G):COLOR=(7,C,0,0):COLOR=(8,F,F,0):COLOR=(9,A,A,A):NEXT
650 A$=INKEY$:IFA$=""THEN650
660 IFA$=CHR$(11)THEN670ELSE730
670 RESTORE690:FORA=1TO8:A$=INPUT$(1):READB$:IFA$<>B$THEN730ELSENEXT
680 SCREEN4:COLOR3,0,0:CLS:CLOSE:OPEN"GRP:"AS1:COLOR=(3,0,0,0):FORA=57TO56STEP-1:PRESET(A,92):PRINT#1,"AUNQUE CON TRUCOS,":NEXT:ZZ=750:GOSUB1860:GOTO1970
690 DATAF,E,I,N,G,O,L,D
700 ' ЫЫЫЫЫЫЫЫЫЫ
710 ' ЫЫ MENU ЫЫ
720 ' ЫЫЫЫЫЫЫЫЫЫ
730 LINE(6,6)-(506,206),4,BF
740 COLOR=(3,0,0,2):DRAW"BM203,47C3R104D12L104U12"
750 PRESET(208,50):PRINT#1,"ELIGE OPCION"
760 PRESET(200,80):PRINT#1,"1.INSTRUCCIONES"
770 PRESET(200,100):PRINT#1,"2.JUGAR"
780 PRESET(200,120):PRINT#1,"3.VOLVER A LA PORTADA"
790 PRESET(200,140):PRINT#1,"4.VOLVER AL BASIC"
800 B=0:D=2:FORA=1TO7:IFA<4THENB=B+1
810 IFB=3THEND=3
820 COLOR=(3,B,A,D):FORI=1TO30:NEXT:NEXT
830 A$=INKEY$:IFA$=CHR$(27)THEN COLOR15,0,0:END ELSEA=VAL(A$):IFA>4ORA<1THEN830
840 COLOR=(15,7,0,2):ONAGOTO890,1120,850,1080
850 PRESET(200,120):COLOR15:PRINT#1,"3.VOLVER A LA PORTADA":FORC=1TO300:NEXT:GOSUB1160:GOTO300
860 ' ЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫ
870 ' ЫЫ INSTRUCCIONES ЫЫ
880 ' ЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫ
890 PRESET(200,80):COLOR15:PRINT#1,"1.INSTRUCCIONES":FORC=1TO300:NEXT:GOSUB1160:SCREEN0:FORA=4TO3STEP-1:COLOR=(A,0,0,2):NEXT:WIDTH80:KEYOFF:COLOR3,4,4
900 LOCATE25,2:PRINT"XWWWWWWWWWWWWWWWWWWWWWWWWWWWY"
910 LOCATE25,3:PRINT"V I N S T R U C C I O N E S V"
920 LOCATE25,4:PRINT"ZWWWWWWWWWWWWWWWWWWWWWWWWWWW["
930 LOCATE5,6:PRINT"SIMON es un juego sencillo y adictivo, y para terminarlo sўlo  se  necesitauna buena memoria. Al empezar a jugar, el ordenador mostrar  una combinaciўn  decuatro colores (por ejemplo ROJO, VERDE, AZUL, ROJO), y tЈ tendr s que";
940 PRINT" repetirlaa continuaciўn, color por color. Si lo aciertas, la siguiente combinaciўn tendr un color m s. Si consigues llegar hasta los veinte  colores,  habr s  pasado  defase, haci‚ndose el juego m s r pido, y, por lo tanto, m s difЎcil. ";
950 PRINT"Si pasas  laquinta fase, te habr s acabado el juego. Si por el contrario,  te  equivocas  alintroducir la combinaciўn, tendr s que volver a empezar desde el principio.":LOCATE5,14:PRINT"Antes de jugar, el ordenador ";
960 PRINT"preguntar  por  la  fase  en  la  que  quieresempezar, del uno al cinco. Lўgicamente, la dificultad aumentar  de  una  fase  aotra."
970 LOCATE5,17:PRINT"Puedes usar los cursores o el joystick. Para  abandonar  la  partida  pulsa®F1Ї, y volver s al MENU. ­Buena suerte!"
980 LOCATE31,21:PRINT"®PULSA UNA TECLAЇ"
990 B=0:D=2:FORA=1TO7:IFA<4THENB=B+1
1000 IFB=3THEND=3
1010 COLOR=(3,B,A,D):FORI=1TO30:NEXT:NEXT
1020 IFINKEY$=""THEN1020ELSEQ=1:GOSUB1160
1030 SCREEN7:COLOR=(4,0,0,2)
1040 CLEAR:OPEN"GRP:"AS#1:COLOR=(2,0,0,2):COLOR3,4,4:CLS:LINE(1,1)-(511,211),2,B:LINE(2,2)-(510,210),2,B:LINE(3,3)-(509,209),2,B:FORC=1TO7:COLOR=(2,C,0,2):FORW=1TO50:NEXT:NEXT:GOTO740
1050 ' ЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫ
1060 ' ЫЫ VOLVER AL BASIC ЫЫ
1070 ' ЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫ
1080 PRESET(200,140):COLOR15:PRINT#1,"4.VOLVER AL BASIC":FORC=1TO300:NEXT:GOSUB1160:SCREEN0:COLOR15,1,1:KEYOFF:KEY1,"color ":END
1090 ' ЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫ
1100 ' ЫЫ EMPEZAR A JUGAR ЫЫ
1110 ' ЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫ
1120 PRESET(200,100):COLOR15:PRINT#1,"2.JUGAR":FORC=1TO300:NEXT:GOSUB1160:GOTO1230
1130 ' ЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫ
1140 ' ЫЫ RUTINA DE FADING DE LETRAS VERDES Y BORDE ROJO ЫЫ
1150 ' ЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫ
1160 D=3:B=3:FORA=6TO0STEP-1:IFA>3THENB=B-1
1170 IFA=6THEND=D-1
1180 COLOR=(3,B,A,D):COLOR=(15,A,0,2):FORI=1TO30:NEXT:NEXT:IFQ=1THENRETURN
1190 LINE(200,45)-(366,150),4,BF:RETURN
1200 ' ЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫ
1210 ' ЫЫ ELEGIR FASE ЫЫ
1220 ' ЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫ
1230 COLOR3:PRESET(100,102):PRINT#1,"FASE EN LA QUE QUIERES EMPEZAR (1-5):";
1240 B=0:D=2:FORA=1TO7:IFA<4THENB=B+1
1250 IFB=3THEND=3
1260 COLOR=(3,B,A,D):FORI=1TO30:NEXT:NEXT:CLEAR:OPEN"GRP:"AS#1
1270 F$=INPUT$(1):L=VAL(F$):IFL<1ORL>5THEN1270ELSEPRINT#1,L:FORC=1TO100:NEXT:T=ABS(L-6)*100:KEY1,CHR$(255):GOSUB1160
1280 ' ЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫ
1290 ' ЫЫ DIBUJAR PANTALLA DE JUEGO ЫЫ
1300 ' ЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫ
1310 COLOR9,0,0:SCREEN5:DIMA$(100):DIMB$(100)
1320 FORA=1TO13:COLOR=(A,0,0,0):NEXT
1330 DRAW"C5BM80,48D100H50E50BM86,43R83H42G42BM175,48D100E50H50BM86,153R83G42H42":PAINT(79,50),1,5:PAINT(87,42),2,5:PAINT(176,50),3,5:PAINT(88,155),4,5
1340 DRAW"C5BM127,76F8L15R15BF3F8G8U15D15BG3G8H8R15L15BH3H8E8D15U15BE3E8":PAINT(127,83),2,5:PAINT(140,96),3,5:PAINT(127,113),4,5:PAINT(114,96),1,5
1350 I=0:FORA=1TO3:IFA<2THENI=I+1
1360 FORK=1TO4:COLOR=(K,I,I,I):NEXT:COLOR=(5,A,A,A):COLOR=(6,I,I,I):COLOR=(13,A,A,A):NEXT:PRESET(5,8):COLOR10:PRINT#1,"COLORES:":PRESET(195,8):PRINT#1,"FASE:":CC=10:GOSUB1890:A=3:GOSUB1910:GOSUB1920:COLOR9:PRESET(92,200):PRINT#1,"EMPEZAMOS":GOSUB1870
1370 FORC=1TO500:NEXT
1380 ' ЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫ
1390 ' ЫЫ BUCLE PRINCIPAL ЫЫ
1400 ' ЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫ
1410 IFA=20THENCOLOR=(9,0,0,0):PRESET(108,200):PRINT#1,"FASE";L:GOSUB1870:FORC=1TO1000:NEXT:GOSUB1880:GOSUB1920:LINE(0,200)-(256,208),0,BF:COLOR=(9,0,0,0):PRESET(92,200):PRINT#1,"EMPEZAMOS":GOSUB1870:FORC=1TO1000:NEXT:GOSUB1880:LINE(0,200)-(256,208),0,BF
1420 A=3:GOSUB1910:FORC=1TO50:NEXT
1430 FORA=4TO20
1440 IFA>4THENR=A:AA=1:FORC=1TOA-1:P$=A$(C):P=VAL(P$):FORQ=1TOT/2:NEXT:ONPGOSUB1670,1680,1690,1700:FORQ=1TOT/2:NEXT:COLOR=(P,1,1,1):R=R+1:NEXT:R=A-1:FORC=1TOT/2:NEXTELSEAA=4:R=0
1450 FORB=1TOAA:R=R+1:Z=INT(RND(-TIME)*4):Z=Z+1:ONZGOSUB1670,1680,1690,1700
1460 FORC=1TOT/2:NEXT:COLOR=(Z,1,1,1):FORC=1TOT/2:NEXT:NEXT:GOSUB1880:LINE(0,200)-(256,208),0,BF:PRESET(56,200):PRINT#1,"AHORA PRUEBA TU...":GOSUB1870:D=0
1470 ' ЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫ
1480 ' ЫЫ MOVIMIENTO DEL JUGADOR ЫЫ
1490 ' ЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫ
1500 S=STICK(0)ORSTICK(1)
1510 IFS=1THENCOLOR=(2,0,7,0):D=D+1:B$(D)="2":FORC=1TO150:NEXT:COLOR=(2,1,1,1):IFD=ATHEN1600
1520 IFS=3THENCOLOR=(3,0,0,7):D=D+1:B$(D)="3":FORC=1TO150:NEXT:COLOR=(3,1,1,1):IFD=ATHEN1600
1530 IFS=5THENCOLOR=(4,7,7,0):D=D+1:B$(D)="4":FORC=1TO150:NEXT:COLOR=(4,1,1,1):IFD=ATHEN1600
1540 IFS=7THENCOLOR=(1,7,0,0):D=D+1:B$(D)="1":FORC=1TO150:NEXT:COLOR=(1,1,1,1):IFD=ATHEN1600
1550 IFINKEY$=CHR$(255)THEN1030
1560 GOTO1500
1570 ' ЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫ
1580 ' ЫЫ CORRIGE COMBINACION INTRODUCIDA ЫЫ
1590 ' ЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫ
1600 GOSUB1880:LINE(0,200)-(256,208),0,BF:EQ=0:FORC=1TOA:IFA$(C)<>B$(C)THENEQ=1:NEXTELSENEXT
1610 IFEQ=0ANDA=20THENGOTO1630'PASA DE FASE
1620 IFEQ=0THENPRESET(16,200):COLOR9:PRINT#1,"­MUY BIEN! AHORA CON UNO MAS":GOSUB1870:FORC=1TO500:NEXT:GOSUB1910:NEXTELSEPRESET(60,200):COLOR9:PRINT#1,"TE HAS EQUIVOCADO":GOSUB1870:FORC=1TO700:NEXT:GOSUB1880:GOTO1740
1630 IFL=5THENGOTO1820ELSEL=L+1:T=ABS(L-6)*100:PRESET(24,200):COLOR9:PRINT#1,"­MUY BIEN! ­PASAS DE FASE!":GOSUB1870:FORC=1TO1000:NEXT:GOSUB1880:LINE(0,200)-(256,208),0,BF:GOTO1410
1640 ' ЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫ
1650 ' ЫЫ RUTINAS DE COLOR DE LAS COMBINACIONES ЫЫ
1660 ' ЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫ
1670 COLOR=(1,7,0,0):A$(R)="1":RETURN
1680 COLOR=(2,0,7,0):A$(R)="2":RETURN
1690 COLOR=(3,0,0,7):A$(R)="3":RETURN
1700 COLOR=(4,7,7,0):A$(R)="4":RETURN
1710 ' ЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫ
1720 ' ЫЫ GAME OVER ЫЫ
1730 ' ЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫ
1740 GOSUB1750:GOTO1760
1750 GOSUB1930:FORC=6TO0STEP-1:FORK=10TO12:COLOR=(K,0,0,C):NEXT:FORO=1TO20:NEXT:NEXT:FORO=2TO0STEP-1:COLOR=(5,O,O,O):FORF=1TO20:NEXT:NEXT:COLOR=(1,0,0,0):RETURN
1760 SCREEN7:COLOR9,0,0:COLOR=(9,0,0,0):IFA>4THENPRESET(72,98):PRINT#1,"HAS ACERTADO COMBINACIONES DE HASTA";A-1;"COLORES."ELSEPRESET(112,98):PRINT#1,"NO HAS ACERTADO NINGUNA COMBINACION.":PRESET(172,114):PRINT#1,"MIRA QUE ERES MALO...":GOTO1810
1770 IFA-1<8THENPRESET(136,114):PRINT#1,"­VENGA! ­PUEDES HACERLO MEJOR!":GOTO1810
1780 IFA-1<12THENPRESET(180,114):PRINT#1,"NO ESTA NADA MAL...":GOTO1810
1790 IFA-1<16THENPRESET(216,114):PRINT#1,"­MUY BIEN!":GOTO1810
1800 IFA-1<20THENPRESET(128,114):PRINT#1,"­FENOMENAL! AHORA, ­A POR TODAS!"
1810 GOSUB1870:FORC=1TO2000:NEXT:GOSUB1880:SCREEN4:COLOR3,0,0:COLOR=(3,0,0,0):FORC=93TO92STEP-1:PRESET(C,92):PRINT#1,"GAME OVER":NEXT:ZZ=1000:GOSUB1860:FORC=1TO200:NEXT:GOTO1030'VUELVE AL MENU
1820 PRESET(56,200):PRINT#1,"­TE LO TERMINASTE!":GOSUB1870:FORC=1TO1000:NEXT:GOSUB1880:GOSUB1750:SCREEN4:GOTO1970'FINAL DEL JUEGO
1830 ' ЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫ
1840 ' ЫЫ SUBRUTINAS ЫЫ
1850 ' ЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫ
1860 FORX=1TO7:COLOR=(3,0,0,X):FORY=1TO20:NEXT:NEXT:FORX=1TOZZ:NEXT:FORX=6TO0STEP-1:COLOR=(3,0,0,X):FORY=1TO20:NEXT:NEXT:CLS:RETURN'APARECEN Y DESAPARECEN LETRAS AZULES
1870 FORX=1TO7:COLOR=(9,0,0,X):FORY=1TO20:NEXT:NEXT:RETURN'APARECEN LETRAS AZULES
1880 FORX=6TO0STEP-1:COLOR=(9,0,0,X):FORY=1TO20:NEXT:NEXT:RETURN'DESAPARECEN LETRAS AZULES
1890 FORX=1TO7:COLOR=(CC,0,0,X):FORY=1TO20:NEXT:NEXT:RETURN'APARECEN LETRAS
1900 FORX=6TO0STEP-1:COLOR=(CC,0,0,X):FORY=1TO20:NEXT:NEXT:RETURN'DESAPARECEN
1910 COLOR=(11,0,0,7):PRESET(69,8):COLOR11:PRINT#1,A+1:RETURN'RENUEVA MARCADOR COLORES
1920 COLOR=(12,0,0,7):PRESET(235,8):COLOR12:PRINT#1,L:RETURN'RENUEVA MARCADOR FASES
1930 PAINT(79,50),1,5:PAINT(87,42),1,5:PAINT(176,50),1,5:PAINT(88,155),1,5:PAINT(127,83),1,5:PAINT(140,96),1,5:PAINT(127,113),1,5:PAINT(114,96),1,5:RETURN'HACE MAS RAPIDO EL FADING FINAL
1940 ' ЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫ
1950 ' ЫЫ SECUENCIA FINAL ЫЫ
1960 ' ЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫ
1970 ZZ=750:COLOR3,0,0:COLOR=(3,0,0,0):FORA=24TO25:PRESET(A,92):PRINT#1,"TE HAS ACABADO EL JUEGO...":NEXT:GOSUB1860:FORA=60TO61:PRESET(A,92):PRINT#1,"TE FELICITAMOS...":NEXT:GOSUB1860
1980 FORA=28TO29:PRESET(A,86):PRINT#1,"MUCHOS SE HABRIAN RENDIDO":NEXT:FORA=36TO37:PRESET(A,98):PRINT#1,"ANTES DE CONSEGUIRLO...":NEXT:GOSUB1860:FORA=60TO61:PRESET(A,86):PRINT#1,"PERO TU CONFIASTE":NEXT
1990 FORA=64TO65:PRESET(A,102):PRINT#1,"EN TU DESTINO...":NEXT:GOSUB1860
2000 FORA=56TO57:PRESET(A,92):PRINT#1,"COMO RECOMPENSA...":NEXT:GOSUB1860:FORA=49TO48STEP-1:PRESET(A,86):PRINT#1,"ILLUMINATUS SOFTWARE":NEXT:FORA=25TO24STEP-1:PRESET(A,102):PRINT#1,"TE PERMITIRA PRESENCIAR...":NEXT:GOSUB1860
2010 FORA=56TO57:PRESET(A,86):PRINT#1,"LA APARICION DE SU":NEXT:FORA=29TO28STEP-1:PRESET(A,98):PRINT#1,"GRAN PIRAMIDE DE PODER...":NEXT:GOSUB1860
2020 FORA=52TO53:PRESET(A,86):PRINT#1,"HECHO DE LOS CUATRO":NEXT:FORA=20TO21:PRESET(A,98):PRINT#1,"COLORES BASICOS DE LA VIDA:":NEXT:GOSUB1860
2030 FORA=12TO13:PRESET(A,86):PRINT#1,"ROJO, VERDE, AZUL Y AMARILLO,":NEXT:FORA=0TO1:PRESET(A,98):PRINT#1,"EL MAS PODEROSO DE LOS CUATRO...":NEXT:GOSUB1860
2040 FORA=21TO20STEP-1:PRESET(A,80):PRINT#1,"EN LA PIRAMIDE, EL AMARILLO":NEXT:FORA=69TO68STEP-1:PRESET(A,92):PRINT#1,"SOLO ES VISIBLE":NEXT:FORA=40TO41:PRESET(A,104):PRINT#1,"PARA LOS ILUMINADOS...":NEXT:GOSUB1860
2050 FORA=53TO52STEP-1:PRESET(A,86):PRINT#1,"SI QUIERES ALCANZAR":NEXT:FORA=60TO61:PRESET(A,98):PRINT#1,"LA ILUMINACION...":NEXT:GOSUB1860
2060 FORA=60TO61:PRESET(A,86):PRINT#1,"PONTE EN CONTACTO":NEXT:FORA=68TO69:PRESET(A,98):PRINT#1,"CON NOSOTROS...":NEXT:GOSUB1860
2070 FORA=13TO12STEP-1:PRESET(A,92):PRINT#1,"SOLO ASI PODRAS ILUMINARTE...":NEXT:GOSUB1860:FORA=60TO61:PRESET(A,86):PRINT#1,"NUESTRA DIRECCION":NEXT:FORA=60TO61:PRESET(A,98):PRINT#1,"ES EL UNIVERSO...":NEXT:GOSUB1860
2080 FORA=48TO49:PRESET(A,92):PRINT#1,"TE ESPERAMOS ALLI...":NEXT:GOSUB1860
2090 ' ЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫ
2100 ' ЫЫ PIRAMIDE DE PODER ЫЫ
2110 ' ЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫ
2120 COLOR4,0,0:SCREEN7:CLEAR:OPEN"GRP:"AS#1:FORA=1TO15:COLOR=(A,0,0,0):NEXT
2130 FORA=0TO512STEP8:LINE(256,0)-(A,212),1:NEXT:FORA=256TO0STEP-6:LINE(512,212)-(A,B),2:B=B+5:NEXT:FORA=256TO512STEP6:LINE(0,212)-(A,C),2:C=C+5:NEXT
2140 FORA=256TO0STEP-6:LINE(256,212)-(A,D),3:D=D+5:NEXT:FORA=256TO512STEP6:LINE(256,212)-(A,E),3:E=E+5:NEXT
2150 FORA=1TO7:COLOR=(1,0,0,A):COLOR=(2,A,0,0):COLOR=(3,0,A,0):FORB=1TO40:NEXT:NEXT
2160 ' ЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫ
2170 ' ЫЫ EXPLICACION DE PIRAMIDE ЫЫ
2180 ' ЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫЫ
2190 CC=4:PRESET(5,0):PRINT#1,"EL AZUL REPRESENTA AL CIELO,":PRESET(5,12):PRINT#1,"INALCANZABLE, Y TAMBIEN":PRESET(5,24):PRINT#1,"AL ESTADO METAFISICO":PRESET(5,36):PRINT#1,"DE LA ILUMINACION...":GOSUB1890:FORA=1TO5000:NEXT:GOSUB1900
2200 PRESET(5,0):COLOR5:PRINT#1,"EL ROJO REPRESENTA AL FUEGO,":PRESET(5,12):PRINT#1,"A LAS EMOCIONES HUMANAS,":PRESET(5,24):PRINT#1,"A LA MORTALIDAD DEL HOMBRE":PRESET(5,36):PRINT#1,"Y A LA MADRE TIERRA,":PRESET(5,48):PRINT#1,"FOGOSA E INCONTROLABLE."
2210 FORX=1TO7:COLOR=(5,X,0,0):FORY=1TO20:NEXT:NEXT:FORA=1TO5000:NEXT:FORX=6TO0STEP-1:COLOR=(5,X,0,0):FORY=1TO20:NEXT:NEXT
2220 PRESET(5,0):COLOR6:PRINT#1,"EL VERDE REPRESENTA A LA":PRESET(5,12):PRINT#1,"HUMANIDAD, MOVIENDOSE ENTRE":PRESET(5,24):PRINT#1,"LOS DEMAS ELEMENTOS DE LA":PRESET(5,36):PRINT#1,"VIDA, A LA VEZ EN UNION":PRESET(5,48):PRINT#1,"Y DESUNION CON ELLOS..."
2230 FORX=1TO7:COLOR=(6,0,X,0):FORY=1TO20:NEXT:NEXT:FORA=1TO5000:NEXT:FORX=6TO0STEP-1:COLOR=(6,0,X,0):FORY=1TO20:NEXT:NEXT
2240 PRESET(5,0):COLOR7:PRINT#1,"EL AMARILLO ES LA UNION ENTRE":PRESET(5,12):PRINT#1,"EL ROJO Y EL VERDE, ENTRE LA":PRESET(5,24):PRINT#1,"HUMANIDAD Y SU ENTORNO":PRESET(5,36):PRINT#1,"FISICO Y ESOTERICO.":PRESET(5,48):PRINT#1,"PARA LOS NO ILUMINADOS"
2250 PRESET(5,60):PRINT#1,"ES INVISIBLE, DE AHI":PRESET(5,72):PRINT#1,"QUE LA VIDA CAREZCA":PRESET(5,84):PRINT#1,"DE SENTIDO PARA":PRESET(5,96):PRINT#1,"ELLOS..."
2260 FORX=1TO7:COLOR=(7,X,X,0):FORY=1TO20:NEXT:NEXT:FORA=1TO5000:NEXT:FORX=6TO0STEP-1:COLOR=(7,X,X,0):FORY=1TO20:NEXT:NEXT
2270 FORA=1TO1000:NEXT:FORA=6TO0STEP-1:COLOR=(1,0,0,A):COLOR=(2,A,0,0):COLOR=(3,0,A,0):FORB=1TO40:NEXT:NEXT
2280 SCREEN4:COLOR3,0,0:COLOR=(3,0,0,0):FORA=13TO12STEP-1:PRESET(A,92):PRINT#1,"(C) ILLUMINATUS SOFTWARE 1991":NEXT:ZZ=2000:GOSUB1860:FORA=1TO500:NEXT:GOTO1030'VUELTA AL MENU
