10 ' ллллллллллллллллллллллллллллллллллллл
20 ' лл COLOCACION ALEATORIA DE PIEDRAS лл
30 ' ллллллллллллллллллллллллллллллллллллл
40 DEFINT P,V,M
50 DIM P(10,10),V(10,10),M(8,87)
60 _TURBO ON (M(),V())
70 R=50000!:FOR I=1 TO 9:FOR N=1 TO 9:V(I,N)=PEEK(R):R=R+1:NEXT N,I:FOR I=1 TO 8:FOR N=1 TO 43:M(I,N+44)=PEEK(R):R=R+1:NEXT N,I:M(0,44)=PEEK(R):V(0,0)=PEEK(R+1)
80 _TURBO OFF
90 SET ADJUST(5,4)
100 GOSUB 110:GOTO 180
110 FOR I=1 TO 9:FOR N=1 TO 9
120 P(I,N)=INT(RND(-TIME)*8+1)
130 NEXT N,I
140 M=1:V(10,10)=0:M(0,0)=1:RETURN
150 ' лллллллллллллллллллллл
160 ' лл DIBUJAR PANTALLA лл
170 ' лллллллллллллллллллллл
180 SCREEN 8:SET PAGE 0,0:COLOR 255,0,0:CLS
190 _TURBO ON (P(),V(),M(),M)
200 IF V(10,10)=0 THEN 210 ELSE IF V(5,10)=0 THEN 440 ELSE GOSUB 810:GOTO 440
210 A$="":DEFUSR=&H41:A=USR(0)
220 FOR I=0 TO 5:M=I MOD 2
230 FOR N=0 TO 8 STEP 2
240 COPY(162,0)-(194,32),1 TO (N*32+M*32,I*32+9),0
250 COPY(128,0)-(160,32),1 TO (N*32-M*32+32,I*32+9),0
260 NEXT N,I
270 FOR I=0 TO 1
280 FOR N=0 TO 8 STEP 2
290 COPY(128,I*24)-(160,I*24+8),1 TO (N*32+I*32,I*202),0
300 COPY(162,I*24)-(194,I*24+8),1 TO (N*32+32-I*32,I*202),0
310 NEXT N,I
320 DEFUSR=&H44:A=USR(0)
330 FOR Q=36 TO 221 STEP 23:X=X+1:Y=3
340 FOR W=82 TO 128 STEP 23:Y=Y+1
350 CIRCLE(Q,W),10,0:PAINT(Q,W),0
360 GOSUB 680
370 NEXT W,Q
380 FOR I=0 TO 138 STEP 138:S=6*I/138:X=3
390 FOR Q=105 TO 151 STEP 23:X=X+1:Y=0:J=0
400 FOR W=13+I TO 59+I STEP 23:Y=J+S+1:J=J+1
410 CIRCLE(Q,W),10,0:PAINT(Q,W),0
420 GOSUB 680
430 NEXT W,Q,I
440 G=V(0,0):IF V(10,10)=0 THEN P(5,5)=0:X=5:Y=5:X1=5:Y1=5:V(10,10)=1:V=0:P=0:E=0:CIRCLE(128,105),10,0:PAINT(128,105),0 ELSE X=V(1,0):Y=V(0,1):X1=V(9,10):Y1=V(10,9):XX=V(8,10):YY=V(10,8)
450 GOSUB 1200
460 ' лл BUCLE DE MOVIMIENTO лл
470 S=STICK(0) OR STICK(1)
480 ST=STRIG(0) OR STRIG(1)
490 A$=INKEY$:M=VAL(A$):IF A$=CHR$(27) THEN GOSUB 920:M=25:GOTO 1230
500 IF M>0 AND M<7 THEN GOSUB 1180:GOSUB 1210:E=0:XX=0:YY=0:GOTO 1260
510 IF S=1 THEN Y=Y1-1:P=1
520 IF S=3 THEN X=X1+1:P=1
530 IF S=5 THEN Y=Y1+1:P=1
540 IF S=7 THEN X=X1-1:P=1
550 IF X<1 THEN X=1:P=0
560 IF X>9 THEN X=9:P=0
570 IF Y<1 THEN Y=1:P=0
580 IF Y>9 THEN Y=9:P=0
590 IF (Y<4 OR Y>6) AND (Y1<4 OR Y1>6) AND X<4 THEN X=4:P=0
600 IF (Y<4 OR Y>6) AND (Y1<4 OR Y1>6) AND X>6 THEN X=6:P=0
610 IF (X<4 OR X>6) AND Y<4 THEN Y=4:P=0
620 IF (X<4 OR X>6) AND Y>6 THEN Y=6:P=0
630 IF S<>0 AND P=1 THEN GOSUB 1100:IF X1=XX AND Y1=YY THEN GOSUB 1190:GOSUB 1200 ELSE GOSUB 1200
640 X1=X:Y1=Y:P=0
650 IF ST=-1 THEN GOSUB 1000
660 GOTO 470
670 ' лл COLOCAR PIEDRAS лл
680 L=P(X,Y)
690 IF P(X,Y)=0 THEN L=9
700 ON L GOSUB 710,720,730,740,750,760,770,780,790:RETURN
710 COPY(0,0)-(14,14),1 TO (Q-7,W-7),0:RETURN ' yellow stones
720 COPY(16,0)-(30,14),1 TO (Q-7,W-7),0:RETURN ' rubies
730 COPY(32,0)-(46,14),1 TO (Q-7,W-7),0:RETURN ' steel balls
740 COPY(48,0)-(62,14),1 TO (Q-7,W-7),0:RETURN ' orange stones
750 COPY(64,0)-(78,14),1 TO (Q-7,W-7),0:RETURN ' emeralds
760 COPY(80,0)-(94,14),1 TO (Q-7,W-7),0:RETURN ' purple stones
770 COPY(96,0)-(110,14),1 TO (Q-7,W-7),0:RETURN ' blue stones
780 COPY(112,0)-(126,14),1 TO (Q-7,W-7),0:RETURN ' sapphires
790 FOR N=9 TO 10:CIRCLE(Q,W),N,0:NEXT N:PAINT(Q,W),0:RETURN
800 ' лл RECOLOCAR PIEDRAS лл
810 FOR Q=36 TO 221 STEP 23:X=X+1:Y=3
820 FOR W=82 TO 128 STEP 23:Y=Y+1
830 GOSUB 680
840 NEXT W,Q
850 FOR I=0 TO 138 STEP 138:S=6*I/138:X=3
860 FOR Q=105 TO 151 STEP 23:X=X+1:Y=0:J=0
870 FOR W=13+I TO 59+I STEP 23:Y=J+S+1:J=J+1
880 GOSUB 680
890 NEXT W,Q,I
900 RETURN
910 ' лл CONTAR PIEDRAS лл
920 V=0:FOR I=1 TO 9:FOR N=1 TO 9
930 IF P(I,N)>0 THEN V=V+1
940 NEXT N,I
950 V(2,10)=V-36
960 RETURN
970 ' лл CONTADOR DE MOVIMIENTOS лл
980 J=M(0,0):M(1,J)=Q:M(2,J)=W:M(3,J)=P(Q,W):M(4,J)=XX:M(5,J)=YY:M(6,J)=P(XX,YY):M(7,J)=X:M(8,J)=Y:M(0,0)=J+1:RETURN
990 ' лл RUTINA DE SALTO лл
1000 IF E=1 THEN 1020
1010 IF P(X,Y)<>0 THEN XX=X:YY=Y:E=1:GOSUB 1190:RETURN ELSE E=0:RETURN
1020 IF (ABS(YY-Y)=2 AND XX=X) THEN Z=0:Q=X:W=(YY+Y)/2 ELSE IF (ABS(XX-X)=2 AND YY=Y) THEN Z=0:Q=(XX+X)/2:W=Y ELSE Z=1
1030 IF XX=X AND YY=Y THEN RETURN
1040 IF P(X,Y)<>0 OR P(Q,W)=0 THEN E=0:GOSUB 1180:XX=0:YY=0:RETURN
1050 IF Z=1 THEN E=0:GOSUB 1180:XX=0:YY=0:RETURN
1060 IF V(2,10)=5 THEN V(2,10)=0:FOR I=M(0,0)+1 TO 43:FOR N=1 TO 8:M(N,I)=0:NEXT N,I
1070 GOSUB 980:P(X,Y)=P(XX,YY):P(XX,YY)=0:P(Q,W)=0:GOSUB 1180:GOSUB 1110:XX=Q:YY=W:GOSUB 1180:GOSUB 1110:GOSUB 1210:Q=X*23+13:W=Y*23-10:GOSUB 680:GOSUB 1200
1080 E=0:XX=0:YY=0:RETURN
1090 ' лл BORRAR UNA PIEDRA лл
1100 IF E=1 AND X1=XX AND Y1=YY THEN RETURN ELSE GOSUB 1210:RETURN
1110 FOR I=1 TO 15 STEP 2:LINE(XX*23+6,YY*23-18+I)-(XX*23+20,YY*23-18+I),0:NEXT I
1120 'FOR I=1 TO 15 STEP 2:LINE(XX*23+5+I,YY*23-17)-(XX*23+5+I,YY*23-3),0:NEXT I
1130 FOR I=2 TO 14 STEP 2:LINE(XX*23+6,YY*23-18+I)-(XX*23+20,YY*23-18+I),0:NEXT I
1140 'FOR I=2 TO 14 STEP 2:LINE(XX*23+5+I,YY*23-17)-(XX*23+5+I,YY*23-3),0:NEXT I
1150 PAINT(XX*23+13,YY*23-10),0
1160 RETURN
1170 ' лл TRATAMIENTO DE MARCAS лл
1180 FOR I=0 TO 1:CIRCLE(XX*23+13,YY*23-10),9+I,0:NEXT I:RETURN 'BORRAR MARCA
1190 FOR I=0 TO 1:CIRCLE(XX*23+13,YY*23-10),10-I,28:NEXT I:RETURN ' MARCA ROJA
1200 FOR I=0 TO 1:CIRCLE(X*23+13,Y*23-10),10-I,233:NEXT I:RETURN ' MARCA VERDE
1210 FOR I=0 TO 1:CIRCLE(X1*23+13,Y1*23-10),9+I,0:NEXT I:RETURN 'BORRAR MARCA
1220 ' лл VER SI SE HA GANADO лл
1230 FOR I=0 TO 255 STEP 2
1240 LINE(I,0)-(I,211),0:LINE(255-I,0)-(255-I,211),0:LINE(0,I)-(255,I),0:LINE(0,211-I)-(255,211-I),0
1250 FOR N=1 TO 25:NEXT N,I
1260 V(1,0)=X:V(0,1)=Y:V(9,10)=X1:V(10,9)=Y1:V(8,10)=XX:V(10,8)=YY
1270 _TURBO OFF
1280 IF M=25 THEN 1350
1290 O=94:P=80:CLOSE:OPEN"GRP:"AS1:COPY(O,P)-(68+O,50+P),0 TO (180,150),1:LINE(O,P)-(68+O,50+P),0,BF:LINE(O,P)-(68+O,50+P),255,B:LINE(O,12+P)-(68+O,12+P),255:LINE(O,24+P)-(68+O,24+P),255:PRESET(3+O,3+P):G=V(0,0)
1300 ON ERROR GOTO 1480:ON M GOSUB 1750,1780,1800,1810,1820,1840:ON ERROR GOTO 0
1310 COPY(180,150)-(248,200),1 TO (O,P),0:M=1:V=1:V(5,10)=C:GOTO 190
1320 ' ллллллллллл
1330 ' лл FINAL лл
1340 ' ллллллллллл
1350 IF V(2,10)=1 AND P(5,5)>0 THEN 1370
1360 SCREEN 4:CLOSE:OPEN"GRP:"AS1:COLOR 4,0,0:COLOR=(4,0,0,0):W=4:RESTORE 1710:GOTO 1380
1370 SCREEN 4:CLOSE:OPEN"GRP:"AS1:COLOR 4,0,0:COLOR=(4,0,0,0):VV=1:W=10:RESTORE 1700
1380 FOR I=1 TO W:READ A$:A=(256-(LEN(A$)*8))/2:CLS:GOSUB 1640:GOSUB 1650:FOR N=1 TO 400:NEXT N
1390 IF I=10 THEN 1440
1400 GOSUB 1660
1410 IF INKEY$<>"" THEN 1440
1420 NEXT I
1430 IF VV=1 THEN FOR I=1 TO 7:COLOR=(4,I,0,7-I):FOR N=1 TO 18:NEXT N,I:FOR I=1 TO 7:COLOR=(4,7-I,0,I):FOR N=1 TO 18:NEXT N,I:FOR N=1 TO 500:NEXT:GOSUB 1660
1440 GOTO 2270
1450 ' лллллллллллллллллллллллллллллллллллл
1460 ' лл CORRECCION DE ERRORES DE DISCO лл
1470 ' лллллллллллллллллллллллллллллллллллл
1480 IF ERR=70 THEN A$="NO HAY":B$="DISCO"
1490 IF ERR=69 THEN A$="ERROR DE":B$="DISCO"
1500 IF ERR=68 THEN A$="DISCO":B$="PROTEG."
1510 IF ERR=67 THEN A$="DISCO":B$="LLENO"
1520 IF ERR=66 THEN A$="DISCO":B$="LLENO"
1530 IF ERR=62 THEN A$="MAL":B$="NOMBRE"
1540 IF ERR=56 THEN A$="MAL":B$="NOMBRE"
1550 IF ERR=53 THEN A$="FICHERO":B$="NO ESTA"
1560 LINE(1+O,27+P)-(63+O,49+P),0,BF
1570 K=O+(8-LEN(A$))*4:PRESET(3+K,27+P):PRINT#1,A$
1580 K=O+(8-LEN(B$))*4:PRESET(3+K,38+P):PRINT#1,B$
1590 C=0:FOR I=1 TO 1000:NEXT I
1600 RESUME 1770
1610 ' ллллллллллллллллллллллллллллллл
1620 ' лл ESCRIBIR MENSAJES FINALES лл
1630 ' ллллллллллллллллллллллллллллллл
1640 FOR N=A+1 TO A STEP -1:PRESET(N,92):IF A$<>"*" THEN PRINT #1,A$:NEXT N:RETURN ELSE FOR N=25 TO 24 STEP -1:PRESET(N,92):PRINT #1,"TE HAN FALTADO";V(2,10);"PIEDRAS":NEXT N:RETURN
1650 FOR N=1 TO 7:COLOR=(4,0,0,N):FOR T=1 TO 20:NEXT T,N:RETURN
1660 FOR N=6 TO 0 STEP -1:COLOR=(4,0,0,N):FOR T=1 TO 20:NEXT T,N:RETURN
1670 ' ллллллллллллллллллллллллллллллл
1680 ' лл DATOS DE MENSAJES FINALES лл
1690 ' ллллллллллллллллллллллллллллллл
1700 DATA ­FELICIDADES!,­LO HAS CONSEGUIDO!,"PARECIA IMPOSIBLE, ЈVERDAD?","TE HAS GANADO UN PREMIO:",UN VIAJE GRATIS AL JAPON,"DE IDA, PERO NO DE VUELTA...",GAME OVER,GRACIAS A SEGA POR LOS GRAFICOS,"PROGRAMACION: ALTAIR",(c) ILLUMINATUS SOFTWARE 1993
1710 DATA"HAS PERDIDO, COLEGA",*,PERO TRANQUILO,SIEMPRE HAY OTRA OPORTUNIDAD...
1720 ' лллллллллллллл
1730 ' лл OPCIONES лл
1740 ' лллллллллллллл
1750 PRINT#1," CARGAR":PRESET(15+O,15+P):PRINT#1,"DISCO":C=4:GOSUB 1980:IF A=1 THEN PRESET(3+O,27+P):PRINT#1,"NOMBRE:":LINE(O,24+P)-(64+O,24+P),255:LINE(1+O,35+P)-(63+O,49+P),0,BF:N=0:N$="":GOSUB 1890 ELSE C=0:RETURN
1760 C=1:BLOAD N$+".SVG":GOSUB 2160
1770 RETURN
1780 PRINT#1," GRABAR":PRESET(15+O,15+P):PRINT#1,"DISCO":C=4:GOSUB 1980:IF A=1 THEN PRESET(3+O,27+P):PRINT#1,"NOMBRE:":LINE(O,24+P)-(64+O,24+P),255:LINE(1+O,35+P)-(63+O,49+P),0,BF:N=0:N$="":GOSUB 1890 ELSE C=0:RETURN
1790 GOSUB 2200:C=0:BSAVE N$+".SVG",50000!,50425!:RETURN
1800 PRINT#1," CARGAR":C=1:PRESET(7+O,15+P):PRINT#1,"MEMORIA":LINE(O,24+P)-(64+O,24+P),255:IF G=1 THEN GOSUB 1980:RETURN ELSE PRESET(3+O,28+P):PRINT#1,"   NO":PRESET(3+O,38+P):PRINT#1," PUEDES":FOR I=1 TO 700:NEXT I:C=0:RETURN
1810 PRINT#1," GRABAR":C=0:PRESET(7+O,15+P):PRINT#1,"MEMORIA":LINE(O,24+P)-(64+O,24+P),255:N=0:N$="":GOSUB 1980:C=0:RETURN
1820 PRINT#1,"RETRASAR":PRESET(11+O,15+P):PRINT#1,"JUGADA":LINE(O,24+P)-(64+O,24+P),255:C=2:IF M(0,0)<>1 THEN GOSUB 1980:V(2,10)=5 ELSE PRESET(3+O,28+P):PRINT#1,"   NO":PRESET(3+O,38+P):PRINT#1," PUEDES":FOR I=1 TO 700:NEXT I:C=0
1830 RETURN
1840 PRESET(7+O,3+P):PRINT#1,"AVANZAR":PRESET(11+O,15+P):PRINT#1,"JUGADA":LINE(O,24+P)-(64+O,24+P),255:C=3:IF M(1,M(0,0))<>0 THEN GOSUB 1980 ELSE PRESET(3+O,28+P):PRINT#1,"   NO":PRESET(3+O,38+P):PRINT#1," PUEDES":FOR I=1 TO 700:NEXT I:C=0
1850 RETURN
1860 ' лллллллллллллллллллллллллллллллллллллллллллллл
1870 ' лл RUTINA PARA INTRODUCIR NOMBRE DE FICHERO лл
1880 ' лллллллллллллллллллллллллллллллллллллллллллллл
1890 A$=INPUT$(1):R=ASC(A$)
1900 IF R=13 THEN RETURN
1910 IF R=8 AND N>0 THEN N=N-1:LINE(N*8+3+O,38+P)-(N*8+11+O,46+P),0,BF:N$=LEFT$(N$,N):GOTO 1890
1920 IF ((R<65 OR R>122) AND (R<48 OR R>57)) OR N=8 THEN GOTO 1890
1930 PRESET(N*8+3+O,38+P):PRINT#1,A$:N=N+1:N$=N$+A$
1940 GOTO 1890
1950 ' лллллллллллллллллл
1960 ' лл RUTINA SI/NO лл
1970 ' лллллллллллллллллл
1980 PRESET(5+O,29+P):PRINT#1,"1-SI":PRESET(5+O,39+P):PRINT#1,"2-NO"
1990 A$=INPUT$(1):A=VAL(A$):IF A<1 OR A>2 THEN GOTO 1990
2000 IF A=1 AND C=0 THEN V(0,0)=1:GOSUB 2080:RETURN
2010 IF A=1 AND C=1 THEN GOSUB 2120:RETURN
2020 IF A=1 AND C=2 THEN J=M(0,0)-1:P(M(1,J),M(2,J))=M(3,J):P(M(4,J),M(5,J))=M(6,J):P(M(7,J),M(8,J))=0:C=1:M(0,0)=J:RETURN
2030 IF A=1 AND C=3 THEN J=M(0,0):P(M(1,J),M(2,J))=0:P(M(4,J),M(5,J))=0:P(M(7,J),M(8,J))=M(6,J):C=1:M(0,0)=J+1:RETURN
2040 IF A=2 OR C=4 THEN C=0:RETURN
2050 ' лллллллллллллллллллллллл
2060 ' лл RUTINAS ACELERADAS лл
2070 ' лллллллллллллллллллллллл
2080 _TURBO ON (P(),V(),M(),M)
2090 FOR I=1 TO 9:FOR N=1 TO 9:V(I,N)=P(I,N):NEXT N,I:FOR I=1 TO 8:FOR N=1 TO 43:M(I,N+44)=M(I,N):NEXT N,I:M(0,44)=M(0,0)
2100 _TURBO OFF
2110 RETURN
2120 _TURBO ON (P(),V(),M(),M)
2130 FOR I=1 TO 9:FOR N=1 TO 9:P(I,N)=V(I,N):NEXT N,I:FOR I=1 TO 8:FOR N=1 TO 43:M(I,N)=M(I,N+44):NEXT N,I:M(0,0)=M(0,44)
2140 _TURBO OFF
2150 RETURN
2160 _TURBO ON (P(),V(),M(),M)
2170 R=50000!:FOR I=1 TO 9:FOR N=1 TO 9:P(I,N)=PEEK(R):R=R+1:NEXT N,I:FOR I=1 TO 8:FOR N=1 TO 43:M(I,N)=PEEK(R):R=R+1:NEXT N,I:M(0,0)=PEEK(R)
2180 _TURBO OFF
2190 RETURN
2200 _TURBO ON (P(),V(),M(),M)
2210 R=50000!:FOR I=1 TO 9:FOR N=1 TO 9:POKE R,P(I,N):R=R+1:NEXT N,I:FOR I=1 TO 8:FOR N=1 TO 43:POKE R,M(I,N):R=R+1:NEXT N,I:POKE R,M(0,0)
2220 _TURBO OFF
2230 RETURN
2240 ' лллллллллллллллллллллллллллл
2250 ' лл VOLVER AL MENU INICIAL лл
2260 ' лллллллллллллллллллллллллллл
2270 _TURBO ON (V(),M())
2280 R=50000!:FOR I=1 TO 9:FOR N=1 TO 9:POKE R,V(I,N):R=R+1:NEXT N,I:FOR I=1 TO 8:FOR N=1 TO 43:POKE R,M(I,N+44):R=R+1:NEXT N,I:POKE R,M(0,44):POKE R+1,V(0,0)
2290 _TURBO OFF
2300 RUN"SOL2.LDR"
