2 ' лллллллллллл
3 ' лл INICIO лл
4 ' лллллллллллл
10 OPEN"GRP:"AS#1:IF PEEK(&HF346)=1 THEN GOSUB 11:COLOR=(9,0,0,0):GOTO 20 ELSE 12
11 GOSUB 19150:COLOR 15,14:PRESET(24,6):PRINT#1,"MSX RISK      (c) 1994 ILLUMINATUS SOFTWARE (MAX FEINGOLD)":RETURN
12 D=4:FOR I=1 TO 7
14 IF I>4 THEN D=I
16 COLOR=(1,7,7,I):COLOR=(2,7,I,I):COLOR=(3,I,I,7):COLOR=(4,D,I,I):COLOR=(5,I,7,I):COLOR=(6,7,I,7):NEXT:COLOR=(13,7,7,7)
20 FOR I=1 TO 500:NEXT:COPY(155,88)-(357,124),0 TO (285,0),1
21 COLOR 15,14:LINE(155,89)-(357,99),7,BF:LINE(155,101)-(357,111),8,BF:LINE(155,113)-(357,123),9,BF:LINE(155,88)-(357,124),15,B:LINE(155,100)-(357,112),15,B
22 COLOR=(7,0,0,7):COLOR=(8,5,0,0):COLOR,7:PRESET(208,91):PRINT#1,"ELIGE OPCION":COLOR,8:PRESET(172,103):PRINT#1,"EMPEZAR PARTIDA NUEVA":COLOR,9:PRESET(168,115):PRINT#1,"CARGAR PARTIDA ANTIGUA"
23 U=5:D=1:A=2:GOSUB 65500
24 COPY(285,0)-(487,36),1 TO (155,88),0
25 IF C=2 THEN ON ERROR GOTO 10998:O=0:K=1000:EL=3:POKE&HF346,1:FOR N=7 TO 12:COLOR=(N,0,0,0):NEXT:SET PAGE 0,1:BLOAD"RISK.BIN":DEFUSR=&HC001:PRINTUSR("RISK2.SCR"):SET PAGE 0,0:RUN"RISK.PRG"
34 ' лллллллллллллллл
35 ' лл LEER DATOS лл
36 ' лллллллллллллллл
37 IF VZ=0 THEN DEFINT A-L,N-Z:DIMC$(42),CO(42),CC(42),CX(42,3),CY(42,3),CB(42),CA(42),T(6),PL$(6),PC(6,3),B(10),CL(3),CP(3),PY$(6),PR(6,3),R(6),O(6),TP(6),AR(6):VZ=1 ELSE 300
38 RESTORE 10010:FOR I=1 TO 42:READ C$(I),CO(I),CC(I):FOR N=1 TO CC(I):READ CX(I,N),CY(I,N):NEXT N,I
40 RESTORE 10460:S1$="":S2$="":S3$="":FOR I=1 TO 8:READ A$,B$,C$:S1$=S1$+CHR$(VAL("&B"+A$)):S2$=S2$+CHR$(VAL("&B"+B$)):S3$=S3$+CHR$(VAL("&B"+C$)):NEXT:SPRITE$(0)=S1$:SPRITE$(1)=S2$:SPRITE$(2)=S3$
45 IF PEEK(&HF346)=0 THEN FOR I=1 TO 42:FOR N=1 TO CC(I):PAINT(CX(I,N),CY(I,N)),13,0:NEXT N,I
50 GOTO 300
77 ' лллллллллллллллллл
78 ' лл MOVER FLECHA лл
79 ' лллллллллллллллллл
90 S=STICK(0) OR STICK(1) OR STICK(2):ST=STRIG(0) OR STRIG(1) OR STRIG(2)
95 XX=511+(2*X-503)
100 IF S=1 THEN Y=Y-3
110 IF S=2 THEN X=X+3:Y=Y-3
120 IF S=3 THEN X=X+3
130 IF S=4 THEN X=X+3:Y=Y+3
140 IF S=5 THEN Y=Y+3
150 IF S=6 THEN X=X-3:Y=Y+3
160 IF S=7 THEN X=X-3
170 IF S=8 THEN X=X-3:Y=Y-3
230 IF X<0 THEN X=247
240 IF X>247 THEN X=0
250 IF Y<18 THEN Y=18
260 IF Y>203 THEN Y=203
270 PUT SPRITE 0,(X,Y),15,0:IF ST=-1 THEN RETURN ELSE 90
297 ' ллллллллллллллллллллллллл
298 ' лл NUMERO DE JUGADORES лл
299 ' ллллллллллллллллллллллллл
300 COPY(199,64)-(313,148),0 TO (285,0),1
305 COLOR=(6,0,0,7):COPY(0,0)-(114,84),1 TO (199,64),0
310 A=5:COLOR=(8,5,0,0):AL=0:U=5:D=1:GOSUB 65500:PL=C+1:COPY(285,0)-(399,84),1 TO (199,64),0
311 COLOR=(6,0,0,0):FOR I=7 TO 12:COLOR=(I,0,0,0):NEXT:I=0
312 GOSUB 16000
314 ' лллллллллллллллллллллллл
315 ' лл INTRODUCIR NOMBRES лл
316 ' лллллллллллллллллллллллл
319 I=I+1
320 COPY(171,78)-(341,134),0 TO (285,0),1
330 COLOR=(7,0,0,7):SETPAGE 0,1:COLOR,7:PRESET(221,4):PRINT#1,MID$(STR$(I),2):SETPAGE0,0:COPY(114,0)-(284,56),1 TO (171,78),0
340 COLOR=(8,5,0,0):N$="":U=3:D=7:A=2:AL=0:C=1:B=1:FOR N=1 TO 10:B(N)=32:NEXT
341 GOSUB 65501:IF C=2 AND N$="" THEN 341
342 IF C=2 THEN PL$(I)=N$:GOTO 400
344 FOR N=1 TO 125:NEXT:C=1:H=B(1)
345 PRESET(196+C*10,110):COLOR,14:PRINT#1,CHR$(H):FOR N=0 TO 1:PUT SPRITE N+1,(INT((188+C*10)/2),107+N*8),15,N+1:NEXT
346 S=STICK(0) OR STICK(1) OR STICK(2):ST=STRIG(0) OR STRIG(1) OR STRIG(2)
347 FOR N=0 TO 1:PUT SPRITE N+1,(INT((188+C*10)/2),107+N*8),15,N+1:NEXT
348 IF S=1 THEN H=H+1:GOSUB 356
349 IF S=3 THEN C=C+1:IF C=11 THEN C=10 ELSE B(C-1)=H:H=B(C):FOR N=1 TO 50:NEXT
350 IF S=5 THEN H=H-1:GOSUB 356
351 IF S=7 THEN C=C-1:IF C=0 THEN C=1 ELSE B(C+1)=H:H=B(C):FOR N=1 TO 50:NEXT
352 IF ST=-1 THEN FOR N=0 TO 1:PUT SPRITE N+1,(511,211),0,N+1:NEXT:N$="":FOR N=1 TO 10:N$=N$+CHR$(B(N)):NEXT:GOSUB 953:B=1:C=1:GOTO 341
353 GOTO 346
356 IF H=64 THEN H=32
357 IF H=91 THEN H=97
358 IF H=96 THEN H=90
359 IF H=123 THEN H=32
362 IF H=33 THEN H=65
363 IF H=31 THEN H=122
364 B(C)=H:PRESET(196+C*10,110):COLOR,14:PRINT#1,CHR$(H):RETURN
394 ' лллллллллллллллллллллллл
395 ' лл INTRODUCIR COLORES лл
396 ' лллллллллллллллллллллллл
400 GOSUB 19000:IF RR>0 THEN COPY(285,0)-(455,56),1 TO (171,78),0:COLOR 15,14:A$="EL JUGADOR"+STR$(RR)+" TIENE EL MISMO NOMBRE":O=0:K=2000:GOSUB 13070:COLOR=(9,0,0,0):GOTO 320
401 COPY(285,0)-(455,56),1 TO (171,78),0
402 FOR N=8 TO 12:COLOR=(N,0,0,0):NEXT
405 COPY(183,41)-(329,171),0 TO (285,0),1:COLOR=(7,0,0,7):COLOR=(8,5,0,0)
410 SET PAGE 0,1:PRESET(221,60):COLOR,7:PRINT#1,MID$(STR$(I),2):SET PAGE 0,0:COPY(114,56)-(260,186),1 TO (183,41),0
420 FOR N=1 TO 3:CL(N)=I+N
421 IF CL(N)>6 THEN CL(N)=CL(N)-6
423 NEXT:COLOR=(CL(1),7,0,0):COLOR=(CL(2),0,7,0):COLOR=(CL(3),0,0,7):COLOR=(I,0,0,0):PAINT(195,148),I,15:FOR N=1 TO 3:CP(N)=0:NEXT
424 LINE(190,128)-(221,128),CL(1),BF
425 LINE(240,128)-(271,128),CL(2),BF
426 LINE(291,128)-(322,128),CL(3),BF
430 FOR N=1 TO 75:NEXT:A=2:AL=0:U=3:D=7:GOSUB 65500:IF C=2 THEN 480 ELSE COLOR=(10,5,0,0):C=1:B=1
440 FOR N=1 TO 75:NEXT:A=3:AL=-2:U=3:D=7:GOSUB 65501:IF ST=-1 THEN FORN=10TO12:COLOR=(N,0,0,0):NEXT:GOTO 430
450 IF CP(C)=8 THEN CP(C)=7:GOTO 440
460 IF CP(C)=-1 THEN CP(C)=0:GOTO 440
470 ON C GOSUB 471,474,477:GOTO 440
471 IF AU=1 THEN LINE(190,135-CP(C)*8)-(221,127-CP(C)*8),CL(1),BF:COLOR=(I,CP(1),CP(2),CP(3)):FOR N=1 TO 3:PC(I,N)=CP(N):NEXT
472 IF AU=-1 THEN LINE(190,127-CP(C)*8)-(221,119-CP(C)*8),14,BF:COLOR=(I,CP(1),CP(2),CP(3)):FOR N=1 TO 3:PC(I,N)=CP(N):NEXT
473 RETURN
474 IF AU=1 THEN LINE(240,135-CP(C)*8)-(271,127-CP(C)*8),CL(2),BF:COLOR=(I,CP(1),CP(2),CP(3)):FOR N=1 TO 3:PC(I,N)=CP(N):NEXT
475 IF AU=-1 THEN LINE(240,127-CP(C)*8)-(271,119-CP(C)*8),14,BF:COLOR=(I,CP(1),CP(2),CP(3)):FOR N=1 TO 3:PC(I,N)=CP(N):NEXT
476 RETURN
477 IF AU=1 THEN LINE(291,135-CP(C)*8)-(322,127-CP(C)*8),CL(3),BF:COLOR=(I,CP(1),CP(2),CP(3)):FOR N=1 TO 3:PC(I,N)=CP(N):NEXT
478 IF AU=-1 THEN LINE(291,127-CP(C)*8)-(322,119-CP(C)*8),14,BF:COLOR=(I,CP(1),CP(2),CP(3)):FOR N=1 TO 3:PC(I,N)=CP(N):NEXT
479 RETURN
480 GOSUB 19100:IF RR>0 THEN COPY(285,0)-(431,130),1 TO (183,41),0:COLOR 15,14:A$="EL JUGADOR"+STR$(RR)+" TIENE EL MISMO COLOR":O=0:K=2000:GOSUB 13070:COLOR=(9,0,0,0):GOTO 402
481 IF PL=2 AND PC(I,1)=1 AND PC(I,2)=1 AND PC(I,3)=1 THEN COPY(285,0)-(431,130),1 TO (183,41),0:COLOR 15,14:A$="EL EJERCITO ALIADO TIENE EL MISMO COLOR":O=0:K=2000:GOSUB 13070:COLOR=(9,0,0,0):GOTO 402
482 IF PC(I,1)=7 AND PC(I,2)=7 AND PC(I,3)=7 THEN COPY(285,0)-(431,130),1 TO (183,41),0:COLOR 15,14:A$="NO PUEDES TENER ESE COLOR":O=0:K=2000:GOSUB 13070:COLOR=(9,0,0,0):GOTO 402
483 COPY(285,0)-(431,130),1 TO (183,41),0:FOR N=1 TO PL:COLOR=(N,PC(N,1),PC(N,2),PC(N,3)):NEXT
484 ' лллллллллллллллллллл
485 ' лл ORDEN DE JUEGO лл
486 ' лллллллллллллллллллл
489 FOR N=1 TO PL:PY$(N)=PL$(N):NEXT:FOR N=7 TO 12:COLOR=(N,0,0,0):NEXT:IF I<PL THEN 319 ELSE GOSUB 60000:GOSUB 11:COLOR=(9,0,0,0):IF C=2 THEN COLOR=(6,0,0,7):COLOR=(8,5,0,0):FOR N=1 TO PL:PL$(N)="":FOR J=1 TO 3:PC(N,J)=0:NEXT J,N:GOTO 20
490 COLOR15,14:A$="SORTEANDO EL ORDEN DE JUEGO...":O=0:K=1500:GOSUB 13070:I=0
500 I=I+1:N=0
510 R(I)=INT(RND(-TIME)*PL)+1
520 N=N+1
530 IF R(I)=R(N) AND I<>N THEN N=0:GOTO 510
540 IF N<I THEN 520
545 IF I<PL THEN 500
546 A$="ESTE ES EL ORDEN DE JUEGO":O=0:K=1500:GOSUB 13070:FOR N=1 TO PL:PY$(R(N))=PL$(N):FOR J=1 TO 3:PR(R(N),J)=PC(N,J):NEXT J,N:FOR N=1 TO PL:COLOR=(N,PR(N,1),PR(N,2),PR(N,3)):NEXT:G=4:GOSUB 60000:G=0:GOSUB 12000
547 COPY(285,0)-(415,E),1 TO (191,F),0:M=42 MOD PL:IF M=0 THEN FOR N=1 TO PL:TP(N)=42/PL:NEXT ELSE FOR N=1 TO 2:TP(N)=INT(42/PL)+1:NEXT:FOR N=3 TO PL:TP(N)=INT(42/PL):NEXT:IF PL=2 THEN QT=14 ELSE QT=0
549 ' лллллллллллллллллллллллллл
550 ' лл REPARTIR TERRITORIOS лл
551 ' лллллллллллллллллллллллллл
555 IF PL=2 THEN FOR N=1 TO 3:TP(N)=14:NEXT:COLOR=(3,1,1,1)
556 COLOR 15,14:A$="ESPERA UN MOMENTO...":O=0:K=1500:GOSUB 13070:ON ERROR GOTO 10998:EL=1:O=1:SETPAGE 0,1:BLOAD"RISK2.PIC",S:ON ERROR GOTO 0:SET PAGE 0,0:P=0:T=0:X=105:Y=X:COLOR=(11,1,0,0)
557 GOSUB 19150:GOSUB 65519:LINE(1,1)-(254,17),8,BF:LINE(255,1)-(255,17),15:LINE(256,1)-(510,17),9,BF:PRESET(52,6):COLOR,8:PRINT#1,"SORTEAR TERRITORIOS":PRESET(311,6):COLOR,9:PRINT#1,"ELEGIR TERRITORIOS":A=2:GOSUB 65500:IF C=1 THEN 568 ELSE GOSUB 11
558 P=P+1:T=T+1:GOSUB 695:PW=2:JW=1
559 GOSUB 55010:GOSUB 30004:IF CO=0 OR CB(CO)<>0 THEN 559
560 CB(CO)=P:A$=C$(CO):O=0:K=200:GOSUB 13070:FOR N=1 TO CC(CO):PAINT(CX(CO,N),CY(CO,N)),P,0:NEXT:IF P=PL THEN P=0
561 IF T=42-QT THEN LINE(180,182)-(360,211),0,BF:PUT SPRITE 0,(511,211),0,0 ELSE 558
562 IF PL=2 THEN FOR I=1 TO 42:IF CB(I)=0 THEN CB(I)=3:NEXT ELSE NEXT
563 GOTO 680
568 GOSUB 11:COLOR 15,14:A$="SORTEANDO TERRITORIOS...":O=0:K=1500:GOSUB 13070:C=0:IF PL=2 THEN PY=3 ELSE PY=PL
569 FOR N=1 TO PY:T(N)=0:NEXT
570 C=C+1
580 P=INT(RND(-TIME)*PY)+1:IF T(P)=TP(P) THEN 580 ELSE T(P)=T(P)+1:CB(C)=P
620 IF C<42 THEN 570
630 PY=PL:FOR I=1 TO PL
640 A$=PY$(I)+", ESTOS SON TUS TERRITORIOS":O=0:K=1500:GOSUB 13070
650 FOR N=1 TO 42
660 IF CB(N)=I THEN A$=C$(N):O=0:K=500:GOSUB 13070:FOR J=1 TO CC(N):PAINT(CX(N,J),CY(N,J)),I,0:NEXT:FOR J=1 TO 350:NEXT
670 NEXT N,I:P=0
680 IF PL=2 THEN A$="ESTOS SON LOS TERRITORIOS DEL EJERCITO ALIADO":O=0:K=2000:GOSUB 13070:GOSUB 11000
684 ' ллллллллллллллллллллллл
685 ' лл COLOCAR EJERCITOS лл
686 ' ллллллллллллллллллллллл
690 P=P+1:GOSUB 695:GOTO 696
695 LINE(180,182)-(360,211),0,BF:Z=LEN(PY$(P)):L=INT((511-Z*8)/2):PRESET(L,200):COLOR,0:PRINT#1,PY$(P):FOR N=1 TO 3 STEP 2:LINE(L-2*N-5,198-N)-(L+Z*8+2*N+1,208+N),15,B:NEXT:PAINT(L-10,196),P,15:RETURN
696 COLOR,14:A$=PY$(P)+", COLOCA TUS EJERCITOS":O=0:K=1500:GOSUB 13070
697 U=3:D=7:AL=0:FOR N=1 TO 42:IF CB(N)=P THEN CA(N)=1:PRESET(CX(N,1),CY(N,1)):COLOR,P:PRINT#1,"1":NEXT ELSE NEXT
700 T=AR(P)-T(P):GOSUB 20021
710 GOSUB 11
720 IF P=PL THEN LINE(180,182)-(360,211),0,BF:GOTO 730 ELSE 690
730 IF PL=2 THEN FOR N=1 TO 42:IF CB(N)=3 THEN CA(N)=2:PRESET(CX(N,1),CY(N,1)):COLOR,3:PRINT#1,"2":NEXT ELSE NEXT
740 GOTO 50010
939 ' лллллллллллллллллллллл
940 ' лл ARREGLAR NOMBRES лл
941 ' лллллллллллллллллллллл
950 L=LEN(PY$(N)):IFL<10THENFORK=L+1TO10:PY$(N)=PY$(N)+CHR$(32):NEXT
951 RETURN
953 J=0:L=0:FORK=10TO1STEP-1
954 IFRIGHT$(LEFT$(N$,K),1)<>CHR$(32)ORJ=1THENJ=1ELSEL=L+1
955 NEXT:N$=LEFT$(N$,10-L)
956 RETURN
10004 ' ллллллллллл
10005 ' лл DATOS лл
10006 ' ллллллллллл
10010 DATA Alaska,1,1,37,50
10020 DATA Territorios del Noroeste,1,1,80,51
10030 DATA Groenlandia,1,3,192,35,113,48,137,27
10040 DATA Alberta,1,1,89,67
10050 DATA Ontario,1,1,120,68
10060 DATA Qubec,1,1,156,65
10070 DATA Estados Unidos Occidentales,1,1,97,85
10080 DATA Estados Unidos Orientales,1,1,131,92
10090 DATA Amrica Central,1,1,105,108
10100 DATA Venezuela,2,1,159,126
10110 DATA Brasil,2,1,183,150
10120 DATA PerЃ,2,1,147,147
10130 DATA Argentina,2,1,160,183
10140 DATA Islandia,3,1,230,51
10150 DATA Escandinavia,3,1,289,47
10160 DATA Ucrania,3,1,319,62
10170 DATA Gran BretaЄa,3,2,245,66,252,65
10180 DATA Europa del Norte,3,1,280,69
10190 DATA Europa Occidental,3,1,248,85
10200 DATA Europa del Sur,3,1,281,81
10210 DATA Africa del Norte,4,1,255,112
10220 DATA Egipto,4,1,291,108
10230 DATA Africa Oriental,4,1,310,128
10240 DATA El Congo,4,1,291,143
10250 DATA Africa del Sur,4,1,292,164
10260 DATA Madagascar,4,1,326,157
10270 DATA Ural,5,1,361,58
10280 DATA Siberia,5,1,391,51
10290 DATA Yakutsk,5,1,431,50
10300 DATA Irkutsk,5,1,420,74
10310 DATA Kamchatka,5,1,477,48
10320 DATA Mongolia,5,1,419,90
10330 DATA Afganist n,5,1,349,87
10340 DATA China,5,1,405,102
10350 DATA JapЂn,5,1,463,91
10360 DATA Medio Oriente,5,1,316,93
10370 DATA India,5,1,370,105
10380 DATA Indochina,5,1,406,117
10390 DATA Indonesia,6,1,422,137
10400 DATA Nueva Guinea,6,1,464,140
10410 DATA Australia Occidental,6,1,437,163
10420 DATA Australia Oriental,6,1,466,167
10460 DATA 00001000,00111111,00100001,00011100,00100001,00100001,00111110,00100001,00111111,01111111,00100001,00000000,00011100,00100001,00000000,00011100,00100001,00000000,00011100,00100001,00000000,00011100,00100001,00000000
10989 ' ллллллллллллл
10990 ' лл ERRORES лл
10991 ' ллллллллллллл
10998 SETPAGE0,0:ER=ERR:RESUME 13000
10999 ' лл TERRITORIOS DEL EJERCITO ALIADO лл
11000 FOR N=1 TO 42
11010 IF CB(N)=3 THEN A$=C$(N):G=0:K=500:GOSUB 13070:FOR J=1 TO CC(N):PAINT(CX(N,J),CY(N,J)),3,0:NEXT:FOR J=1 TO 350:NEXT
11020 NEXT:RETURN
11997 ' ллллллллллллллллллллл
11998 ' лл PULSA UNA TECLA лл
11999 ' ллллллллллллллллллллл
12000 ST=STRIG(0) OR STRIG(1) OR STRIG(2):IF ST=-1 THEN RETURN ELSE 12000
12997 ' лллллллллллллллллллллл
12998 ' лл ERRORES DE DISCO лл
12999 ' лллллллллллллллллллллл
13000 COLOR 15,14
13005 IF ER=70 THEN A$="NO HAY DISCO EN LA UNIDAD"
13010 IF ER=69 THEN A$="HAY UN ERROR EN EL FICHERO"
13020 IF ER=68 THEN A$="EL DISCO ESTA PROTEGIDO"
13030 IF ER=67 THEN A$="EL DISCO TIENE DEMASIADOS FICHEROS"
13040 IF ER=66 THEN A$="EL DISCO ESTA LLENO"
13050 IF ER=60 THEN A$="EL DISCO NO ESTA FORMATEADO"
13060 IF ER=57 OR ERR=61 THEN A$="EL FICHERO NO SIRVE"
13061 IF ER=53 THEN A$="AL DISCO LE FALTAN FICHEROS"
13062 GOSUB 13070:GOTO 13080
13065 ' лллллллллллллллллллллл
13066 ' лл ESCRIBIR MENSAJE лл
13067 ' лллллллллллллллллллллл
13070 M=INT((511-LEN(A$)*8)/2):COPY(M-13,99)-(524-M,111),0 TO (M-13,198),1:LINE(M-13,99)-(524-M,111),15,B:LINE(M-12,100)-(523-M,110),14,BF:PRESET(M+3,102):COLOR,14:PRINT#1,A$
13071 IF O=1 THEN GOSUB 12000 ELSE FOR Q=1 TO K:NEXT
13072 COPY(M-13,198)-(524-M,210),1 TO (M-13,99),0:RETURN
13080 IF EL=1 THEN 556
13090 IF EL=2 THEN 50010
13100 IF EL=3 THEN COLOR=(6,0,0,7):COLOR=(8,5,0,0):COLOR=(9,0,0,0):GOTO 20
15989 ' ллллллллллллллллллллллллл
15990 ' лл NUMERO DE EJERCITOS лл
15991 ' ллллллллллллллллллллллллл
16000 IF PL=2 THEN AR=40
16010 IF PL=3 THEN AR=35
16020 IF PL=4 THEN AR=30
16030 IF PL=5 THEN AR=25
16040 IF PL=6 THEN AR=20
16050 FOR N=1 TO PL:AR(N)=AR:NEXT:RETURN
18989 ' лллллллллллллллллллллллл
18990 ' лл ARREGLAR NOMBRES 2 лл
18991 ' лллллллллллллллллллллллл
19000 RR=0:FOR N=1 TO I
19010 IF PL$(I)=PL$(N) AND N<>I THEN RR=N
19020 NEXT:RETURN
19100 RR=0:FOR N=1 TO I
19110 IF PC(I,1)=PC(N,1) AND PC(I,2)=PC(N,2) AND PC(I,3)=PC(N,3) AND N<>I THEN RR=N
19120 NEXT:RETURN
19139 ' ллллллллллллллллллллллллллл
19140 ' лл BORRAR LINEA SUPERIOR лл
19141 ' ллллллллллллллллллллллллллл
19150 LINE(1,1)-(510,17),14,BF:RETURN
20009 ' ллллллллллллллллллллллл
20010 ' лл COLOCAR EJERCITOS лл
20011 ' ллллллллллллллллллллллл
20021 FORN=1TO1500:NEXT:LINE(0,199)-(121,211),15,B:LINE(1,200)-(120,210),14,BF:COLOR,14:PRESET(5,202):PRINT#1,"EJERCITOS:";T:FORN=9TO10:COLOR=(N,0,0,0):NEXT:LINE(1,1)-(169,17),8,BF:B=1:C=1
20022 LINE(171,1)-(340,17),9,BF:LINE(342,1)-(510,17),10,BF:LINE(170,0)-(341,18),15,B:COLOR=(8,5,0,0):COLOR,8:PRESET(21,6):PRINT#1,"AЅADIR EJERCITOS":COLOR,9:PRESET(191,6):PRINT#1,"RESTAR EJERCITOS":COLOR,10:PRESET(406,6):PRINT#1,"SALIR"
20023 A=3:GOSUB 65501
20024 X=105:Y=X:ON C GOTO 20026,20027,20025
20025 IF T>0 THEN GOSUB 65510:GOTO 20024 ELSE LINE(0,180)-(125,211),0,BF:PUT SPRITE0,(511,211),0,0:COLOR=(8,5,0,0):COLOR=(9,0,0,0):GOSUB 65524:IF C=2 THEN 20021 ELSE RETURN
20026 IF T=0 THEN A=3:GOTO 20023 ELSE GOSUB 21000:GOSUB 90:TP=1:GOTO 20040
20027 IF T=AR(P)-T(P) THEN A=3:GOTO 20023 ELSE GOSUB 21000:GOSUB 90:TP=-1:GOTO 20040
20040 IF XX>180 AND XX<228 AND Y>85 AND Y<97 THEN PUT SPRITE 0,(511,211),0,0:LINE(180,85)-(228,97),0,BF:FOR N=1 TO 50:NEXT:GOTO 20023
20050 IF (T=0 AND TP=1) OR (T=AR(P)-T(P) AND TP=-1) THEN GOSUB 90:GOTO 20040
20060 GOSUB 30004:IF CO=0 OR CB(CO)<>P THEN GOSUB 90:GOTO 20040 ELSE CA(CO)=CA(CO)+TP:IF CA(CO)<1 THEN CA(CO)=1:GOSUB 90:GOTO 20040
20070 T=T-TP:PAINT(CX(CO,1),CY(CO,1)),P,0:COLOR,P:L=LEN(STR$(CA(CO))):PRESET(CX(CO,1)-(L-2)*4,CY(CO,1)):PRINT#1,MID$(STR$(CA(CO)),2):COLOR,14:PRESET(85,202):PRINT#1,T:GOSUB 90:GOTO 20040
21000 LINE(180,85)-(228,97),15,B:LINE(181,86)-(227,96),14,BF:COLOR,14:PRESET(185,88):PRINT#1,"SALIR":RETURN
30000 ' ллллллллллллллллллллллллл
30002 ' лл IDENTIFICAR EL PAIS лл
30003 ' ллллллллллллллллллллллллл
30004 SET PAGE 0,1:M=POINT(XX,Y):SET PAGE 0,0:CO=0:IF M=0 OR M=15 THEN RETURN
30005 IF XX<216 THEN IF M=13 THEN CO=1 ELSE CO=M+1:RETURN
30010 IF XX>215 AND XX<290 THEN 30011 ELSE 30020
30011 IF M=1 THEN CO=15:RETURN
30013 IF M=2 THEN CO=M+1:RETURN
30014 IF M>2 AND M<8 THEN CO=M+15:RETURN
30015 IF M=8 OR M=9 THEN CO=M+16:RETURN
30016 IF M=13 THEN CO=14:RETURN
30017 IF M=10 THEN CO=17:RETURN
30020 IF XX>289 AND XX<345 THEN 30021 ELSE 30030
30021 IF M=3 THEN CO=18:RETURN
30022 IF M=1 THEN CO=15:RETURN
30023 IF M=4 THEN CO=36:RETURN
30024 IF M>4 AND M<8 OR M=11 THEN CO=M+15:RETURN
30025 IF M=8 OR M=9 THEN CO=M+16:RETURN
30026 IF M=10 THEN CO=23:RETURN
30027 IF M=13 THEN CO=16:RETURN
30028 IF M=12 THEN CO=33:RETURN
30030 IF XX>344 AND XX<391 THEN 30031 ELSE 30039
30031 IF M=11 THEN CO=M+16:RETURN
30032 IF M=5 THEN CO=34:RETURN
30033 IF M=8 THEN CO=28:RETURN
30034 IF M=10 THEN CO=37:RETURN
30035 IF M=13 THEN CO=16:RETURN
30036 IF M=12 THEN CO=33:RETURN
30039 IF XX>391 THEN 30040 ELSE RETURN
30040 IF M=3 OR M=5 OR M=12 THEN CO=M+29:RETURN
30041 IF M=1 THEN CO=31:RETURN
30042 IF M=2 THEN CO=30:RETURN
30044 IF M=4 OR M=9 THEN CO=M+31:RETURN
30046 IF M=6 OR M=7 OR M=10 THEN CO=M+32:RETURN
30047 IF M=13 THEN CO=29:RETURN
30048 IF M=8 THEN CO=28:RETURN
50000 ' ллллллллллллллллллллл
50005 ' лл CARGAR EL JUEGO лл
50006 ' ллллллллллллллллллллл
50010 A$="CARGANDO EL JUEGO...":O=0:K=2000:GOSUB 13070
50011 FOR N=1 TO PL:GOSUB 950:NEXT
50019 SET PAGE 0,1:M=4864:VPOKE M,PL
50020 FOR I=1 TO PL
50030 FOR N=1 TO 10:M=M+1:VPOKE M,ASC(RIGHT$(LEFT$(PY$(I),N),1)):NEXT
50040 FOR N=1 TO PL:FOR W=1 TO 3:M=M+1:VPOKE M,PR(N,W):NEXTW,N
50050 FOR N=1 TO PL:M=M+1:VPOKE M,T(N):NEXT
50060 FOR N=1 TO 42:M=M+1:VPOKE M,CB(N):M=M+1:VPOKE M,CA(N):NEXT
50070 NEXT:EL=2:O=1:ON ERROR GOTO 10998:POKE &HF346,0:RUN"RISK.PRG"
55000 ' лллллллллллллллллллллллллллллллллл
55005 ' лл RUTINA DE FLECHA PARPADEANTE лл
55006 ' лллллллллллллллллллллллллллллллллл
55010 S=STICK(0) OR STICK(1) OR STICK(2):ST=STRIG(0) OR STRIG(1) OR STRIG(2)
55020 XX=511+(2*X-503)
55030 IF S=1 THEN Y=Y-3
55040 IF S=2 THEN X=X+3:Y=Y-3
55050 IF S=3 THEN X=X+3
55060 IF S=4 THEN X=X+3:Y=Y+3
55070 IF S=5 THEN Y=Y+3
55080 IF S=6 THEN X=X-3:Y=Y+3
55090 IF S=7 THEN X=X-3
55100 IF S=8 THEN X=X-3:Y=Y-3
55110 IF X<0 OR X>247 THEN X=ABS(X-247)
55130 IF Y<18 OR Y>203 THEN Y=Y-SGN(Y-19)
55140 PW=PW+JW:COLOR=(11,PW,0,0):IF PW>6 THEN JW=-1 ELSE IF PW<3 THEN JW=1
55150 PUT SPRITE 0,(X,Y),11,0:IF ST=-1 THEN RETURN ELSE 55010
57987 ' ллллллллллллллллллллл
57988 ' лл PONER EJERCITOS лл
57989 ' ллллллллллллллллллллл
57990 COLOR,P:PAINT(CX(DC,1),CY(DC,1)),P,0:L=LEN(STR$(CA(DC))):PRESET(CX(DC,1)-(L-2)*4,CY(DC,1)):PRINT#1,MID$(STR$(CA(DC)),2):RETURN
57991 COLOR,D:PAINT(CX(E,1),CY(E,1)),D,0:L=LEN(STR$(CA(E))):PRESET(CX(E,1)-(L-2)*4,CY(E,1)):PRINT#1,MID$(STR$(CA(E)),2):RETURN
59997 ' лллллллллллл
59998 ' лл STATUS лл
59999 ' лллллллллллл
60000 E=PL*10+5:F=INT((211-E)/2):COPY(191,F)-(321,E+F),0 TO (285,0),1:COLOR,14:LINE(191,F)-(321,F+E),15,B:LINE(192,F+1)-(320,F+E-1),14,BF:FOR N=1 TO PL:Q=F+4+(N-1)*10:PRESET(204,Q):COLOR 15:PRINT#1,PY$(N):COLOR N:PRESET(284,Q):PRINT#1," лл":NEXT
60010 IF G=4 THEN RETURN ELSE COLOR=(8,5,0,0):COLOR=(9,0,0,0):COLOR15:GOSUB65524:COPY(285,0)-(415,E),1 TO (191,F),0:RETURN
65495 ' лллллллллллллллллллллллл
65496 ' лл MOVIMIENTO DE MENU лл
65497 ' лллллллллллллллллллллллл
65500 B=1:C=1
65501 S=STICK(0) OR STICK(1) OR STICK(2):ST=STRIG(0) OR STRIG(1) OR STRIG(2)
65502 IF AL=-2 AND S=1 THEN CP(C)=CP(C)+1:AU=1:RETURN
65503 IF AL=-2 AND S=5 THEN CP(C)=CP(C)-1:AU=-1:RETURN
65504 IF S=U THEN B=B+1
65505 IF S=D THEN B=B-1
65506 IF B<1 THEN B=A ELSE IF B>A THEN B=1
65508 IF B<>C THEN GOSUB 65511
65509 IF ST=-1 THEN RETURN
65510 GOTO 65501
65511 FOR N=4 TO 0 STEP-1:J=ABS(5-N):COLOR=(C+7-AL,N,0,0):COLOR=(B+7-AL,J,0,0):NEXT
65513 C=B:RETURN
65519 FOR Z=9 TO 12:COLOR=(Z,0,0,0):NEXT:COLOR=(8,5,0,0):RETURN
65520 COLOR=(8,5,0,0):FOR Z=9 TO 14:COLOR=(Z,0,0,0):NEXT:RETURN
65521 FOR Z=7 TO 11:IF S(1)<>Z-6+AL AND S(2)<>Z-6+AL AND S(3)<>Z-6+AL THEN COLOR=(Z,0,0,0):NEXT ELSE NEXT
65522 RETURN
65523 ' лл SI/NO лл
65524 LINE(1,1)-(255,17),8,BF:LINE(256,1)-(256,17),15:LINE(257,1)-(510,17),9,BF:PRESET(120,6):COLOR,8:PRINT#1,"SI":COLOR,9:PRESET(376,6):PRINT#1,"NO":A=2:GOSUB 65500:RETURN
