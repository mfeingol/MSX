1 DEFINTA-L,N-Z:DIMC$(42),CO(42),CX(42),CY(42),CF(42,6),CB(42),CA(42),CT(42),T(6),C(6,10),CD$(44),CD(44),PL$(6),A(3),D(3),P(6),VA(44),PC(6,3)
2 IFPEEK(&HF346)=0THENSETPAGE0,1:M=4864:PL=VPEEK(M)ELSELL=1:OPEN"GRP:"AS1:GOSUB40:X=105:Y=X:GOSUB11000
3 FORI=1TOPL
4 FORN=1TO10:M=M+1:PL$(I)=PL$(I)+CHR$(VPEEK(M)):NEXT
5 FORN=1TOPL:FORW=1TO3:M=M+1:PC(N,W)=VPEEK(M):NEXTW,N
6 FORN=1TOPL:M=M+1:T(N)=VPEEK(M):NEXT
7 FORN=1TO42:M=M+1:CB(N)=VPEEK(M):M=M+1:CA(N)=VPEEK(M):NEXT
8 NEXT:GOSUB11910:LINE(0,19)-(511,20),0,B:SETPAGE0,0
9 IFPL=2THENPH=1:PJ=1:T(3)=14:COLOR=(3,1,1,1):PL$(3)="EJERCITO ALIADO"ELSEPH=0
20 CLOSE:OPEN"GRP:"AS1
30 COLOR=(0,0,0,3):FORN=7TO14:COLOR=(N,0,0,0):NEXT:COLOR=(15,7,7,7):FORN=1TOPL:COLOR=(N,PC(N,1),PC(N,2),PC(N,3)):NEXT
33 'LEER DATOS
34 GOSUB40:GOTO300
40 RESTORE10460:S1$="":S2$="":S3$="":FORI=1TO8:READA$,B$,C$:S1$=S1$+CHR$(VAL("&B"+A$)):S2$=S2$+CHR$(VAL("&B"+B$)):S3$=S3$+CHR$(VAL("&B"+C$)):NEXT:SPRITE$(0)=S1$:SPRITE$(1)=S2$:SPRITE$(2)=S3$
75 RESTORE10010:FORI=1TO42:READC$(I),CO(I),CX(I),CY(I),CF(I,0):FORN=1TOCF(I,0):READCF(I,N):NEXT:READCD$(I),CD(I):NEXT:FORI=43TO44:CD$(I)="COMODIN":CD(I)=4:NEXT:RETURN
78 'FLECHA
90 S=STICK(0)ORSTICK(1)ORSTICK(2):ST=STRIG(0)ORSTRIG(1)ORSTRIG(2)
95 XX=511+(2*X-503)
100 IFS=1THENY=Y-3
110 IFS=2THENX=X+3:Y=Y-3
120 IFS=3THENX=X+3
130 IFS=4THENX=X+3:Y=Y+3
140 IFS=5THENY=Y+3
150 IFS=6THENX=X-3:Y=Y+3
160 IFS=7THENX=X-3
170 IFS=8THENX=X-3:Y=Y-3
230 IFX<0THENX=247
240 IFX>247THENX=0
250 IFY<18THENY=18
260 IFY>203THENY=203
270 PUTSPRITE0,(X,Y),15,0:IFST=-1THENRETURNELSE90
298 'RUTINA
300 X=105:Y=X:R=0:Q=0:CR=0:P=P+1:IFP>PLTHENP=1
301 PY=P:IFP(P)=1THEN300
302 IFPH=1ANDPJ=1THENA$=PL$(3):GOSUB303:GOTO304ELSEA$=PL$(P):GOSUB303:GOTO304
303 LINE(180,182)-(360,211),0,BF:Z=LEN(A$):X=INT((511-Z*8)/2):PRESET(X,200):COLOR,0:PRINT#1,A$:FORN=1TO3STEP2:LINE(X-2*N-5,198-N)-(X+Z*8+2*N+1,208+N),15,B:NEXT:PAINT(X-10,196),P,15:RETURN
304 GOSUB65520:LINE(1,1)-(255,17),8,BF:LINE(256,1)-(256,17),15:LINE(257,1)-(510,17),9,BF:PRESET(52,6):COLOR,8:PRINT#1,"INTERCAMBIAR CARTAS":COLOR,9:PRESET(359,6):PRINT#1,"SEGUIR":A=2:GOSUB65500
305 IFC=1THENR=1:GOSUB58000
306 IFC=2THENIFC(P,0)=5THENGOSUB19150:COLOR,14:PRESET(83,6):PRINT#1,"TIENES 5 CARTAS. TIENES QUE INTERCAMBIARLAS":FORN=1TO1500:NEXT:R=1:RR=0:GOSUB58000
310 GOSUB41010:GOSUB19150:COLOR,14
311 IFPH=1ANDPJ=1THENTY=T:T=INT(T/2):P=3:GOSUB312:GOTO319ELSEGOSUB312:GOTO319
312 IFPH=1ANDPJ=0THENT=TY
313 IFT>1THENPRESET(115,6):PRINT#1,"TE CORRESPONDE COLOCAR";T;"EJERCITOS":GOSUB20021:RETURN
314 PRESET(119,6):PRINT#1,"TE CORRESPONDE COLOCAR";T;"EJERCITO":GOSUB20021:RETURN
319 X=105:Y=X:B=1:C=1:GOSUB65520
320 COPY(0,0)-(511,18),1TO(0,0),0:A=5:R=0:COLOR=(C+7,5,0,0):GOSUB65501:ONCGOSUB35000,321,65010,400,60000:FORN=1TO325:NEXT:GOTO320
321 P=PY:GOSUB58000:B=2:C=2:GOSUB65519:IFPJ=1THENP=3:GOTO320ELSE320
400 GOSUB65524:IFC=1THEN410ELSEC=4:B=4:COLOR=(9,0,0,0):RETURN
410 IFPH=1ANDPJ=1THENGOTO412
411 IFCR=1THENGOSUB19150:COLOR,14:PRESET(163,6):COLOR15:PRINT#1,"TE HAS GANADO UNA CARTA":GOSUB45000:FORN=1TO1500:NEXT:GOSUB19150
412 IFPH=0THEN414ELSEIFPJ=0THENPJ=1:P=PY:RETURN300ELSEP=ABS(PY-2)+1:A$=PL$(P):GOSUB303:GOSUB415:P=PY:A$=PL$(P):GOSUB303:PJ=0:CR=0:RETURN310
414 GOSUB415:GOTO431
415 FORN=8TO9:COLOR=(N,0,0,0):NEXT:LINE(1,1)-(254,17),8,BF:LINE(256,1)-(510,17),9,BF:LINE(255,1)-(255,17),15:COLOR=(8,5,0,0):COLOR,8:PRESET(68,6):PRINT#1,"MOVER EJERCITOS":COLOR,9:PRESET(312,6):PRINT#1,"NO MOVER EJERCITOS":A=2:GOSUB65500:IFC=2THENRETURN
416 GOSUB19150:COLOR,14:PRESET(155,6):PRINT#1,"SELECCIONA PAIS DE ORIGEN"
417 GOSUB90:GOSUB30004:IFCO=0THEN417ELSEIFCB(CO)<>PORCA(CO)=1THENGOSUB490:GOTO415ELSEGOSUB500:IFQ=1THEN417ELSEC1=CO
418 GOSUB19150:COLOR,14:PRESET(151,6):PRINT#1,"SELECCIONA PAIS DE DESTINO"
419 GOSUB90:GOSUB30004:IFCO=0ORCO=C1THEN419ELSEIFCB(CO)<>PTHENGOSUB490:GOTO415ELSER=0:AC=C1:GOSUB35085:IFR=1THENC2=COELSE419
420 PUTSPRITE0,(511,211),0,0:GOSUB19150:COLOR,14:PRESET(INT((511-(6+LEN(C$(C1))+LEN(C$(C2)))*8)/2),6):PRINT#1,"DE ";C$(C1);" A ";C$(C2):FORN=1TO1500:NEXT:GOSUB19150:CC=CA(C1)-1:PRESET(30,6):PRINT#1,"®A CUANTOS EJERCITOS MUEVES? ";CC
421 S=STICK(0)ORSTICK(1)ORSTICK(2):ST=STRIG(0)ORSTRIG(1)ORSTRIG(2)
422 IFS=1ORS=3THENCC=CC+1:IFCC>CA(C1)-1THENCC=CA(C1)-1
423 IFS=5ORS=7THENCC=CC-1:IFCC<1THENCC=1
424 IFST=-1THEN428
425 PRESET(262,6):PRINT#1,CC
426 FORN=1TO15:NEXT
427 GOTO421
428 GOSUB65524:IFC=2THEN415ELSECA(C1)=CA(C1)-CC:CA(C2)=CA(C2)+CC
429 PAINT(CX(C1),CY(C1)),P,0:COLOR,P:L=LEN(STR$(CA(C1))):PRESET(CX(C1)-(L-2)*4,CY(C1)):PRINT#1,MID$(STR$(CA(C1)),2):COLOR,P:L=LEN(STR$(CA(C2))):PRESET(CX(C2)-(L-2)*4,CY(C2)):PRINT#1,MID$(STR$(CA(C2)),2)
430 PUTSPRITE0,(511,211),0,0:RETURN
431 RETURN300
490 PUTSPRITE0,(511,211),0,0:GOSUB19150:PRESET(220,6):PRINT#1,"IMPOSIBLE":FORN=1TO1500:NEXT:RETURN
500 Q=0:IFCA(CO)=1THENQ=1:RETURN
510 Q=1:FORN=1TOCF(CO,0)
520 IFCB(CF(CO,N))=PTHENQ=0
530 NEXT:RETURN
10010 DATAAlaska,1,37,50,3,2,4,31,Alaska,3
10020 DATATerritorios del Noroeste,1,80,51,4,1,3,4,5,T. del Nor.,1
10030 DATAGroenlandia,1,192,35,4,2,5,6,14,Groenlandia,1
10040 DATAAlberta,1,89,67,4,1,2,5,7,Alberta,3
10050 DATAOntario,1,120,68,6,2,3,4,6,7,8,Ontario,3
10060 DATAQuÇbec,1,156,65,3,3,5,8,QuÇbec,3
10070 DATAEstados Unidos Occidentales,1,97,85,4,4,5,8,9,EE.UU. Oeste,3
10080 DATAEstados Unidos Orientales,1,131,92,4,5,6,7,9,EE.UU. Este,3
10090 DATAAmÇrica Central,1,105,108,3,7,8,10,AmÇrica Cen.,2
10100 DATAVenezuela,2,159,126,3,9,11,12,Venezuela,1
10110 DATABrasil,2,183,150,4,10,12,13,21,Brasil,2
10120 DATAPer£,2,147,147,3,10,11,13,Per£,1
10130 DATAArgentina,2,160,183,2,11,12,Argentina,2
10140 DATAIslandia,3,230,50,3,3,15,17,Islandia,1
10150 DATAEscandinavia,3,289,47,4,14,16,17,18,Escandinavia,2
10160 DATAUcrania,3,319,62,6,15,18,20,27,33,36,Ucrania,2
10170 DATAGran Breta§a,3,245,66,4,14,15,18,19,Gran Breta§a,2
10180 DATAEuropa del Norte,3,280,69,5,15,16,17,19,20,Europa de N.,1
10190 DATAEuropa Occidental,3,248,85,4,17,18,20,21,Europa Occi.,2
10200 DATAEuropa del Sur,3,281,81,6,16,18,19,21,22,36,Europa de S.,2
10210 DATAAfrica del Norte,4,255,112,6,11,19,20,22,23,24,Africa de N.,2
10220 DATAEgipto,4,291,108,4,20,21,23,36,Egipto,1
10230 DATAAfrica Oriental,4,310,128,6,21,22,24,25,26,36,Africa Orie.,2
10240 DATAEl Congo,4,291,143,3,21,23,25,El Congo,3
10250 DATAAfrica del Sur,4,292,164,3,23,24,26,Africa de S.,3
10260 DATAMadagascar,4,326,157,2,23,25,Madagascar,1
10270 DATAUral,5,361,58,4,16,28,33,34,Ural,2
10280 DATASiberia,5,391,51,5,27,29,30,32,34,Siberia,2
10290 DATAYakutsk,5,431,50,3,28,30,31,Yakutsk,3
10300 DATAIrkutsk,5,420,74,4,28,29,31,32,Irkutsk,3
10310 DATAKamchatka,5,477,48,5,1,29,30,32,35,Kamchatka,3
10320 DATAMongolia,5,419,90,5,28,30,31,34,35,Mongolia,1
10330 DATAAfganist†n,5,349,87,5,16,27,34,36,37,Afganist†n,2
10340 DATAChina,5,405,102,6,27,28,32,33,37,38,China,3
10350 DATAJap¢n,5,463,91,2,31,32,Jap¢n,1
10360 DATAMedio Oriente,5,316,93,6,16,20,22,23,33,37,Medio Orien.,3
10370 DATAIndia,5,370,105,4,33,34,36,38,India,1
10380 DATAIndochina,5,406,117,3,34,37,39,Indochina,1
10390 DATAIndonesia,6,422,137,3,38,40,41,Indonesia,2
10400 DATANueva Guinea,6,464,140,3,39,41,42,Nueva Guinea,1
10410 DATAAustralia Occidental,6,437,163,3,39,40,42,A. Occident.,3
10420 DATAAustralia Oriental,6,466,167,2,40,41,A. Oriental,1
10460 DATA1000,111111,100001,11100,100001,100001,111110,100001,111111,1111111,100001,,11100,100001,,11100,100001,,11100,100001,,11100,100001,,
10990 ER=ERR:SETPAGE0,0:RESUME13000
10997 ER=ERR:Q=0:SETPAGE0,1:LINE(0,19)-(511,20),0,B:SETPAGE0,0:RESUME13000
10998 IFERR=53THENER(N)=1:RESUMENEXTELSEER=ERR:RESUME13000
10999 'DISCO
11000 GOSUB19150:LINE(194,0)-(245,18),15,B:LINE(195,1)-(244,17),9,BF:LINE(246,1)-(510,17),0,BF:COLOR=(7,0,7,0):COLOR=(8,7,2,1):COLOR=(9,0,0,0):COLOR,14:PRESET(9,6):PRINT#1,"PARTIDA:":COLOR,9:PRESET(201,6):PRINT#1,"SALIR"
11010 ONERRORGOTO10998:FORN=1TO9
11020 ER(N)=0:FILES"RISKSAVE."+MID$(STR$(N),2)
11030 PRESET(70+N*12,6):IFER(N)=1THENCOLOR7ELSECOLOR8
11035 COLOR,14:PRINT#1,MID$(STR$(N),2)
11040 NEXT:ONERRORGOTO0
11050 XX=1:FORN=1TO2:PUTSPRITEN,(37,N*8-5),15,N:NEXT
11060 S=STICK(0)ORSTICK(1)ORSTICK(2):ST=STRIG(0)ORSTRIG(1)ORSTRIG(2)
11070 IFS=3THENXX=XX+1
11080 IFS=7THENXX=XX-1
11090 IFXX<1THENXX=1
11100 IFXX>10THENXX=10
11110 IFXX<10THENCOLOR=(9,0,0,0):FORN=1TO2:PUTSPRITEN,(31+XX*6,N*8-5),15,N:NEXT
11120 IFXX=10THENFORN=1TO2:PUTSPRITEN,(511,211),0,N:NEXT:COLOR=(9,5,0,0)
11130 IFST=-1THEN11150
11135 FORN=1TO40:NEXT
11140 GOTO11060
11150 IFXX=10THENIFPEEK(&HF346)=0THENCOLOR15:RETURNELSE65113
11155 IFLL=1ANDER(XX)=1THEN11060
11160 ONERRORGOTO0:IFLL=1THEN11161ELSE11170
11161 SETPAGE0,1:BLOAD"RISKSAVE."+MID$(STR$(XX),2),S:ONERRORGOTO0:IFVPEEK(5200)=162ANDVPEEK(4866)<7THENM=4866!:PH=VPEEK(M-2):P=VPEEK(M-1):PL=VPEEK(M)ELSEGOTO12997
11162 FORN=1TO42:M=M+1:CB(N)=VPEEK(M):M=M+1:CA(N)=VPEEK(M):M=M+1:CT(N)=VPEEK(M):NEXT:FOR N=1 TO 44:M=M+1:VA(N)=VPEEK(M):NEXT:FORN=1TOPL:M=M+1:T(N)=VPEEK(M):M=M+1:P(N)=VPEEK(M):NEXT
11163 FORN=1TOPL:PL$(N)="":M=M+1:C(N,0)=VPEEK(M):FORW=1TO10:M=M+1:C(N,W)=VPEEK(M):M=M+1:PL$(N)=PL$(N)+CHR$(VPEEK(M)):NEXTW,N:FORN=1TOPL:FORW=1TO3:M=M+1:PC(N,W)=VPEEK(M):NEXTW,N:M=M+1:PJ=VPEEK(M):M=M+1:PY=VPEEK(M):M=M+1:TY=VPEEK(M):M=M+1:CR=VPEEK(M)
11164 LINE(0,19)-(511,20),0,B:SETPAGE0,0:FORN=1TO2:PUTSPRITEN,(511,211),0,N:NEXT:GOSUB11910:FORN=1TOPL:COLOR=(N,PC(N,1),PC(N,2),PC(N,3)):NEXT:X=105:Y=X:COLOR15
11165 IFPL=2THENT(3)=42-T(1)-T(2):COLOR=(3,1,1,1):PL$(3)="EJERCITO ALIADO"
11167 FORI=1TO3:PAINT(CX(I),CY(I)),CB(I),0:L=LEN(STR$(CA(I))):PRESET(CX(I)-(L-2)*4,CY(I)):COLOR,CB(I):PRINT#1,MID$(STR$(CA(I)),2):GOSUB65518:NEXT
11168 FORI=4TO42:PAINT(CX(I),CY(I)),CB(I),0:L=LEN(STR$(CA(I))):PRESET(CX(I)-(L-2)*4,CY(I)):COLOR,CB(I):PRINT#1,MID$(STR$(CA(I)),2):NEXT:A$=PL$(P):POKE&HF346,0:IFPL=2ANDP=3THENP=PY:GOSUB303:P=3:RETURN319ELSEGOSUB303:RETURN319
11170 ONERRORGOTO10997:SETPAGE0,1:M=4866:VPOKEM-2,PH:VPOKEM-1,P:VPOKEM,PL
11171 FORN=1TO42:M=M+1:VPOKEM,CB(N):M=M+1:VPOKEM,CA(N):M=M+1:VPOKEM,CT(N):NEXT:FORN=1TO44:M=M+1:VPOKEM,VA(N):NEXT:FORN=1TOPL:M=M+1:VPOKEM,T(N):M=M+1:VPOKEM,P(N):NEXT:FORN=1TOPL
11172 GOSUB11900:M=M+1:VPOKEM,C(N,0):FORW=1TO10:M=M+1:VPOKEM,C(N,W):M=M+1:VPOKEM,ASC(RIGHT$(LEFT$(PL$(N),W),1)):NEXTW,N:FORN=1TOPL:FORW=1TO3:M=M+1:VPOKEM,PC(N,W):NEXTW,N:M=M+1:VPOKEM,PJ:M=M+1:VPOKEM,PY:M=M+1:VPOKEM,TY:M=M+1:VPOKEM,CR:VPOKE5199,P(0)
11173 VPOKE5200,162
11180 GOSUB11910:BSAVE"RISKSAVE."+MID$(STR$(XX),2),4864,5200,S:ONERRORGOTO0:LINE(0,19)-(511,20),0,B:SETPAGE0,0:FORN=1TO2:PUTSPRITEN,(511,211),0,N:NEXT:GOSUB19150:PRESET(151,6):COLOR15,14:PRINT#1,"LA PARTIDA HA SIDO GRABADA":FORN=1TO2000:NEXT
11190 RETURN319
11900 L=LEN(PL$(N)):IFL<10THENFORK=L+1TO10:PL$(N)=PL$(N)+CHR$(32):NEXT
11901 RETURN
11910 FORN=1TOPL
11911 J=0:L=0:FORK=10TO1STEP-1
11912 IFRIGHT$(LEFT$(PL$(N),K),1)<>CHR$(32)ORJ=1THENJ=1ELSEL=L+1
11913 NEXT:PL$(N)=LEFT$(PL$(N),10-L):NEXT
11914 RETURN
12997 SETPAGE0,0:GOSUB19150:FORN=0TO2:PUTSPRITEN,(511,211),0,N:NEXT:PRESET(123,6):COLOR15,14:PRINT#1,"EL FICHERO NO ESTA EN CONDICIONES":GOSUB59050
12998 IFPEEK(&HF346)=1THENGOSUB11000ELSERETURN65011
12999 'ERRORS
13000 FORN=0TO2:PUTSPRITEN,(511,211),0,N:NEXT:GOSUB13004:IFQ<1THENIFLL=1THENC=1:B=1:COLOR=(9,0,0,0):GOTO65011ELSEC=2:B=2:COLOR=(8,0,0,0):GOTO65011ELSE15010
13004 GOSUB19150:COLOR15,14
13005 IFER=70THENA$="NO HAY DISCO EN LA UNIDAD"
13010 IFER=69THENA$="HAY UN ERROR EN EL FICHERO"
13020 IFER=68THENA$="EL DISCO ESTA PROTEGIDO"
13030 IFER=67THENA$="EL DISCO TIENE DEMASIADOS FICHEROS"
13040 IFER=66THENA$="EL DISCO ESTA LLENO"
13050 IFER=60THENA$="EL DISCO NO ESTA FORMATEADO"
13060 IFER=57ORERR=61THENA$="EL FICHERO NO SIRVE"
13061 IFER=53THENA$="FALTAN FICHEROS"
13070 PRESET(INT((511-LEN(A$)*8)/2),6):COLOR15,14:PRINT#1,A$
13075 IFPEEK(&HF346)=1THENGOSUB59050:RETURN11000
13080 GOSUB59050:RETURN
14999 'FIN
15000 GOSUB19150:COLOR15,14:PRESET(INT((511-(46+LEN(PL$(P)))*8)/2),6):PRINT#1,PL$(P);" HA GANADO LA PARTIDA Y HA CONQUISTADO EL MUNDO":FORN=1TO8000:NEXT
15010 GOSUB19150:LINE(180,182)-(360,211),0,BF:PRESET(139,6):PRINT#1,"CARGANDO INICIO DE PARTIDA...":FORI=1TO6:COLOR=(I,7,7,7):NEXT:COLOR=(13,7,7,7):Q=1:GOTO65111
19150 LINE(1,1)-(510,17),14,BF:RETURN
20010 'COLOCAR EJ.
20021 FORN=1TO1500:NEXT:LINE(0,199)-(121,211),15,B:LINE(1,200)-(120,210),14,BF:COLOR,14:PRESET(5,202):PRINT#1,"EJERCITOS:";T:TT=T:FORN=8TO10:COLOR=(N,0,0,0):NEXT:LINE(1,1)-(169,17),8,BF:B=1:C=1
20022 LINE(171,1)-(340,17),9,BF:LINE(342,1)-(510,17),10,BF:LINE(170,0)-(341,18),15,B:GOSUB65520:COLOR,8:PRESET(21,6):PRINT#1,"A•ADIR EJERCITOS":COLOR,9:PRESET(191,6):PRINT#1,"RESTAR EJERCITOS":COLOR,10:PRESET(406,6):PRINT#1,"SALIR"
20023 A=3:GOSUB65501
20024 X=105:Y=X:ONCGOTO20026,20027,20025
20025 IFTT>0THENGOSUB65510:GOTO20024ELSEFORN=1TO42:CA(N)=CA(N)+CT(N):CT(N)=0:NEXT:LINE(0,180)-(125,211),0,BF:PUTSPRITE0,(511,211),0,0:RETURN
20026 IFTT=0THENGOTO20023ELSEGOSUB21000:FORN=1TO25:NEXT:GOSUB90:TP=1:GOTO20040
20027 IFTT=TTHENGOTO20023ELSEGOSUB21000:FORN=1TO25:NEXT:GOSUB90:TP=-1:GOTO20040
20040 IFXX>180ANDXX<228ANDY>85ANDY<97THENPUTSPRITE0,(511,211),0,0:LINE(180,85)-(228,97),0,BF:FORUE=1TO50:NEXT:GOTO20023
20050 IF(TT=0ANDTP=1)OR(TT=T ANDTP=-1)THENGOSUB90:GOTO20040
20055 GOSUB30004:IFCB(CO)<>PTHENGOSUB90:GOTO20040
20060 CT(CO)=CT(CO)+TP:IFCT(CO)<0THENCT(CO)=0:GOSUB90:GOTO20040
20070 TT=TT-TP:PAINT(CX(CO),CY(CO)),P,0:IFCO=3THENGOSUB65518
20080 COLOR,P:L=LEN(STR$(CA(CO)+CT(CO))):PRESET(CX(CO)-(L-2)*4,CY(CO)):PRINT#1,MID$(STR$(CA(CO)+CT(CO)),2):COLOR,14:PRESET(85,202):PRINT#1,TT:GOSUB90:GOTO20040
21000 LINE(180,85)-(228,97),15,B:LINE(181,86)-(227,96),14,BF:COLOR,14:PRESET(185,88):PRINT#1,"SALIR":RETURN
30002 'QUE PAIS
30004 SETPAGE0,1:M=POINT(XX,Y):SETPAGE0,0:CO=0:IFM=0ORM=15THENRETURN
30005 IFXX<216THENIFM=13THENCO=1ELSECO=M+1:RETURN
30010 IFXX>215ANDXX<290THEN30011ELSE30020
30011 IFM=1THENCO=15:RETURN
30013 IFM=2ORM=13THENCO=M+1:RETURN
30014 IFM>2ANDM<8THENCO=M+15:RETURN
30015 IFM=8ORM=9THENCO=M+16:RETURN
30017 IFM=10THENCO=17:RETURN
30020 IFXX>289ANDXX<345THEN30022ELSE30030
30022 IFM=1THENCO=15:RETURN
30023 IFM=4THENCO=36:RETURN
30024 IFM=3ORM>4ANDM<8ORM=11THENCO=M+15:RETURN
30025 IFM=8ORM=9THENCO=M+16:RETURN
30026 IFM=10THENCO=23:RETURN
30027 IFM=13THENCO=16:RETURN
30028 IFM=12THENCO=33:RETURN
30030 IFXX>344ANDXX<391THEN30031ELSE30039
30031 IFM=11THENCO=M+16:RETURN
30032 IFM=5THENCO=34:RETURN
30033 IFM=8THENCO=28:RETURN
30034 IFM=10THENCO=37:RETURN
30035 IFM=13THENCO=16:RETURN
30036 IFM=12THENCO=33:RETURN
30039 IFXX>391THEN30040ELSERETURN
30040 IFM=3ORM=5ORM=12THENCO=M+29:RETURN
30041 IFM=1THENCO=31:RETURN
30042 IFM=2THENCO=30:RETURN
30044 IFM=4ORM=9THENCO=M+31:RETURN
30046 IFM=6ORM=7ORM=10THENCO=M+32:RETURN
30047 IFM=13THENCO=29:RETURN
30048 IFM=8THENCO=28:RETURN
34998 'ATACAR
35000 GOSUB19150:PRESET(179,6):COLOR,14:PRINT#1,"ELIGE PAIS ATACANTE"
35004 GOSUB90
35005 GOSUB30004:IFCO=0THEN35004
35010 IFCB(CO)<>PTHENPUTSPRITE0,(511,211),0,0:LINE(175,5)-(350,16),14,BF:PRESET(179,6):PRINT#1,"ESE PAIS NO ES TUYO":FORN=1TO1000:NEXT:RETURN
35020 R=0:FORN=1TOCF(CO,0)
35030 IFCB(CF(CO,N))<>PAND(PJ=0ORPJ=1ANDCB(CF(CO,N))<>PY)THENR=1
35040 NEXT
35050 IFR=0ORCA(CO)=1THENPUTSPRITE0,(511,211),0,0:LINE(175,5)-(350,16),14,BF:PRESET(127,6):PRINT#1,"ESE PAIS NO PUEDE ATACAR A NADIE":FORN=1TO1000:NEXT:RETURN
35055 AC=CO
35060 GOSUB19150:PRESET(183,6):COLOR,14:PRINT#1,"ELIGE PAIS ATACADO"
35064 GOSUB90
35065 GOSUB30004:IFCO=0ORCO=ACTHEN35064
35070 PO=CB(CO):IFPO=PORPO=PYTHENPUTSPRITE0,(511,211),0,0:LINE(175,5)-(350,16),14,BF:PRESET(191,6):PRINT#1,"ESE PAIS ES TUYO":FORN=1TO1000:NEXT:RETURN
35080 R=0:GOSUB35085:GOTO35110
35085 FORN=1TOCF(CO,0)
35090 IFCF(CO,N)=ACTHENR=1
35100 NEXT:RETURN
35110 IFR=0THENPUTSPRITE0,(511,211),0,0:LINE(175,5)-(350,16),14,BF:PRESET(INT((511-(34+LEN(C$(AC)))*8)/2),6):PRINT#1,"NO PUEDES ATACAR A ESE PAIS DESDE ";C$(AC):FORN=1TO1000:NEXT:RETURN
35120 PUTSPRITE0,(511,211),0,0:DC=CO:A=LEN(C$(AC)):B=LEN(C$(DC)):C=A+7+B:GOSUB19150:COLOR,14:PRESET(INT((511-C*8)/2),6):PRINT#1,C$(AC);" ATACA ";C$(DC):FORN=1TO1000:NEXT:GOSUB65524:IFC=2THENPUTSPRITE0,(511,211),0,0:B=1:C=1:GOSUB65519:RETURN
35130 IFCA(AC)=2THENC=1:CC=1:GOTO35150
35140 GOSUB19150:COLOR,14:CC=CA(AC)-1:PRESET(30,6):PRINT#1,"®CON CUANTOS EJERCITOS ATACAS? ";CC
35141 S=STICK(0)ORSTICK(1)ORSTICK(2):ST=STRIG(0)ORSTRIG(1)ORSTRIG(2)
35142 IFS=1ORS=3THENCC=CC+1:IFCC>CA(AC)-1THENCC=CA(AC)-1
35143 IFS=5ORS=7THENCC=CC-1:IFCC<1THENCC=1
35144 IFST=-1THENC=CC:IFC>3THENC=3:GOTO35150ELSE35150
35145 PRESET(278,6):PRINT#1,CC
35146 FORN=1TO15:NEXT
35147 GOTO35141
35150 AA=C:DD=CA(DC):IFDD>2THENDD=2
35160 IFAA=1ANDDD=1THENGOSUB40004:GOTO35170
35161 IFAA=1ANDDD=2THENGOSUB40060:GOTO35170
35162 IFAA=2ANDDD=1THENGOSUB40020:GOTO35170
35163 IFAA=2ANDDD=2THENGOSUB40080:GOTO35170
35164 IFAA=3ANDDD=1THENGOSUB40040:GOTO35170
35165 IFAA=3ANDDD=2THENGOSUB40130:GOTO35170
35170 GOSUB19150:COLOR,14:PRESET(20,2):PRINT#1,"ATACANTE:":PRESET(20,10):PRINT#1,"DEFENSOR:":FORN=1TOAA:PRESET(89+N*12,2):PRINT#1,MID$(STR$(A(N)),2):NEXT
35171 FORN=1TODD:PRESET(89+N*12,10):PRINT#1,MID$(STR$(D(N)),2):NEXT
35172 FORN=1TO1000:NEXT
35180 IFAT>0THENGOSUB19150:PRESET(INT((511-(16+AT+LEN(C$(DC)))*8)/2),6):COLOR,14:PRINT#1,C$(DC);" PIERDE";AT;:IFAT=2THENPRINT#1,"EJERCITOS":FORN=1TO1000:NEXTELSEPRINT#1,"EJERCITO":FORN=1TO1000:NEXT
35190 IFDF>0THENGOSUB19150:PRESET(INT((511-(16+DF+LEN(C$(AC)))*8)/2),6):COLOR,14:PRINT#1,C$(AC);" PIERDE";DF;:IFDF=2THENPRINT#1,"EJERCITOS":FORN=1TO1000:NEXTELSEPRINT#1,"EJERCITO":FORN=1TO1000:NEXT
35200 CA(AC)=CA(AC)-DF:CA(DC)=CA(DC)-AT:D=PO:E=DC:GOSUB57991:D=P:E=AC:GOSUB57991
35210 IFCA(AC)=1THENGOSUB19150:COLOR,14:PRESET(INT((511-(23+LEN(C$(AC)))*8)/2),6):PRINT#1,C$(AC);" YA NO PUEDE ATACAR MAS":FORN=1TO1000:NEXT:C=1:B=1:GOSUB65519:RETURN
35220 IFCA(DC)=0THENGOSUB19150:COLOR,14:PRESET(INT((511-(21+LEN(C$(DC)))*8)/2),6):PRINT#1,C$(DC);" HA SIDO CONQUISTADO"ELSE35240
35230 FORN=1TO1000:NEXT:CR=1:CB(DC)=P:PAINT(CX(DC),CY(DC)),P,0:IFDC=3THENGOSUB65518
35235 CA(DC)=CC:CA(AC)=CA(AC)-CC:T(P)=T(P)+1:T(PO)=T(PO)-1:GOSUB57990:DC=AC:GOSUB57990:IFT(PO)=0THEN50000ELSEB=1:C=1:GOSUB65519:RETURN
35240 GOSUB65520:LINE(1,1)-(255,17),8,BF:LINE(256,1)-(256,17),15:LINE(257,1)-(510,17),9,BF:PRESET(68,6):COLOR,8:PRINT#1,"VOLVER A ATACAR":COLOR,9:PRESET(324,6):PRINT#1,"DEJAR DE ATACAR":A=2:GOSUB65500:IFC=2THENB=1:C=1:GOSUB65519:RETURNELSE35130
40001 '1,1
40004 A(1)=INT(RND(-TIME)*6)+1:D(1)=INT(RND(-TIME)*6)+1
40010 IFA(1)>D(1)THENAT=1:DF=0ELSEAT=0:DF=1
40011 RETURN
40018 '2,1
40020 A(1)=INT(RND(-TIME)*6)+1:A(2)=INT(RND(-TIME)*6)+1:D(1)=INT(RND(-TIME)*6)+1
40030 IFA(1)>D(1)ORA(2)>D(1)THENAT=1:DF=0ELSEAT=0:DF=1
40031 RETURN
40038 '3,1
40040 A(1)=INT(RND(-TIME)*6)+1:A(2)=INT(RND(-TIME)*6)+1:A(3)=INT(RND(-TIME)*6)+1:D(1)=INT(RND(-TIME)*6)+1
40050 IFA(1)>D(1)ORA(2)>D(1)ORA(3)>D(1)THENAT=1:DF=0ELSEAT=0:DF=1
40051 RETURN
40058 '1,2
40060 A(1)=INT(RND(-TIME)*6)+1:D(1)=INT(RND(-TIME)*6)+1:D(2)=INT(RND(-TIME)*6)+1
40070 IFA(1)>D(1)ANDA(1)>D(2)THENAT=1:DF=0ELSEAT=0:DF=1
40071 RETURN
40078 '2,2
40080 A(1)=INT(RND(-TIME)*6)+1:A(2)=INT(RND(-TIME)*6)+1:D(1)=INT(RND(-TIME)*6)+1:D(2)=INT(RND(-TIME)*6)+1
40090 IFA(1)<A(2)THENSWAPA(1),A(2)
40091 IFD(1)<D(2)THENSWAPD(1),D(2)
40092 IFA(1)>D(1)ANDA(2)>D(2)THENAT=2:DF=0:RETURN
40093 IF(A(1)>D(1)ANDA(2)<=D(2))OR(A(1)<=D(1)ANDA(2)>D(2))THENAT=1:DF=1:RETURN
40094 IFA(1)<=D(1)ANDA(2)<=D(2)THENAT=0:DF=2:RETURN
40095 RETURN
40110 '3,2
40130 A(1)=INT(RND(-TIME)*6)+1:A(2)=INT(RND(-TIME)*6)+1:A(3)=INT(RND(-TIME)*6)+1:D(1)=INT(RND(-TIME)*6)+1:D(2)=INT(RND(-TIME)*6)+1
40140 IFA(1)<A(2)THENSWAPA(1),A(2)
40141 IFA(2)<A(3)THENSWAPA(2),A(3)
40142 IFA(1)<A(2)THENSWAPA(1),A(2)
40150 IFD(1)<D(2)THENSWAPD(1),D(2)
40160 IFA(1)>D(1)ANDA(2)>D(2)THENAT=2:DF=0:RETURN
40170 IF(A(1)>D(1)ANDA(2)<=D(2))OR(A(1)<=D(1)ANDA(2)>D(2))THENAT=1:DF=1:RETURN
40180 IFA(1)<=D(1)ANDA(2)<=D(2)THENAT=0:DF=2:RETURN
40185 RETURN
41005 ' EJERCITOS NUEVOS
41010 T1=INT(T(P)/3):T2=0:IFT1<3THENT1=3
41020 FORI=1TO9:IFCB(I)<>PTHEN41030ELSENEXT:T2=T2+5
41030 FORI=10TO13:IFCB(I)<>PTHEN41040ELSENEXT:T2=T2+2
41040 FORI=14TO20:IFCB(I)<>PTHEN41050ELSENEXT:T2=T2+5
41050 FORI=21TO26:IFCB(I)<>PTHEN41060ELSENEXT:T2=T2+3
41060 FORI=27TO38:IFCB(I)<>PTHEN41070ELSENEXT:T2=T2+7
41070 FORI=39TO42:IFCB(I)<>PTHEN41080ELSENEXT:T2=T2+2
41080 I=0:T=T1+T2+T3:T1=0:T2=0:T3=0
41090 RETURN
44998 'CARTA NUEVA
45000 C=INT(RND(-TIME)*44)+1:IFVA(C)=1THEN45000
45010 C(P,C(P,0)+1)=C:C(P,0)=C(P,0)+1:VA(C)=1:RETURN
49998 'MUERTO
50000 P(PO)=1:IFPH=1ANDPO=3THENPL$(3)="EL "+PL$(3):PH=0ELSEP(0)=P(0)+1
50001 GOSUB19150:COLOR,14:PRESET(INT((511-(33+LEN(PL$(PO)))*8)/2),6):PRINT#1,PL$(PO);" HA PERDIDO TODOS SUS TERRITORIOS":FORN=1TO2000:NEXT:IFPH=1ORPL-P(0)=1THENGOTO15000
50010 IFC(PO,0)>0THENGOSUB19150:PRESET(INT((511-(24+LEN(PL$(P)))*8)/2),6):PRINT#1,PL$(P);" SE QUEDA CON SUS CARTAS":FORN=1TO1500:NEXT:FORN=1TOC(PO,0):C(P,C(P,0)+N)=C(PO,N):NEXT:C(P,0)=C(P,0)+C(PO,0):IFC(P,0)=>5THENGOSUB50015
50011 B=1:C=1:GOSUB65519:RETURN
50015 GOSUB19150:COLOR,14:PRESET(151,6):PRINT#1,"PUEDES INTERCAMBIAR CARTAS":FORN=1TO1500:RR=1:GOSUB58000:T=T3:GOSUB19150:COLOR,14:PRESET(128-LEN(STR$(T))*4,6):PRINT#1,"TE CORRESPONDE COLOCAR";T;"EJERCITOS":GOSUB20021:T3=0:B=1:C=1:GOSUB65519:RETURN
57988 'PONER EJ.
57990 COLOR,P:PAINT(CX(DC),CY(DC)),P,0:L=LEN(STR$(CA(DC))):PRESET(CX(DC)-(L-2)*4,CY(DC)):PRINT#1,MID$(STR$(CA(DC)),2):RETURN
57991 COLOR,D:PAINT(CX(E),CY(E)),D,0:L=LEN(STR$(CA(E))):PRESET(CX(E)-(L-2)*4,CY(E)):PRINT#1,MID$(STR$(CA(E)),2):RETURN
57996 'CARTAS
58000 IFC(P,0)=0THENLINE(0,150)-(120,211),0,BF:GOSUB19150:PRESET(191,6):COLOR,14:PRINT#1,"NO TIENES CARTAS":FORN=1TO2000:NEXT:RETURNELSES=0:FORI=1TO3:S(I)=0:NEXT:AL=0:IFC(P,0)>5THENAT=5ELSEAT=0
58005 FORI=7TO14:COLOR=(I,0,0,0):NEXT:LINE(1,1)-(510,17),0,BF:N=0:GOSUB59900:FORI=1TO1+102*(A-1)STEP102:N=N+1:COLOR=(N+6,0,0,0):LINE(I,0)-(I+100+INT(I/350),18),15,B:LINE(I+1,1)-(I+99+INT(I/350),17),N+6,BF:GOSUB59000
58010 A=INT((102-LEN(A$)*8)/2+1):B=INT((102-LEN(B$)*8)/2+1):COLOR,N+6:PRESET(B+I,2):PRINT#1,B$:PRESET(A+I,10):PRINT#1,A$:NEXT
58020 IFR=0THENGOSUB59050:RETURN
58030 LINE(0,165)-(106,179),15,B:LINE(1,166)-(105,178),12,BF:COLOR,12:PRESET(5,169):PRINT#1,"INTERCAMBIAR"
58040 LINE(0,181)-(106,195),15,B:LINE(1,182)-(105,194),13,BF:COLOR,13:PRESET(5,185):PRINT#1,"OTRAS CARTAS"
58050 LINE(0,197)-(106,211),15,B:LINE(1,198)-(105,210),14,BF:COLOR,14:PRESET(33,201):PRINT#1,"SALIR":GOSUB59920:GOTO59600
58065 IFK=5THEN58070ELSE58120
58070 IFS(1)=C+ALTHENS(1)=S(2):S(2)=S(3):S(3)=0:S=S-1:GOSUB58115:COLOR=(C+6,5,0,0):GOTO59610
58080 IFS(2)=C+ALTHENS(2)=S(3):S(3)=0:S=S-1:GOSUB58115:COLOR=(C+6,5,0,0):GOTO59610
58090 IFS(3)=C+ALTHENS(3)=0:S=S-1:GOSUB58115:COLOR=(C+6,5,0,0):GOTO59610
58100 IFS=3THENGOSUB58115:COLOR=(C+6,5,0,0):GOTO59610
58110 S=S+1:S(S)=C+AL:FORN=1TO100:NEXT:GOTO59610
58115 FORV=4TO0STEP-1:COLOR=(C+6,V,0,0):FORN=1TO5:NEXTN,V:FORN=1TO100:NEXT:RETURN
58120 ONCGOTO58130,58200,58210
58130 IFS<>3THEN59610ELSE C1=CD(C(P,S(1))):C2=CD(C(P,S(2))):C3=CD(C(P,S(3)))
58131 IFC3>C2THENSWAPC2,C3
58132 IFC2>C1THENSWAPC1,C2
58133 IFC3>C2THENSWAPC2,C3
58134 IFC1<>4THEN58135ELSE58141
58135 IFC1=C2ANDC2=C3THEN58138
58136 IFC1<>C2ANDC2<>C3ANDC1<>C3THENTR=10:T3=T3+10:GOTO58150
58137 GOTO59610
58138 IFC2=1THENTR=8:T3=T3+8:GOTO58150
58139 IFC2=2THENTR=6:T3=T3+6:GOTO58150
58140 IFC2=3THENTR=4:T3=T3+4:GOTO58150
58141 IFC2=4THENTR=10:T3=T3+10:GOTO58150
58142 IFC2=C3THEN58138
58143 IFC2<>C3THENTR=10:T3=T3+10:GOTO58150
58150 C(P,0)=C(P,0)-3:GOSUB19150:PRESET(163,6):COLOR,14:PRINT#1,"TE HAS GANADO";TR;"EJERCITOS":FORN=1TO2000:NEXT:GOSUB59970:FORN=1TO3:VA(C(P,S(N)))=0:C(P,S(N))=-1:NEXT:GOSUB59960:GOTO58000
58200 IFAT=0THENGOTO59610ELSEAL=AT-AL:GOTO58005
58210 IFC(P,0)>RR+4THEN59610ELSELINE(0,150)-(110,211),0,BF:IFR=2THENRETURN400ELSEFORN=7TO14:COLOR=(N,0,0,0):NEXT:RETURN
58900 GOTO59050
59000 A$=CD$(C(P,N+AL)):B=CD(C(P,N+AL)):ONBGOSUB59010,59020,59030,59040:RETURN
59010 B$="CABALLERO":RETURN
59020 B$="SOLDADO":RETURN
59030 B$="CA•ON":RETURN
59040 B$="COMODIN":RETURN
59050 IFSTRIG(0)=0ANDSTRIG(1)=0ANDSTRIG(2)=0THEN59050ELSERETURN
59070 IFC<6THENS(L)=C
59500 IFC(P,0)<3THENQ=0:RETURN
59600 GOSUB59900:I=3:J=7:K=5:H=1:Q=0:COLOR=(7,5,0,0)
59601 B=1:C=1
59610 T=STICK(0)ORSTICK(1)ORSTICK(2):ST=STRIG(0)ORSTRIG(1)ORSTRIG(2)
59620 IFT=ITHENB=B+1
59630 IFT=JTHENB=B-1
59635 IFT=KORT=HTHENIFK=5THENGOSUB65521:COLOR=(12,5,0,0):I=5:J=1:K=3:H=7:A=3:Q=5:GOTO59601ELSEGOSUB65521:FORN=12TO14:COLOR=(N,0,0,0):NEXT:GOTO59600
59640 IFB<1THENB=A
59650 IFB>ATHENB=1
59660 IFB<>CTHENGOSUB59690
59670 IFST=-1THEN58065
59680 GOTO59610
59690 IFK=3OR(K=5ANDS(1)<>C+ALANDS(2)<>C+ALANDS(3)<>C+AL)THENL1=1ELSEL1=0
59691 IFK=3OR(K=5ANDS(1)<>B+ALANDS(2)<>B+ALANDS(3)<>B+AL)THENL2=1ELSEL2=0
59693 FORV=4TO0STEP-1:Z=ABS(5-V)
59694 IFL1=1THENCOLOR=(C+Q+6,V,0,0)
59695 IFL2=1THENCOLOR=(B+Q+6,Z,0,0)
59696 NEXT:C=B:RETURN
59900 IFAT=5THENIFAL=5THENA=C(P,0)-5ELSEA=5ELSEA=C(P,0)
59910 RETURN
59920 IFS(1)>0THENIFS(1)<6ANDAL=0THENCOLOR=(S(1)+6,5,0,0)ELSEIFS(1)>6ANDAL=5THENCOLOR=(S(1)+1,5,0,0)
59930 IFS(2)>0THENIFS(2)<6ANDAL=0THENCOLOR=(S(2)+6,5,0,0)ELSEIFS(2)>6ANDAL=5THENCOLOR=(S(2)+1,5,0,0)
59940 IFS(3)>0THENIFS(3)<6ANDAL=0THENCOLOR=(S(3)+6,5,0,0)ELSEIFS(3)>6ANDAL=5THENCOLOR=(S(3)+1,5,0,0)
59950 RETURN
59960 N=0
59961 N=N+1:IFC(P,N)=-1THENFORL=N+1TO10:C(P,L-1)=C(P,L):NEXT:N=N-1
59962 IFN=10THENRETURNELSE59961
59970 FORN=1TO3:GOSUB19150
59975 CO=C(P,S(N)):IFCO>42THEN59981ELSECB=CB(CO)
59980 IFCB=PTHENPRESET(INT((511-(27+LEN(C$(CO)))*8)/2),6):COLOR,14:PRINT#1,C$(CO);" SE HA GANADO DOS EJERCITOS":CA(CO)=CA(CO)+2:COLOR15,P:L=LEN(STR$(CA(CO))):PRESET(CX(CO)-(L-2)*4,CY(CO)):PRINT#1,MID$(STR$(CA(CO)),2):FORL=1TO1000:NEXT
59981 NEXT:RETURN
59998 'STATUS
60000 E=PL*10+5:COLOR15,14:LINE(0,211-E)-(140,211),15,B:LINE(1,212-E)-(139,210),14,BF:FORN=1TOPL:PRESET(6,215-E+(N-1)*10):PRINT#1,PL$(N);:COLORN:PRINT#1," €€ ";:COLOR15:PRINT#1,MID$(STR$(T(N)),2)
60010 NEXT:GOSUB59050:LINE(0,211-E)-(140,211),0,BF:RETURN
61000 FORI=1TO42
61010 IFCB(I)=NTHENAR(N)=AR(N)+CA(I)
61020 NEXT:RETURN
65001 ' MENU DE OPCIONES
65010 B=1:C=1:GOSUB65520
65011 COLOR=(C+7,5,0,0):LINE(1,1)-(67,17),8,BF:LINE(69,1)-(135,17),9,BF:LINE(137,1)-(219,17),10,BF:LINE(221,1)-(370,17),11,BF:LINE(372,1)-(510,17),12,BF:LINE(68,0)-(136,18),15,B:LINE(220,0)-(371,18),15,B:PRESET(10,6):COLOR,8:PRINT#1,"CARGAR"
65020 COLOR,9:PRESET(78,6):PRINT#1,"GRABAR":COLOR,10:PRESET(146,6):PRINT#1,"50/60 Hz":COLOR,11:PRESET(229,6):PRINT#1,"ABANDONAR PARTIDA":PRESET(379,6):COLOR,12:PRINT#1,"VOLVER A PARTIDA"
65025 A=5:COLOR=(C+7,5,0,0):GOSUB65501
65030 U=C:ONCGOSUB65040,65040,65100,65110,65120:IFR=1THENCOLOR=(12,0,0,0):RETURN320ELSEIFC=3THEN65025ELSECOLOR=(C+7,0,0,0):GOTO65011
65040 R=0:GOSUB65524:IFC=2THENC=U:B=U:IFU=1THENCOLOR=(9,0,0,0):RETURNELSERETURNELSELL=U:GOSUB11000:C=U:B=U:IFU=1THENCOLOR=(9,0,0,0):RETURNELSECOLOR=(8,0,0,0):RETURN
65100 R=0:V=VDP(10):IFV=128THENVDP(10)=130:FORN=1TO200:NEXT:RETURNELSEVDP(10)=128:FORN=1TO300:NEXT:RETURN
65110 R=0:GOSUB65524:IFC=2THENC=4:B=4:COLOR=(9,0,0,0):RETURNELSELINE(180,182)-(360,211),0,BF:GOSUB19150:COLOR,14:PRESET(139,6):PRINT#1,"CARGANDO INICIO DE PARTIDA...":FORI=1TO6:COLOR=(I,7,7,7):NEXT:COLOR=(13,7,7,7)
65111 LINE(180,182)-(360,211),0,BF:FORI=1TO3:PAINT(CX(I),CY(I)),13,0:NEXT:CB(3)=13:GOSUB65518
65112 FORI=4TO42:PAINT(CX(I),CY(I)),13,0:NEXT:FORI=1TO6:COLOR=(I,0,0,0):NEXT
65113 Q=1:ONERRORGOTO10990:SETPAGE0,1:BLOAD"RISK.BIN":DEFUSR=&HC001:PRINTUSR("RISK3.SCR"):SETPAGE0,0:POKE&HF346,1:RUN"RISK3.LDR"
65120 R=1:B=3:C=3:RETURN
65496 'MOV.MENU
65500 B=1:C=1
65501 S=STICK(0)ORSTICK(1)ORSTICK(2):ST=STRIG(0)ORSTRIG(1)ORSTRIG(2)
65502 IFS=3THENB=B+1
65503 IFS=7THENB=B-1
65504 IFB<1THENB=A
65505 IFB>ATHENB=1
65506 IFB<>CTHENGOSUB65511
65509 IFST=-1THENRETURN
65510 GOTO65501
65511 FORI=4TO0STEP-1:J=ABS(5-I):COLOR=(C+7,I,0,0):COLOR=(B+7,J,0,0):NEXT
65513 C=B:RETURN
65515 'SUBRUT.
65518 PAINT(137,27),CB(3),0:PAINT(113,48),CB(3),0:RETURN
65519 FORZ=7TO14:COLOR=(Z,0,0,0):NEXT:RETURN
65520 FORZ=7TO14:COLOR=(Z,0,0,0):NEXT:COLOR=(8,5,0,0):RETURN
65521 FORZ=7TO11:IFS(1)<>Z-6+ALANDS(2)<>Z-6+ALANDS(3)<>Z-6+ALTHENCOLOR=(Z,0,0,0):NEXTELSENEXT
65522 RETURN
65523 'SI/NO
65524 GOSUB65520:LINE(1,1)-(255,17),8,BF:LINE(256,1)-(256,17),15:LINE(257,1)-(510,17),9,BF:PRESET(120,6):COLOR,8:PRINT#1,"SI":COLOR,9:PRESET(376,6):PRINT#1,"NO":A=2:GOSUB65500:RETURN
