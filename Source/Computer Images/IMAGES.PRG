10 ONERRORGOTO2280:SCREEN4:COLOR15,0,0:CLS:OPEN"GRP:"AS1:COLOR=(15,0,0,0)
20 _TURBO ON
30 _TURBO OFF
40 ONERRORGOTO0:FORA=49TO48STEP-1:PRESET(A,92):PRINT#1,"ILLUMINATUS SOFTWARE":NEXT
50 FORA=1TO7:COLOR=(15,0,0,A):FORB=1TO20:NEXT:NEXT:FORA=1TO1000:NEXT:FORA=6TO0STEP-1:COLOR=(15,0,0,A):FORB=1TO20:NEXT:NEXT:CLEAR
60 SCREEN7:CLOSE:OPEN"GRP:"AS1:FORA=1TO15:COLOR=(A,0,0,0):NEXT:B=7
70 DRAW"BM256,50C15D120L120E120F120L180":LINE-(256,50),15:LINE-(316,170),15:LINE(196,170)-(256,50),15
80 PAINT(259,58),3,15:PAINT(255,55),5,15:PAINT(263,58),7,15:PAINT(249,58),7,15:LINE(200,80)-(300,90),0,BF:LINE(226,80)-(286,80),15:LINE(216,90)-(296,90),15:PAINT(257,63),2,15:PAINT(255,63),4,15:PAINT(290,100),6,15:PAINT(222,100),6,15
90 COLOR15:PRESET(176,190):PRINT#1,"NOVUS ORDO SECULORUM":PRESET(196,5):PRINT#1,"COMPUTER IMAGES":PRESET(176,20):PRINT#1,"PROGRAMACION: ALTAIR":PRESET(140,35):PRINT#1,"(C) ILLUMINATUS SOFTWARE 1992"
100 FORA=1TO7:IFA=7THENE=1
110 IFA>4THEND=D+1
120 IFA>2THENC=C+1
130 COLOR=(2,0,A,0):COLOR=(3,0,A,0):COLOR=(4,0,0,A):COLOR=(5,0,0,A):COLOR=(6,A,0,0):COLOR=(7,A,0,0):COLOR=(15,E,D,C):NEXT
140 RESTORE200:ONINTERVAL=10GOSUB180:INTERVALON:GOTO150
150 FORA=1TO6:COLOR=(2,0,B,0):COLOR=(3,0,A,0):COLOR=(4,0,0,B):COLOR=(5,0,0,A):COLOR=(6,B,0,0):COLOR=(7,A,0,0):B=B-1:NEXT
160 FORA=7TO1STEP-1:COLOR=(2,0,B,0):COLOR=(3,0,A,0):COLOR=(4,0,0,B):COLOR=(5,0,0,A):COLOR=(6,B,0,0):COLOR=(7,A,0,0):B=B+1:NEXT:B=B-1:GOTO150
170 IFA$<>CHR$(24)THEN210ELSEONINTERVAL=10GOSUB180:V=0:B=7:INTERVALON:RESTORE200:GOTO150
180 A$=INKEY$:IFA$=""THENRETURNELSEREADB$,C$:IFA$<>B$ANDA$<>C$THEN210ELSEV=V+1
190 IFV=6THENQQ=1:H=0:GOTO2320ELSERETURN
200 DATAD,d,E,e,S,s,I,i,R,r,E,e
210 IFH<>0THEN220ELSESCREEN4:CLEAR:DEFINTA-E,I,X-Z:DIMA(40),B(40),D(40),E(40),I(1),Z(10,36),A$(15):OPEN"GRP:"AS1:FORA=0TO15:COLOR=(A,0,0,0):NEXT:GOTO230
220 SCREEN4:CLOSE:OPEN"GRP:"AS1:FORA=0TO15:COLOR=(A,0,0,0):NEXT
230 LINE(0,0)-(255,191),15,B:LINE(1,1)-(254,190),15,B
240 FORA=25TO24STEP-1:COLOR1:PRESET(A,75):PRINT#1,"1- VER INSTRUCCIONES"
250 ONERRORGOTO260:ERASER:ERASER$:ERASESC:GOTO270
260 RESUME270
270 ONERRORGOTO0:COLOR2:PRESET(A,95):PRINT#1,"2- VER TABLA DE RECORDS"
280 COLOR3:PRESET(A,115):PRINT#1,"3- JUGAR"
290 COLOR4:PRESET(A,135):PRINT#1,"4- VOLVER A LA PORTADA"
300 COLOR5:PRESET(A,155):PRINT#1,"5- VOLVER AL BASIC"
310 COLOR6:PRESET(A+56,35):PRINT#1,"ELIGE OPCION":NEXT:DRAW"BM76,32D12R101U12L101"
320 IFX$=""THENX=0:FORA=1TO7:FORB=1TO5:COLOR=(B,0,0,A):NEXT:COLOR=(6,A,0,0):COLOR=(15,0,A,0):IFA<3THENX=X+1:COLOR=(0,0,0,X):NEXTELSENEXT:GOTO340
330 FORA=3TO7:FORB=1TO5:COLOR=(B,0,0,A):NEXT:COLOR=(6,A,0,0):FORI=1TO15:NEXT:NEXT
340 A$=INPUT$(1):A=VAL(A$):IFA<1ORA>5THENIFA$=CHR$(27)THENRUN"MENU.PRG"ELSE340
350 COLOR=(A,7,0,0):FORI=1TO500:NEXT:B$="":ONAGOTO380,2770,390,360,370
360 LINE(5,20)-(250,180),0,BF:L=2:FORI=6TO0STEP-1:COLOR=(6,I,0,0):COLOR=(15,0,I,0):IFI>4THENL=L-1:COLOR=(0,0,0,L):NEXTELSENEXT:GOTO60
370 LINE(5,20)-(250,180),0,BF:L=2:FORI=6TO0STEP-1:COLOR=(6,I,0,0):COLOR=(15,0,I,0):IFI>4THENL=L-1:COLOR=(0,0,0,L):NEXTELSENEXT:SCREEN0:COLOR15,0,0:COLOR=NEW:END
380 GOSUB1830:GOTO220
390 LINE(5,10)-(253,189),0,BF:L=2:FORA=6TO0STEP-1:COLOR=(15,0,A,0):COLOR=(7,0,A,0):IFA>5THENL=L-1:COLOR=(0,0,0,L):NEXTELSENEXT
400 SCREEN7,0,0:CLS:COLOR15,0,0:FORA=1TO15:COLOR=(A,0,0,0):NEXT
405 RESTORE680:FORI=1TO8:READX$:S$=S$+CHR$(VAL("&H"+X$)):NEXT:SPRITE$(0)=S$
410 _TURBO ON
420 B=0:C=0:D=0:E=0:FORA=0TO512STEP8:LINE(256,0)-(A,212),1:NEXTA:FORA=256TO0STEP-6:LINE(512,212)-(A,B),2:B=B+5:NEXTA:FORA=256TO512STEP6:LINE(0,212)-(A,C),2:C=C+5:NEXTA
430 FORA=256TO0STEP-6:LINE(256,212)-(A,D),3:D=D+5:NEXTA:FORA=256TO512STEP6:LINE(256,212)-(A,E),3:E=E+5:NEXTA
440 _TURBO OFF
450 RESTORE470:FORA=10TO50STEP10:READA$:PRESET(23,A):PRINT#1,A$:NEXT:FORA=10TO50STEP10:READA$:PRESET(409,A):PRINT#1,A$:NEXT:X=1:Y=9
460 PUTSPRITE0,(X,Y),15,0:FORA=1TO7:COLOR=(1,0,0,A):COLOR=(2,A,0,0):COLOR=(3,0,A,0):COLOR=(15,A,A,A):NEXT
470 DATA"FEEDBACK","ANCIENT YS VANISHED OMEN","HYDLIDE","LIVINGSTONE SUPONGO","TETRIS #0","TETRIS #1","TETRIS #2","TETRIS #3","TETRIS #4","TETRIS #5"
480 S=STICK(0)ORSTICK(1)ORSTICK(2):P=STRIG(0)ORSTRIG(1)ORSTRIG(2)
490 IFINKEY$=CHR$(27)THENFORI=6TO0STEP-1:COLOR=(1,0,0,I):COLOR=(2,I,0,0):COLOR=(3,0,I,0):COLOR=(15,I,I,I):NEXT:X$="":GOTO220
500 IFS=5THENY=Y+10
510 IFS=3ANDX=1THENX=450
520 IFS=1THENY=Y-10
530 IFS=7ANDX=450THENX=1
540 IFY<9THENY=49
550 IFY>49THENY=9
560 PUTSPRITE0,(X,Y),15,0:IFP=-1THENIFX>300THENA=(Y+1)/10+5ELSEA=(Y+1)/10ELSE480
570 FORI=6TO0STEP-1:COLOR=(1,0,0,I):COLOR=(2,I,0,0):COLOR=(3,0,I,0):COLOR=(15,I,I,I):NEXT:H=A:ONAGOTO580,590,600,610,620,630,640,650,660,670
580 C$="FEED":B$="FEEDBACK":GOTO710
590 C$="YS1-":B$="YS1":GOTO710
600 C$="HYD-":B$="HYDLIDE":GOTO710
610 C$="LIV-":B$="LIVINGST":GOTO710
620 C$="TET0":B$="TETRIS0":GOTO710
630 C$="TET1":B$="TETRIS1":GOTO710
640 C$="TET2":B$="TETRIS2":GOTO710
650 C$="TET3":B$="TETRIS3":GOTO710
660 C$="TET4":B$="TETRIS4":GOTO710
670 C$="TET5":B$="TETRIS5":GOTO710
680 DATA8,C,FE,FF,FE,C,8,0
690 CLS:SCREEN4:CLOSE:OPEN"GRP:"AS1:COLOR15,0,0:CLS:COLOR=(15,0,0,0):FORI=73TO72STEP-1:PRESET(I,92):PRINT#1,"ERROR EN DISCO":NEXT:FORI=1TO7:COLOR=(15,I,0,0):FORA=1TO20:NEXT:NEXT:FORI=1TO750:NEXT:FORI=6TO0STEP-1:COLOR=(15,I,0,0):FORA=1TO20:NEXT:NEXT
700 RESUME400
710 SCREEN4:FORI=0TO15:COLOR=(I,0,0,0):NEXT
720 LINE(0,0)-(255,191),7,B:LINE(1,1)-(254,190),7,B
730 FORA=10TO9STEP-1:COLOR1:PRESET(A,90):PRINT#1,"1- INICIAR PARTIDA NUEVA"
740 COLOR2:PRESET(A,110):PRINT#1,"2- CARGAR PARTIDA DE DISCO"
750 COLOR3:PRESET(A,130):PRINT#1,"3- VOLVER AL MENU DE PANTALLAS"
760 COLOR5:PRESET(A+71,35):PRINT#1,"ELIGE OPCION":NEXT:DRAW"BM76,32D12R101U12L101"
770 L=0:FORA=1TO7:FORB=1TO3:COLOR=(B,0,0,A):NEXT:COLOR=(5,A,0,0):COLOR=(7,0,A,0):IFA<3THENL=L+1:COLOR=(0,0,0,L):NEXTELSENEXT
780 A$=INPUT$(1):A=VAL(A$):IFA<1ORA>3THEN780ELSECOLOR=(A,7,0,0):FORI=1TO500:NEXT:ONAGOTO860,790,390
790 ONERRORGOTO820:BLOADC$+"SVG.BIN":ONERRORGOTO0:LINE(5,10)-(253,189),0,BF:L=2:FORI=7TO0STEP-1:COLOR=(5,I,0,0):COLOR=(7,0,I,0):IFI>5THENL=L-1:COLOR=(0,0,0,L):NEXTELSENEXT
800 FORI=1TO36:A(I)=PEEK(49999!+I):B(I)=I:NEXT:B(36)=0:FORI=1TO36:IFA(I)=40THENA(I)=6:NEXTELSENEXT
810 GOSUB1450:GOTO1080
820 IFERR=53THENE=16:E$="NO EXISTEN PARTIDAS GRABADAS"ELSEE=88:E$="DISK ERROR"
830 COLOR=(13,0,0,2):COLOR=(0,0,0,2):FORA=ETOE-1STEP-1:COLOR13:PRESET(A,160):PRINT#1,E$:NEXT:FORI=3TO7:COLOR=(13,0,0,I):FORN=1TO20:NEXT:NEXT:FORI=1TO1000:NEXT:LINE(5,80)-(250,180),0,BF:FORI=1TO3:COLOR=(I,0,0,2):NEXT
840 FORA=10TO9STEP-1:COLOR1:PRESET(A,90):PRINT#1,"1- INICIAR PARTIDA NUEVA":COLOR2:PRESET(A,110):PRINT#1,"2- CARGAR PARTIDA DE DISCO":PRESET(A,130):COLOR3:PRINT#1,"3- VOLVER AL MENU DE PANTALLAS":NEXT
850 FORI=3TO7:FORN=1TO3:COLOR=(N,0,0,I):NEXT:FORN=1TO15:NEXT:NEXT:RESUME780
860 LINE(5,10)-(250,180),0,BF:L=2:FORI=7TO0STEP-1:COLOR=(5,I,0,0):COLOR=(7,0,I,0):IFI>5THENL=L-1:COLOR=(0,0,0,L):NEXTELSENEXT
870 ONERRORGOTO880:SCREEN8:SETPAGE0,1:CLS:BLOADB$+".PIC",S:ONERRORGOTO0:GOTO890
871 REM ONERRORGOTO880:SCREEN8:SETPAGE0,1:CLS:DEFUSR=&HC001:PRINTUSR(B$+".SCR"):ONERRORGOTO0:GOTO890
880 SCREEN4:COLOR15,0,0:CLS:COLOR=(15,0,0,0):FORI=88TO87STEP-1:PRESET(I,92):PRINT#1,"DISK ERROR":NEXT:FORI=1TO7:COLOR=(15,0,0,I):FORN=1TO20:NEXT:NEXT:FORI=1TO2000:NEXT:FORI=6TO0STEP-1:COLOR=(15,0,0,I):FORN=1TO20:NEXT:NEXT:RESUME710
890 _TURBO ON (A(),B())
900 FORI=3TO255STEP42:FORN=2TO211STEP35:LINE(I,N)-(I+40,N+33),255,B:NEXTN:NEXTI
910 FORI=2TO254STEP42:LINE(I,0)-(I,256),0:NEXTI:FORI=1TO176STEP35:LINE(0,I)-(255,I),0:NEXTI:FORI=0TO1:LINE(0,I)-(255,I),0:LINE(I,0)-(I,211),0:LINE(254+I,0)-(254+I,211),0:NEXTI:LINE(0,211)-(255,211),0:LINE(212,177)-(253,211),0,BF
920 FORI=1TO35:B(I)=I:NEXTI:B(36)=0
930 _TURBO OFF
940 FORI=1TO36:E(I)=0:D(I)=0:NEXT
950 RESTORE1030:FORI=1TO36:READR
960 N=INT(RND(-TIME)*36)+1:IFE(N)=0THENE(N)=R:NEXTELSE960
970 RESTORE1030:FORI=1TO36:READR
980 N=INT(RND(-TIME)*36)+1:IFD(N)=0THEND(N)=R:NEXTELSE980
990 _TURBO ON (A(),D(),E())
1000 FORI=1TO36:A(I)=D(E(I)):NEXTI
1010 _TURBO OFF
1020 GOTO1040
1030 DATA1,2,3,4,5,6,7,8,9,10,11,12,13,14,15,16,17,18,19,20,21,22,23,24,25,26,27,28,29,30,31,32,33,34,35,36
1040 _TURBO ON(A(),B())
1050 V=0:SETPAGE0,0:CLS:FORN=1TO6:FORI=1TO6:V=V+1:X=INT((A(V)/6-INT((A(V)-1)/6))*6+.1):Y=INT((A(V)-1)/6)
1060 COPY((X-1)*42+2,Y*35+2)-((X-1)*42+43,Y*35+36),1TO(2+(I-1)*42,(N-1)*35+2),0:NEXTI:NEXTN
1070 _TURBO OFF
1080 ONERRORGOTO0:GOSUB1310
1090 I=0:P=0:SETPAGE0,0
1100 I=I+1:IFA(I)=36ORA(I)=0THENA(I)=0:X=INT(((I+6)/6-INT((I+5)/6))*6+.4):Y=INT((I-1)/6):X=(X-1)*42+2:Y=Y*35+2ELSE1100
1110 ONKEYGOSUB1230,1250,1240,1270,1300:FORI=1TO5:KEY(I)ON:NEXT
1120 S=STICK(0)ORSTICK(1)ORSTICK(2):F=STRIG(0)ORSTRIG(1)ORSTRIG(2)
1130 IFS=1THENIFY<160THENGOSUB1530:YY=INT((Y-2)/35):XX=INT((X+40)/42)-1:NN=6*YY+XX+1:A(NN)=A(NN+6):A(NN+6)=0:Y=Y+35:GOSUB1210
1140 IFS=3THENIFX>30THENGOSUB1570:YY=INT((Y-2)/35):XX=INT((X+40)/42)-1:NN=6*YY+XX+1:A(NN)=A(NN-1):A(NN-1)=0:X=X-42:GOSUB1210
1150 IFS=5THENIFY>30THENGOSUB1610:YY=INT((Y-2)/35):XX=INT((X+40)/42)-1:NN=6*YY+XX+1:A(NN)=A(NN-6):A(NN-6)=0:Y=Y-35:GOSUB1210
1160 IFS=7THENIFX<200THENGOSUB1650:YY=INT((Y-2)/35):XX=INT((X+40)/42)-1:NN=6*YY+XX+1:A(NN)=A(NN+1):A(NN+1)=0:X=X+42:GOSUB1210
1170 IFF=-1THENSETPAGE1:FORI=1TO300:NEXT:GOTO1190
1180 GOTO1120
1190 FORI=1TO5:KEY(I)OFF:NEXT
1200 F=STRIG(0)ORSTRIG(1)ORSTRIG(2):IFF=-1THENSETPAGE0,0:FORI=1TO300:NEXT:FORI=1TO5:KEY(I)ON:NEXT:GOTO1120ELSE1200
1210 FORI=1TO36:IFA(I)=B(I)THENNEXTI:GOTO2320
1220 IFA(I)=40ORA(I)=6THENNEXTIELSERETURN
1230 P$="SAVE":GOSUB1700:IFI=2THENRETURNELSEONERRORGOTO1690:FORI=1TO36:POKE49999!+I,A(I):NEXT:BSAVEC$+"SVG.BIN",50000!,50035!,50000!:ONERRORGOTO0:RETURN
1240 P$="MSAVE":GOSUB1700:IFI=2THENRETURNELSEFORI=1TO36:Z(H,I)=A(I):NEXT:RETURN
1250 P$="LOAD":GOSUB1700:IFI=2THENRETURNELSEONERRORGOTO1690:BLOADC$+"SVG.BIN":FORI=1TO36:A(I)=PEEK(49999!+I):B(I)=I:NEXT:B(36)=0:ONERRORGOTO0:FORI=1TO36:IFA(I)=40THENA(I)=6:NEXTELSENEXT
1260 GOTO1040
1270 P$="MLOAD":GOSUB1700:IFI=2THENRETURNELSEIFZ(H,1)=0ANDZ(H,2)=0THENCOLOR255:PRESET(X+3,Y+12):PRINT#1,"ERROR":FORI=1TO750:NEXT:LINE(X,Y)-(X+42,Y+34),0,BF:RETURNELSEFORI=1TO36:A(I)=Z(H,I):NEXT
1280 IFB$="TETRIS0"THENI(1)=22:GOSUB1730ELSEIFB$="TETRIS5"THENI(1)=6:GOSUB1780
1290 GOTO1040
1300 P$="QUIT":GOSUB1700:IFI=2THENRETURNELSEX$="":GOTO220
1310 IFB$="TETRIS0"THENI(1)=22:GOTO1320ELSEIFB$="TETRIS5"THENI(1)=6:GOTO1380ELSERETURN
1320 _TURBO ON (A(),B(),I())
1330 RESTORE1370:FORN=1TOI(1):READD:FORT=1TO36:IFA(T)=DTHENA(T)=40
1340 NEXTT:NEXTN
1350 _TURBO OFF
1360 RETURN
1370 DATA1,2,3,4,5,6,7,8,10,11,12,13,17,18,19,23,24,25,29,30,31,35
1380 _TURBO ON (A(),B(),I())
1390 RESTORE1430:FORN=1TOI(1):READD:FORT=1TO36:IFA(T)=DTHENA(T)=40
1400 NEXTT:NEXTN:A(39)=D
1410 _TURBO OFF
1420 RETURN
1430 DATA3,4,6,9,12,15
1440 RETURN
1450 ONERRORGOTO690:SCREEN8:SETPAGE0,1:CLS:BLOADB$+".PIC",S:ONERRORGOTO0
1460 _TURBO ON (A(),B())
1470 FORI=3TO255STEP42:FORN=2TO211STEP35:LINE(I,N)-(I+40,N+33),255,B:NEXTN:NEXTI
1480 FORI=2TO254STEP42:LINE(I,0)-(I,256),0:NEXTI:FORI=1TO176STEP35:LINE(0,I)-(255,I),0:NEXTI:FORI=0TO1:LINE(0,I)-(255,I),0:LINE(I,0)-(I,211),0:LINE(254+I,0)-(254+I,211),0:NEXTI:LINE(0,211)-(255,211),0:LINE(212,177)-(253,211),0,BF
1490 SETPAGE0,0:CLS:FORN=1TO6:FORI=1TO6:V=V+1:X=INT((A(V)/6-INT((A(V)-1)/6))*6+.4):Y=INT((A(V)-1)/6)
1500 COPY((X-1)*42+2,Y*35+2)-((X-1)*42+43,Y*35+36),1TO(2+(I-1)*42,(N-1)*35+2),0:NEXTI:NEXTN
1510 _TURBO OFF
1520 RETURN
1530 _TURBO ON (X,Y)
1540 FORI=34TO0STEP-1:COPY(X,Y+I)-(X+42,Y+I+35),0TO(X,Y+I-1),0:NEXTI
1550 _TURBO OFF
1560 RETURN
1570 _TURBO ON (X,Y)
1580 FORI=0TO-41STEP-1:COPY(X-I,Y)-(X-I-42,Y+34),0TO(X-I+1,Y),0:NEXTI
1590 _TURBO OFF
1600 RETURN
1610 _TURBO ON (X,Y)
1620 FORI=71TO37STEP-1:COPY(X,Y-I+70)-(X+42,Y-I+35),0TO(X,Y-I+71),0:NEXTI
1630 _TURBO OFF
1640 RETURN
1650 _TURBO ON (X,Y)
1660 FORI=42TO1STEP-1:COPY(X+I,Y)-(X+I+42,Y+34),0TO(X+I-1,Y),0:NEXTI
1670 _TURBO OFF
1680 RETURN
1690 PRESET(X+3,Y+3):COLOR255:PRINT#1,"ERROR":PRESET(X+15,Y+12):PRINT#1,"EN":PRESET(X+3,Y+21):PRINT#1,"DISCO":FORI=1TO1000:NEXT:LINE(X,Y)-(X+42,Y+34),0,BF:RESUME1110
1700 IFLEN(P$)=5THENL=3ELSEL=7
1710 PRESET(X+L,Y+3):COLOR255:PRINT#1,P$:PRESET(X+3,Y+12):PRINT#1,"1- SI":PRESET(X+3,Y+21):PRINT#1,"2- NO"
1720 I$=INPUT$(1):I=VAL(I$):IFI<1ORI>2THEN1720ELSELINE(X,Y)-(X+42,Y+34),0,BF:RETURN
1730 _TURBO ON (A(),B(),I())
1740 RESTORE1370:FORN=1TOI(1):READD:FORT=1TO36:IFA(T)=40THENA(T)=D
1750 NEXTT:NEXTN
1760 _TURBO OFF
1770 RETURN
1780 _TURBO ON (A(),B(),I())
1790 RESTORE1430:FORN=1TOI(1):READD:FORT=1TO36:IFA(T)=40THENA(T)=D
1800 NEXTT:NEXTN
1810 _TURBO OFF
1820 RETURN
1830 LINE(5,5)-(250,185),0,BF:FORI=1TO10:COLOR=(I,0,0,2):NEXT:RESTORE1870:FORI=1TO10:READX$:READX:COLORI:FORN=XTOX-1STEP-1:PRESET(N,I*18-7):PRINT#1,X$:NEXT:GOSUB2270:NEXT:A$=INPUT$(1)
1840 LINE(5,5)-(250,185),0,BF:FORI=1TO10:COLOR=(I,0,0,2):NEXT:FORI=1TO10:READX$:READX:COLORI:FORN=XTOX-1STEP-1:PRESET(N,I*18-7):PRINT#1,X$:NEXT:GOSUB2270:NEXT:A$=INPUT$(1)
1850 LINE(5,5)-(250,185),0,BF:FORI=1TO10:COLOR=(I,0,0,2):NEXT:FORI=1TO10:READX$:READX:COLORI:FORN=XTOX-1STEP-1:PRESET(N,I*18-7):PRINT#1,X$:NEXT:GOSUB2270:NEXT:A$=INPUT$(1)
1860 LINE(5,5)-(250,185),0,BF:FORI=1TO10:COLOR=(I,0,0,2):NEXT:FORI=1TO10:READX$:READX:COLORI:FORN=XTOX-1STEP-1:PRESET(N,I*18-7):PRINT#1,X$:NEXT:GOSUB2270:NEXT:A$=INPUT$(1):LINE(5,5)-(250,185),0,BF:FORI=1TO10:COLOR=(I,0,0,2):NEXT:GOTO240
1870 DATA"   COMPUTER IMAGES es un juego",9
1880 DATA"al estilo de las rompecabezas.",9
1890 DATA"El objetivo es ordenar todas",17
1900 DATA"las piezas para formar la",29
1910 DATA"imagen antes seleccionada.",25
1920 DATA"Hay diez diferentes, tomadas",17
1930 DATA"de distintos juegos de MSX2:",17
1940 DATA"digitalizaciones, pantallas de",9
1950 DATA"presentacion, gr ficos, etc.",17
1960 DATA"PULSA UNA TECLA",69
1970 DATA"   Las teclas a emplear son:",17
1980 DATA"CURSORES- mover las piezas",25
1990 DATA"SPACE- ver pantalla completada",9
2000 DATA"F1- grabar pantalla en disco",17
2010 DATA"F2- cargar pantalla de disco",17
2020 DATA"F3- grabar pantalla en memoria",9
2030 DATA"F4- cargar pantalla de memoria",9
2040 DATA"F5- abandonar y volver al menu",9
2050 DATA"Tambi‚n se puede usar JOYSTICK",9
2060 DATA"PULSA UNA TECLA",69
2070 DATA"   Y en un tono m s personal:",13
2080 DATA"Saludos a la extinta MSX CLUB",13
2090 DATA"por su gran ayuda al sistema;",13
2100 DATA"al MSX, por ser, a£n en los",21
2110 DATA"ultimos d¡as de su existencia,",9
2120 DATA"un ordenador peque¤o con m s",17
2130 DATA"personalidad que los dem s;",21
2140 DATA"a los que cambiaron de sistema",9
2150 DATA"por ser listos y despiadados;",13
2160 DATA"PULSA UNA TECLA",69
2170 DATA"a Ram¢n Mendoza, por echar al",13
2180 DATA"entrenador que iba a ganar",25
2190 DATA"la Copa, la Liga y la UEFA;",21
2200 DATA"a Vicente Miera, por ser un",21
2210 DATA"entrenador totalmente inepto;",13
2220 DATA"a Cygnus, Antares y Kitos;",25
2230 DATA"y a U2, que sin la inspiraci¢n",9
2240 DATA"de su m£sica, nunca podr¡a",25
2250 DATA"haber hecho COMPUTER IMAGES...",9
2260 DATA"PULSA UNA TECLA",69
2270 FORT=3TO7:COLOR=(I,0,0,T):FORB=1TO20:NEXT:NEXT:RETURN
2280 FORA=5TO4STEP-1:PRESET(A,83):PRINT#1,"PARA USAR ESTE PROGRAMA HAY QUE":NEXT
2290 FORA=1TO0STEP-1:PRESET(A,101):PRINT#1,"TENER ANTES CARGADO EL KUN BASIC":NEXT
2300 FORA=1TO7:COLOR=(15,0,0,A):FORB=1TO20:NEXT:NEXT:FORA=1TO1500:NEXT:FORA=6TO0STEP-1:COLOR=(15,0,0,A):FORB=1TO20:NEXT:NEXT:RESUME2310
2310 END
2320 IFQQ=1THEN2330ELSE2340
2330 FORA=0TO211STEP2:LINE(0,A)-(511,A),0:LINE(0,211-A)-(511,211-A),0:NEXT:GOTO2350
2340 FORA=0TO225STEP2:LINE(A,0)-(A,212),0:LINE(255-A,0)-(255-A,212),0:LINE(0,A)-(256,A),0:LINE(0,211-A)-(256,211-A),0:NEXT
2350 SCREEN4:COLOR=(0,0,0,0):COLOR=(1,0,0,0):COLOR1,0,0:CLS:IFQQ=1THENFORI=29TO28STEP-1:PRESET(I,92):PRINT#1,"AUNQUE HACIENDO TRAMPA...":NEXT:GOSUB2900
2360 FORI=21TO20STEP-1:PRESET(I,92):PRINT#1,"­LO HAS CONSEGUIDO POR FIN!":NEXT:GOSUB2900:FORI=53TO52STEP-1:PRESET(I,82):PRINT#1,"TU NOMBRE PERDURARA":PRESET(I-12,102):PRINT#1,"A TRAVES DE LOS SIGLOS":NEXT:GOSUB2900
2370 FORI=17TO16STEP-1:PRESET(I,82):PRINT#1,"SERA CONSERVADO PARA SIEMPRE":PRESET(I+4,102):PRINT#1,"EN NUESTRA TABLA DE RECORDS":NEXT:GOSUB2900
2380 FORI=25TO24STEP-1:PRESET(I,82):PRINT#1,"EL PREMIO A TU ESFUERZO ES":PRESET(I-4,102):PRINT#1,"EL HONOR DE FIGURAR EN ELLA":NEXT:GOSUB2900
2390 IFQQ<>1THEN2400ELSEFORI=13TO12STEP-1:PRESET(I,82):PRINT#1,"PERO POR TU FALTA DE HONRADEZ":PRESET(I-4,102):PRINT#1,"TU NOMBRE NO SERA RECORDADO...":NEXT:GOSUB2900:GOTO2510
2400 FORI=65TO64STEP-1:PRESET(I,82):PRINT#1,"TECLEA TU NOMBRE":NEXT
2410 FORI=1TO7:COLOR=(1,0,0,I):FORN=1TO20:NEXT:NEXT:FORI=1TO10:A$(I)="":NEXT:R=0
2420 S$=INPUT$(1):IFS$<CHR$(32)ANDS$<>CHR$(8)ANDS$<>CHR$(13)ORS$=CHR$(127)THEN2420
2430 IFS$=CHR$(13)THENIFR=0THEN2420ELSEIFR<10THENFORI=R+1TO10:A$(I)=CHR$(32):NEXT:GOTO2460ELSE2460
2440 IFS$=CHR$(8)THENIFR>0THENR=R-1:LINE(R*8+87,102)-(R*8+96,110),0,BF:A$(R+1)="":GOTO2420ELSE2420
2450 IFR=10THEN2420ELSEFORI=R*8+88TOR*8+87STEP-1:PRESET(I,102):PRINT#1,S$:NEXT:R=R+1:A$(R)=S$:GOTO2420
2460 ONERRORGOTO2470:BLOAD"RECORDS.DAT":A=PEEK(50000!):GOTO2480
2470 IFERR=53THENA=0:RESUME2480ELSEFORI=6TO0STEP-1:COLOR=(1,0,0,I):FORN=1TO20:NEXT:NEXT:CLS:GOTO2490
2480 ONERRORGOTO0:FORI=6TO0STEP-1:COLOR=(1,0,0,I):FORN=1TO20:NEXT:NEXT:CLS:FORI=1TO10:POKE50000!+A*11+I,ASC(A$(I)):NEXT:POKE50000!+A*11+11,H:POKE50000!,A+1:ONERRORGOTO2490:BSAVE"RECORDS.DAT",50000!,50000!+(A+1)*11,50000!:GOTO2500
2490 COLOR1:FORI=89TO88STEP-1:PRESET(I,92):PRINT#1,"DISK ERROR":NEXT:GOSUB2900:RESUME2400
2500 FORI=12TO11STEP-1:PRESET(I,92):PRINT#1,"TU NOMBRE HA SIDO REGISTRADO":NEXT:GOSUB2900
2510 SCREEN7:CLOSE:OPEN"GRP:"AS1:DEFINTA-Z:COLOR14,0,0:SETPAGE0,0:FORI=1TO15:COLOR=(I,0,0,0):NEXT:LINE(256,55)-(359,158),2:LINE-(153,158),2:LINE-(256,55),2:PAINT(256,56),2:LINE(216,87)-(296,93),0,BF
2520 LINE(217,93)-(212,90),6:LINE-(142,152),6:LINE-(152,158),6:LINE-(217,93),6:PAINT(213,94),6:LINE(213,90)-(290,90),7:LINE-(295,93),7:LINE-(218,93),7:LINE-(214,90),7:PAINT(222,92),7
2530 LINE(224,86)-(218,84),6:LINE-(255,55),6:LINE-(224,86),6:PAINT(223,82),6:LINE(255,55)-(221,85),6:B=212:FORA=98TO158STEP5:LINE(400,A)-(B,A),0:LINE(B,A)-(B-10,A-5),0:B=B-5:NEXT
2540 CIRCLE(256,77),19,9,,,.25:PAINT(256,77),9:CIRCLE(256,77),9,5:PAINT(256,77),5:CIRCLE(256,77),3,10:PAINT(256,77),10:SETPAGE0,1:COPY(0,0)-(511,211),0 TO (0,0),1
2550 LINE(256,77)-(6,130),8:LINE-(6,150),8:LINE-(256,77),8:PAINT(8,141),8:LINE(256,77)-(506,130),8:LINE-(506,150),8:LINE-(256,77),8:PAINT(504,141),8:LINE(256,77)-(6,140),8:LINE(256,77)-(506,140),8
2560 G=1:FORA=1TO7:IFA<6THENE=E+1
2570 IFA<7THENF=F+1
2580 COLOR=(5,0,0,E):COLOR=(8,F,F,0):COLOR=(9,A,A,A):COLOR=(10,A,0,0):NEXT:FORI=1TO1000:NEXT:COPY(0,0)-(511,211),1TO(0,0),0:FORI=1TO3000:NEXT:G=1:B=0:C=0:D=0:FORA=1TO7:IFA<3THENB=B+1
2590 IFA<4THENC=C+1
2600 IFA<5THEND=D+1
2610 FORI=1TO10:NEXT:COLOR=(2,A,0,0):COLOR=(3,C,A,C):COLOR=(4,0,0,B):COLOR=(6,D,G,G):COLOR=(7,C,0,0):NEXT
2620 SETPAGE0,0:COLOR14:COLOR=(14,0,0,0):A=180:RESTORE2730:FORI=77TO57STEP-4:READA$:PRESET(A,I):PRINT#1,A$:A=A+10:NEXT:A=272:FORI=55TO79STEP4:READA$:PRESET(A,I):PRINT#1,A$:A=A+10:NEXT
2630 FORI=1TO7:COLOR=(14,I,I,I):FORN=1TO25:NEXT:NEXT:COLOR=(13,0,0,0):COLOR13:PRESET(156,165):PRINT#1,"(c) ILLUMINATUS SOFT 1992":FORI=1TO100:NEXT:FORI=1TO7:COLOR=(13,I,I,I):FORN=1TO25:NEXT:NEXT:A$=INPUT$(1):G=0
2640 _TURBO ON (B,C,D,E,F,G)
2650 FORA=6TO0STEP-1:IFA>1THENE=E-1
2660 IFA>0THENF=F-1
2670 IFA>3THENC=C-1
2680 IFA<4THEND=D-1
2690 IFA>4THENB=B-1
2700 FORI=1TO1200:NEXTI:COLOR=(2,A,0,0):COLOR=(3,C,A,C):COLOR=(4,0,0,B):COLOR=(5,0,0,E):COLOR=(6,D,G,G):COLOR=(7,C,0,0):COLOR=(8,F,F,0):COLOR=(9,A,A,A):COLOR=(10,A,0,0):COLOR=(13,A,A,A):COLOR=(14,A,A,A):NEXTA
2710 _TURBO OFF
2720 X$="":H=0:GOTO210
2730 DATAA,N,N,U,I,T,C,O,E,P,T,I,S
2740 LINE(5,5)-(250,185),0,BF:FORI=1TO14:COLOR=(I,0,0,2):NEXT:COLOR1:IFERR=53THENE$="NO EXISTE TABLA DE RECORDS":E=24ELSEE$="DISK ERROR":E=88
2750 COLOR1:FORI=20TO19STEP-1:PRESET(I,92):PRINT#1,E$:NEXT:FORI=3TO7:COLOR=(1,0,0,I):FORN=1TO20:NEXT:NEXT:FORI=1TO2000:NEXT:FORI=6TO2STEP-1:COLOR=(1,0,0,I):FORN=1TO20:NEXT:NEXT:LINE(5,92)-(250,100),0,BF:COLOR=(1,0,0,2):X$="1"
2760 RESUME240
2770 DEFINTR:ONERRORGOTO2740:BLOAD"RECORDS.DAT":A=PEEK(50000!):ONERRORGOTO0:DIMR(PEEK(50000!)*11)
2780 _TURBO ON (R())
2790 FORI=1TO11*PEEK(50000!):R(I)=PEEK(50000!+I):NEXTI
2800 _TURBO OFF
2810 R(0)=A:DIMR$(A):DIMSC(A):FORI=1TOA:R$(I)="":FORN=1TO10:R$(I)=R$(I)+CHR$(R(N+(I-1)*11)):NEXT:SC(I)=R(I*11):NEXT
2820 LINE(5,5)-(250,185),0,BF:FORI=1TO14:COLOR=(I,0,0,2):NEXT:COLOR14:FORI=11TO10STEP-1:PRESET(I,5):PRINT#1,"NOMBRE           PANTALLA":NEXT:LINE(10,15)-(246,15),14
2830 FORI=3TO7:COLOR=(14,0,0,I):FORN=1TO20:NEXT:NEXT:IFR(0)>12THEN2850ELSEFORI=1TOR(0):COLORI:X=25+(I-1)*12:FORN=11TO10STEP-1:PRESET(N,X):PRINT#1,R$(I):PRESET(N+136,X):ONSC(I)GOSUB2910,2920,2930,2940,2950,2960,2970,2980,2990,3000:NEXT:X$="1"
2840 FORN=3TO7:COLOR=(I,0,0,N):FORX=1TO25:NEXT:NEXT:NEXT:COLOR13:FORI=69TO68STEP-1:PRESET(I,175):PRINT#1,"PULSA UNA TECLA":NEXT:FORI=3TO7:COLOR=(13,0,0,I):FORN=1TO25:NEXT:NEXT:A$=INPUT$(1):LINE(5,16)-(250,188),0,BF:FORI=1TO14:COLOR=(I,0,0,2):NEXT:GOTO240
2850 DEFDBLC,Q:Q=R(0)/12:C=INT(12*(Q-INT(Q))+.1):Q=INT(Q)
2860 FORQQ=1TOQ:FORI=1TO12:COLORI:X=25+(I-1)*12:FORN=11TO10STEP-1:PRESET(N,X):PRINT#1,R$(12*(QQ-1)+I):PRESET(N+136,X):ONSC(12*(QQ-1)+I)GOSUB2910,2920,2930,2940,2950,2960,2970,2980,2990,3000:NEXT:X$="1"
2870 FORN=3TO7:COLOR=(I,0,0,N):FORX=1TO25:NEXT:NEXT:NEXT:COLOR13:FORI=69TO68STEP-1:PRESET(I,175):PRINT#1,"PULSA UNA TECLA":NEXT:FORI=3TO7:COLOR=(13,0,0,I):FORN=1TO25:NEXT:NEXT:A$=INPUT$(1):LINE(5,16)-(250,188),0,BF:FORI=1TO13:COLOR=(I,0,0,2):NEXT:NEXT
2880 FORI=1TOC:COLORI:X=25+(I-1)*12:FORN=11TO10STEP-1:PRESET(N,X):PRINT#1,R$(12*Q+I):PRESET(N+136,X):ONSC(12*Q+I)GOSUB2910,2920,2930,2940,2950,2960,2970,2980,2990,3000:NEXT:X$="1"
2890 FORN=3TO7:COLOR=(I,0,0,N):FORX=1TO25:NEXT:NEXT:NEXT:COLOR13:FORI=69TO68STEP-1:PRESET(I,175):PRINT#1,"PULSA UNA TECLA":NEXT:FORI=3TO7:COLOR=(13,0,0,I):FORN=1TO25:NEXT:NEXT:A$=INPUT$(1):LINE(5,5)-(250,188),0,BF:FORI=1TO14:COLOR=(I,0,0,2):NEXT:GOTO240
2900 FORI=1TO7:COLOR=(1,0,0,I):FORN=1TO25:NEXT:NEXT:FORI=1TO2000:NEXT:FORI=6TO0STEP-1:COLOR=(1,0,0,I):FORN=1TO25:NEXT:NEXT:CLS:RETURN
2910 PRINT#1,"FEEDBACK":RETURN
2920 PRINT#1,"YS1":RETURN
2930 PRINT#1,"HYDLIDE":RETURN
2940 PRINT#1,"LIVINGSTONE":RETURN
2950 PRINT#1,"TETRIS #0":RETURN
2960 PRINT#1,"TETRIS #1":RETURN
2970 PRINT#1,"TETRIS #2":RETURN
2980 PRINT#1,"TETRIS #3":RETURN
2990 PRINT#1,"TETRIS #4":RETURN
3000 PRINT#1,"TETRIS #5":RETURN
