10 FOR I=6 TO 0 STEP-1:FOR N=1 TO 4:COLOR=(N,0,0,I):NEXT N:COLOR=(14,I,0,0):COLOR=(15,0,I,0):IF I>4 THEN COLOR=(0,0,0,I-5):NEXT I ELSE NEXT I
20 ' INITIAL CONFIGURATION
30 CLEAR 300:DEFINT A-M
40 PL=PEEK(50000!):FOR I=1 TO PL:L(I)=PEEK(50000!+I):NEXT I:FOR I=1 TO PL:FOR N=1 TO L(I):J=J+1:A=PEEK(50004!+J):J$(I)=J$(I)+CHR$(A):NEXT N,I
50 DIM C(19,19),M(4),F(4),MI(4,50,4),FI(4,50,4),SB(4,2),CO(4),BL(4,2),MV(3,2)
60 FOR I=1 TO PL:M(I)=1:F(I)=0:MI(I,0,0)=1:FI(I,0,0)=0:NEXT I
70 ' DRAW SCREEN
80 SCREEN 5,2:COLOR 14,0,0:CLS:OPEN"GRP:"AS1
90 COLOR=(0,1,1,1):COLOR=(2,7,1,1):COLOR=(3,7,3,1):COLOR=(4,1,1,7):COLOR=(5,2,3,7):COLOR=(6,6,0,0):COLOR=(7,1,3,1):COLOR=(8,1,4,1):COLOR=(9,6,7,1):COLOR=(10,4,7,1):COLOR=(12,0,0,4):COLOR=(13,4,3,5):COLOR=(14,4,4,4):COLOR=(15,5,0,0)
100 FOR N=0 TO 190 STEP 10:LINE(N,0)-(N,209),12:NEXT
110 FOR N=0 TO 209 STEP 11:LINE(0,N)-(190,N),12:NEXT
120 RESTORE 3460:X$="":FOR L=0 TO 31:READ A$:X$=X$+CHR$(VAL("&H"+A$)):NEXT:SPRITE$(0)=X$:FOR I=1 TO 4:X$="":FOR L=1 TO 8:READ A$:X$=X$+CHR$(VAL("&H"+A$)):NEXT:SPRITE$(4+I)=X$:NEXT I
130 M=1:FOR N=55 TO 11 STEP -11:PRESET(M,N):DRAW"C5R9U11":M=M+10:NEXT N:M=1
140 FOR N=154 TO 198 STEP 11:PSET(M,N):DRAW"C5R9D11":M=M+10:NEXT N:M=140
150 FOR N=0 TO 44 STEP 11:PRESET(M,N):DRAW"C5D11R9":M=M+10:NEXT N:M=140
160 FOR N=19*11 TO 15*11 STEP -11:PRESET(M,N):DRAW"C5U11R9":M=M+10:NEXT N
170 XX=100:YY=98:LINE(0,0)-(190,209),12,B
180 RESTORE 3510:FOR I=0 TO 52*(PL-1) STEP 52:PRESET(194,I):READ CO(I/52+1):COLOR CO(I/52+1):PRINT#1,J$(I/52+1):LINE(192,I+10)-(255,I+10),CO(I/52+1):NEXT I
190 ' ACCESS INITIAL SUBROUTINES
200 FOR PP=1 TO PL:MT=0:FT=0:P=PP:S=PP:GOSUB 1490:NEXT PP ' DRAW RESERVE SHIPS
210 GOSUB 630 ' PLACE ASTEROIDS
220 PP=1:QU=0:GOSUB 1440:GOSUB 820:COPY(47,86)-(152,123),2 TO (47,86),0 ' PLACE STARBASES
230 ' MAIN PROGRAM
240 COPY(192,0)-(255,211),0 TO (192,0),2
250 PP=PP+1:IF PP>PL THEN PP=1
260 IF QU(PP)=1 THEN 250
270 LINE(192,0)-(255,211),0,BF:COPY(192,(PP-1)*52)-(255,PP*52-1),2 TO (192,0),0
280 ' MENU 1
290 LINE(193,73)-(254,208),14,BF:COPY(0,0)-(63,156),3 TO (192,53),0
300 A$=INPUT$(1):A=VAL(A$):IF A<1 OR A>4 THEN 300 ELSE ON A GOTO 320,380,410,540
310 ' TAKE SHIPS OUT
320 LINE(193,73)-(254,208),14,BF:COPY(64,0)-(127,156),3 TO (192,53),0
330 A$=INPUT$(1):A=VAL(A$):IF A<1 OR A>3 THEN 330 ELSE IF A$=CHR$(27) THEN 290
340 ON A GOTO 350,360,290
350 IF M(PP)-MC>0 THEN MM=1:FF=0:GOTO 1190 ELSE GOSUB 2520:GOTO 320
360 IF F(PP)-FC>0 THEN MM=0:FF=1:GOTO 1190 ELSE GOSUB 2520:GOTO 320
370 ' MOVE SHIPS
380 GOSUB 1530:IF BB=2 THEN GOSUB 2520
390 GOTO 290
400 ' END OF TURN
410 COPY(65,79)-(127,129),0 TO (65,79),2:COPY(0,158)-(62,208),3 TO (65,79),0
420 A$=INPUT$(1)
430 IF A$="S" OR A$="s" THEN COPY(65,79)-(127,129),2 TO (65,79),0:GOTO 460
440 IF A$="N" OR A$="n" THEN COPY(65,79)-(127,129),2 TO (65,79),0:GOTO 290
450 GOTO 420
460 FOR I=1 TO MI(PP,0,0)
470 IF MI(PP,I,4)=2 THEN MI(PP,I,4)=0
480 NEXT I
490 FOR I=1 TO FI(PP,0,0)
500 IF FI(PP,I,4)=2 THEN FI(PP,I,4)=0
510 NEXT I
520 MC=0:FC=0:GOTO 250
530 ' QUIT
540 COPY(65,79)-(127,129),0 TO (65,79),2:COPY(0,158)-(62,208),3 TO (65,79),0
550 A$=INPUT$(1)
560 IF A$="S" OR A$="s" THEN COPY(65,79)-(127,129),2 TO (65,79),0:GOTO 590
570 IF A$="N" OR A$="n" THEN COPY(65,79)-(127,129),2 TO (65,79),0:GOTO 290
580 GOTO 550
590 GOSUB 3060
600 IF QU(1)+QU(2)+QU(3)+QU(4)=PL-1 THEN 3140
610 GOTO 250
620 ' PLACE ASTEROIDS
630 Y=5 '3
640 X=INT(RND(-TIME)*4):IF X1=X THEN GOTO 640 ELSE X1=X:C(X,Y)=1:TIME=TIME*RND(-TIME)
650 Y=Y+2:IF Y<16 AND Y>4 THEN 640
660 Y=0
670 X=INT(RND(-TIME)*4)+4:IF X1=X THEN GOTO 670 ELSE X1=X:C(X,Y)=1:TIME=TIME*RND(-TIME)
680 Y=Y+2:IF Y<19 THEN 670
690 Y=0
700 X=INT(RND(-TIME)*4)+8:IF X1=X THEN GOTO 700 ELSE X1=X:C(X,Y)=1:TIME=TIME*RND(-TIME) '9
710 Y=Y+2:IF Y<19 THEN 700
720 Y=3
730 X=INT(RND(-TIME)*4)+12:IF X1=X THEN GOTO 730 ELSE X1=X:C(X,Y)=1:TIME=TIME*RND(-TIME) '13
740 Y=Y+2:IF Y<19 THEN 730
750 Y=8
760 X=INT(RND(-TIME)*2)+17:IF X1=X THEN GOTO 760 ELSE X1=X:C(X,Y)=1:TIME=TIME*RND(-TIME)
770 Y=Y+4:IF Y<16 AND Y>4 THEN 760
780 FOR I=1 TO 19:FOR N=1 TO 19
790 IF C(I,N)=1 THEN COPY(0,0)-(7,7),1 TO ((I-1)*10+1,(N-1)*11+3),0
800 NEXT N,I:RETURN
810 ' PLACE STARGATE
820 IF PP=1 THEN XX=10:YY=10:MA=3.8:YV=-11
830 IF PP=2 THEN XX=170:YY=186:MA=4.8:COPY(47,86)-(152,123),2 TO (47,86),0:FOR I=1 TO 100:NEXT I:GOSUB 1440
840 IF PP=3 THEN XX=10:YY=186:MA=4.8:COPY(47,86)-(152,123),2 TO (47,86),0:FOR I=1 TO 100:NEXT I:GOSUB 1440
850 IF PP=4 THEN XX=170:YY=10:MA=3.8:COPY(47,86)-(152,123),2 TO (47,86),0:FOR I=1 TO 100:NEXT I:GOSUB 1440
860 PUT SPRITE 1,(XX,YY),11,0
870 A=STICK(0) OR STICK(1)
880 B=STRIG(0) OR STRIG(1)
890 IF B=-1 THEN GOTO 980
900 IF PP=2 THEN GOTO 1030
910 IF PP=3 THEN GOTO 1080
920 IF PP=4 THEN GOTO 1130
930 IF A=1 AND YY=>10 AND (XX/10)+((YY+YV)/11)<=MA THEN FOR Q=1 TO 11:YY=YY-1:PUT SPRITE 1,(XX,YY),11,0:NEXT:GOTO 870 ELSE 940
940 IF A=5 AND YY<=170 AND (XX/10)+(YY/11)<=MA THEN FOR Q=1 TO 11:YY=YY+1:PUT SPRITE 1,(XX,YY),11,0:NEXT:GOTO 870 ELSE 950
950 IF A=3 AND XX<=170 AND (XX/10)+(YY/11)<=MA THEN FOR Q=1 TO 10:XX=XX+1:PUT SPRITE 1,(XX,YY),11,0:NEXT:GOTO 870 ELSE 960
960 IF A=7 AND XX>=10 THEN FOR Q=1 TO 10:XX=XX-1:PUT SPRITE 1,(XX,YY),11,0:NEXT :GOTO 870
970 GOTO 870
980 I=36*PP
990 PUT SPRITE 1,(0,213),0,0:SB(PP,1)=XX/10+1:SB(PP,2)=INT(YY/11)+2
1000 C(XX/10+1,(YY+1)/11+1)=100+PP
1010 COPY(0,PP*9)-(7,PP*9+7),1 TO (XX+1,YY+3),0
1020 IF PP<PL THEN PP=PP+1:GOTO 820 ELSE RETURN
1030 IF A=1 AND YY=>10 AND (XX/10)-14+(YY/11)-14=>MA THEN FOR Q=1 TO 11:YY=YY-1:PUT SPRITE 1,(XX,YY),11,0:NEXT:GOTO 870
1040 IF A=5 AND YY<=190 THEN FOR Q=1 TO 11:YY=YY+1:PUT SPRITE 1,(XX,YY),11,0:NEXT:GOTO 870
1050 IF A=3 AND XX<=170 THEN FOR Q=1 TO 10:XX=XX+1:PUT SPRITE 1,(XX,YY),11,0:NEXT:GOTO 870
1060 IF A=7 AND XX>=10 AND (XX/10)-14+(YY/11)-14=>MA THEN FOR Q=1 TO 10:XX=XX-1:PUT SPRITE 1,(XX,YY),11,0:NEXT :GOTO 870
1070 GOTO 870
1080 IF A=1 AND YY=>10 AND 4-(XX/10)+(YY/11)-14=>MA THEN FOR Q=1 TO 11:YY=YY-1:PUT SPRITE 1,(XX,YY),11,0:NEXT:GOTO 870
1090 IF A=5 AND YY<=190 THEN FOR Q=1 TO 11:YY=YY+1:PUT SPRITE 1,(XX,YY),11,0:NEXT:GOTO 870
1100 IF A=3 AND XX<=170 AND 4-(XX/10)+(YY/11)-14=>MA  THEN FOR Q=1 TO 10:XX=XX+1:PUT SPRITE 1,(XX,YY),11,0:NEXT:GOTO 870
1110 IF A=7 AND XX>=10  THEN FOR Q=1 TO 10:XX=XX-1:PUT SPRITE 1,(XX,YY),11,0:NEXT :GOTO 870
1120 GOTO 870
1130 IF A=1 AND YY=>10 THEN FOR Q=1 TO 11:YY=YY-1:PUT SPRITE 1,(XX,YY),11,0:NEXT:GOTO 870
1140 IF A=5 AND YY<=190 AND 4-((XX/10)-14)+(YY/11)<=MA  THEN FOR Q=1 TO 11:YY=YY+1:PUT SPRITE 1,(XX,YY),11,0:NEXT:GOTO 870
1150 IF A=3 AND XX<=170 THEN FOR Q=1 TO 10:XX=XX+1:PUT SPRITE 1,(XX,YY),11,0:NEXT:GOTO 870
1160 IF A=7 AND XX>=10 AND 4-((XX/10)-14)+(YY/11)<=MA THEN FOR Q=1 TO 10:XX=XX-1:PUT SPRITE 1,(XX,YY),11,0:NEXT:GOTO 870
1170 GOTO 870
1180 ' MOVE OUT SHIPS
1190 XX=SB(PP,1):YY=SB(PP,2)
1200 IF PP=1 THEN X=XX:Y=YY+1:H=3
1210 IF PP=2 THEN X=XX:Y=YY-1:H=1
1220 IF PP=3 THEN X=XX:Y=YY-1:H=1
1230 IF PP=4 THEN X=XX:Y=YY+1:H=3
1240 PUT SPRITE 1,(X*10-10,Y*11-12),11,0
1250 A=STICK(0) OR STICK(1)
1260 IF A=1 AND YY>1 THEN Y=YY-1:X=XX:H=1
1270 IF A=3 AND XX<19 THEN Y=YY:X=XX+1:H=2
1280 IF A=5 AND YY<19 THEN Y=YY+1:X=XX:H=3
1290 IF A=7 AND XX>1 THEN Y=YY:X=XX-1:H=4
1300 IF STRIG(0) OR STRIG(1)=-1 THEN 1330
1310 IF INKEY$=CHR$(27) THEN PUT SPRITE 1,(255,211),0,1:GOTO 320
1320 GOTO 1240
1330 IF STRIG(0) OR STRIG(1)=-1 THEN 1330 ELSE PUT SPRITE 1,(255,211),0,1
1340 GOSUB 2680
1350 IF MM=1 AND C(X,Y)=>1 THEN GOSUB 2520:GOTO 1190
1360 IF MM=1 THEN M(PP)=M(PP)-1:COPY(0,PP*9+35)-(7,PP*9+42),1 TO (X*10-9,Y*11-9),0:C(X,Y)=20+PP:MI(PP,MI,1)=X:MI(PP,MI,2)=Y:MI(PP,MI,4)=2:P=PP:S=1:GOSUB 1490:S=1:COPY(192,0)-(255,51),0 TO (192,(PP-1)*52),2:GOTO 320
1370 IF FF=1 AND C(X,Y)=>1 THEN GOSUB 2520:GOTO 1190
1380 IF FF=1 THEN F(PP)=F(PP)-1:C(X,Y)=10+PP:FI(PP,FI,1)=X:FI(PP,FI,2)=Y:FI(PP,FI,3)=H:FI(PP,FI,4)=2:ON H GOSUB 1390,1400,1410,1420:GOTO 320
1390 COPY(50,PP*9-9)-(57,PP*9-2),1 TO (X*10-9,Y*11-9),0:S=1:P=PP:GOSUB 1490:S=PP:COPY(192,0)-(255,51),0 TO (192,(PP-1)*52),2:RETURN
1400 COPY(50,PP*9+27)-(57,PP*9+34),1 TO (X*10-9,Y*11-9),0:S=1:P=PP:GOSUB 1490:S=PP:COPY(192,0)-(255,51),0 TO (192,(PP-1)*52),2:RETURN
1410 COPY(50,PP*9+99)-(57,PP*9+106),1 TO (X*10-9,Y*11-9),0:S=1:P=PP:GOSUB 1490:S=PP:COPY(192,0)-(255,51),0 TO (192,(PP-1)*52),2:RETURN
1420 COPY(50,PP*9+63)-(57,PP*9+70),1 TO (X*10-9,Y*11-9),0:S=1:P=PP:GOSUB 1490:S=PP:COPY(192,0)-(255,51),0 TO (192,(PP-1)*52),2:RETURN
1430 ' PRINT MESSAGES
1440 COPY(47,86)-(152,123),0 TO (47,86),2:LINE(47,86)-(152,123),14,B:LINE(48,87)-(151,122),0,BF
1450 COLOR 14:L=LEN(J$(PP)):PRESET((202-L*8)/2,92):PRINT#1,J$(PP)
1460 PRESET(65,102):PRINT#1,"COLOCA TU":PRESET(53,112):PRINT#1,"BASE ESTELAR"
1470 RETURN
1480 ' DRAW RESERVE SPACE SHIPS
1490 LINE(192,15+(S-1)*52)-(255,47+(S-1)*52),0,BF:IF M(P)>0 THEN FOR N=1 TO M(P):COPY(0,35+PP*9)-(7,42+PP*9),1 TO (186+10*N,(S-1)*52+18),0:NEXT N
1500 IF F(P)>0 THEN FOR N=1 TO F(P):COPY(50,PP*9-9)-(57,PP*9-2),1 TO (186+10*N,(S-1)*52+35),0:NEXT N
1510 RETURN
1520 ' SHIP MOVING ROUTINE
1530 BB=1:IF MI(PP,0,0)-M(PP)-BL(PP,1)>0 THEN FOR I=1 TO MI(PP,0,0) ELSE GOTO 1560
1540 IF (MI(PP,I,1)=0 AND MI(PP,I,2)=0) OR MI(PP,I,4)>0 THEN NEXT I:GOTO 1560
1550 BB=0:GOSUB 1590:NEXT I
1560 BB=BB+1:IF FI(PP,0,0)-F(PP)-BL(PP,2)>0 THEN FOR I=1 TO FI(PP,0,0) ELSE RETURN
1570 IF (FI(PP,I,1)=0 AND FI(PP,I,2)=0) OR FI(PP,I,4)>0 THEN NEXT I:RETURN
1580 BB=0:GOSUB 1810:NEXT I:RETURN
1590 X=MI(PP,I,1):Y=MI(PP,I,2):PUT SPRITE 1,(X*10-10,Y*11-12),11,0:LINE(193,73)-(254,208),14,BF:COPY(192,0)-(255,156),3 TO (192,53),0
1600 A$=INPUT$(1):A=VAL(A$):IF (A<1 OR A>2) AND A$<>CHR$(27) THEN 1600
1610 PUT SPRITE 1,(255,211),0,1
1620 IF A$=CHR$(27) THEN I=0:RETURN 290
1630 IF A=2 THEN RETURN
1640 XX=X:YY=Y+1:GOSUB 2650
1650 PUT SPRITE 1,(XX*10-10,YY*11-12),11,0
1660 A=STICK(0) OR STICK(1)
1670 IF A=1 AND Y>1 THEN YY=Y-1:XX=X
1680 IF A=3 AND X<19 THEN YY=Y:XX=X+1
1690 IF A=5 AND Y<19 THEN YY=Y+1:XX=X
1700 IF A=7 AND X>1 THEN YY=Y:XX=X-1
1710 IF STRIG(0) OR STRIG(1)=-1 THEN 1740
1720 IF INKEY$=CHR$(27) THEN PUT SPRITE 1,(255,211),0,1:GOTO 1590
1730 GOTO 1650
1740 IF STRIG(0) OR STRIG(1)=-1 THEN 1740
1750 IF MI(PP,I,3)=1 THEN AS=36 ELSE AS=0
1760 IF C(XX,YY)=100+PP AND AS=36 THEN PUT SPRITE 1,(255,211),0,1:LINE(X*10-9,Y*11-9)-(X*10-2,Y*11-1),0,BF:GOSUB 2540:C(X,Y)=0:S=1:P=PP:GOSUB 1490:S=PP:COPY(192,0)-(255,51),0 TO (192,(PP-1)*52),2:MI(PP,I,1)=0:MI(PP,I,2)=0:MI(PP,I,3)=0:RETURN
1770 IF C(XX,YY)>2 OR (C(XX,YY)=1 AND AS=36) THEN GOSUB 2520:GOTO 1650
1780 IF C(XX,YY)=1 THEN AS=36
1790 LINE(X*10-9,Y*11-9)-(X*10-2,Y*11-1),0,BF:COPY(0,PP*9+35+AS)-(7,PP*9+42+AS),1 TO (XX*10-9,YY*11-9):MI(PP,I,1)=XX:MI(PP,I,2)=YY:MI(PP,I,3)=AS/36:MI(PP,I,4)=2:C(XX,YY)=20+PP:C(X,Y)=0:PUT SPRITE 1,(255,211),0,1:RETURN
1800 ' MOVE FIGHTER
1810 Q=FI(PP,I,1):W=FI(PP,I,2):X=Q:Y=W:H=FI(PP,I,3):PUT SPRITE 1,(X*10-10,Y*11-12),11,0:LINE(193,73)-(254,208),14,BF:COPY(128,0)-(191,156),3 TO (192,53),0
1820 A$=INPUT$(1):A=VAL(A$):IF (A<1 OR A>4) AND A$<>CHR$(27) THEN 1820
1830 PUT SPRITE 1,(255,211),0,1
1840 IF A$=CHR$(27) THEN I=0:RETURN 290
1850 ON A GOTO 1870,2060,2220,1860
1860 FI(PP,I,4)=2:RETURN
1870 FOR J=1 TO 3:X=FI(PP,I,1):Y=FI(PP,I,2):H=FI(PP,I,3):ON H GOSUB 1880,1890,1900,1910:GOSUB 2590:GOTO 1920
1880 XX=X:YY=Y-1:AS=-9:RETURN
1890 XX=X+1:YY=Y:AS=27:RETURN
1900 XX=X:YY=Y+1:AS=99:RETURN
1910 XX=X-1:YY=Y:AS=63:RETURN
1920 PUT SPRITE 1,(XX*10-10,YY*11-12),11,0
1930 A=STICK(0) OR STICK(1)
1940 IF A=1 AND Y>1 AND H<>3 THEN YY=Y-1:XX=X
1950 IF A=3 AND X<19 AND H<>4 THEN YY=Y:XX=X+1
1960 IF A=5 AND Y<19 AND H<>1 THEN YY=Y+1:XX=X
1970 IF A=7 AND X>1 AND H<>2 THEN YY=Y:XX=X-1
1980 IF STRIG(0) OR STRIG(1)=-1 THEN 2010
1990 IF INKEY$=CHR$(27) THEN FOR K=1 TO J-1:C(MV(K,1),MV(K,2))=0:NEXT K:LINE(X*10-9,Y*11-9)-(X*10-2,Y*11-1),0,BF:COPY(50,PP*9+AS)-(57,PP*9+AS+7),1 TO (Q*10-9,W*11-9),0:FI(PP,I,1)=Q:FI(PP,I,2)=W:C(Q,W)=10+PP:PUT SPRITE 1,(255,211),0,1:GOTO 1810
2000 GOTO 1920
2010 IF STRIG(0) OR STRIG(1)=-1 THEN 2010
2020 IF C(XX,YY)=>1 THEN GOSUB 2520:GOTO 1920
2030 MV(J,1)=XX:MV(J,2)=YY:LINE(X*10-9,Y*11-9)-(X*10-2,Y*11-1),0,BF:COPY(50,PP*9+AS)-(57,PP*9+AS+7),1 TO (XX*10-9,YY*11-9),0:FI(PP,I,1)=XX:FI(PP,I,2)=YY:FI(PP,I,4)=2:C(XX,YY)=10+PP:C(X,Y)=0:PUT SPRITE 1,(255,211),0,1
2040 NEXT J:RETURN
2050 ' SWIVEL FIGHTER
2060 X=FI(PP,I,1):Y=FI(PP,I,2):H=FI(PP,I,3):PUTSPRITE 1,(X*10-10,Y*11-12),11,0
2070 A=STICK(0) OR STICK(1)
2080 IF A=1 THEN H=1
2090 IF A=3 THEN H=2
2100 IF A=5 THEN H=3
2110 IF A=7 THEN H=4
2120 IF STRIG(0) OR STRIG(1)=-1 THEN 2190
2130 IF INKEY$=CHR$(27) THEN H=FI(PP,I,3):ON H GOSUB 2150,2160,2170,2180:COPY (50,PP*9+AS)-(57,PP*9+AS+7),1 TO (X*10-9,Y*11-9),0:PUT SPRITE 1,(255,211),0,1:GOTO 1810
2140 ON H GOSUB 2150,2160,2170,2180:COPY(50,PP*9+AS)-(57,PP*9+AS+7),1 TO (X*10-9,Y*11-9),0:GOTO 2070
2150 AS=-9:RETURN
2160 AS=27:RETURN
2170 AS=99:RETURN
2180 AS=63:RETURN
2190 IF STRIG(0) OR STRIG(1)=-1 THEN 2190
2200 FI(PP,I,3)=H:FI(PP,I,4)=2:PUT SPRITE 1,(255,211),0,1:RETURN
2210 ' SHOOT MISSILE
2220 PUT SPRITE 1,(255,211),0,1:X=FI(PP,I,1):Y=FI(PP,I,2):H=FI(PP,I,3)
2230 ON H GOSUB 2240,2250,2260,2270:GOTO 2280
2240 DX=0:DY=-1:MX=-9:MY=-16:FF=11:FQ=-6:RETURN
2250 DX=1:DY=0:MX=-2:MY=-10:FF=10:FQ=-7:RETURN
2260 DX=0:DY=1:MX=-9:MY=-4:FF=11:FQ=-6:RETURN
2270 DX=-1:DY=0:MX=-15:MY=-10:FQ=-7:FF=10:RETURN
2280 R=INT(RND(-TIME)*15)+1
2290 IF R<6 THEN DI=1:GOTO 2340
2300 IF R>5 AND R<10 THEN DI=2:GOTO 2340
2310 IF R>9 AND R<13 THEN DI=3:GOTO 2340
2320 IF R>12 AND R<14 THEN DI=4:GOTO 2340
2330 IF R=15 THEN DI=5:GOTO 2340
2340 IF DI*DX+X>19 THEN DI=19-X
2350 IF DI*DY+Y>19 THEN DI=19-Y
2360 IF DI*DY+Y<1 THEN DI=Y-1
2370 IF DI*DX+X<1 THEN DI=X-1
2380 IF DI=0 THEN GOSUB 2520:GOTO 1810
2390 DD=DI:EX=0:FOR J=1 TO DI
2400 IF DD<>DI THEN NEXT J:GOTO 2440
2410 IF C(X+DX*J,Y+DY*J)>0 THEN EX=1:DD=J
2420 IF X+DX*J=0 OR Y+DY*J=0 THEN EX=0:DD=J-DX-DY
2430 NEXT J
2440 IF C(X+DX*DD,Y+DY*DD)>100 THEN EX=0
2450 FOR J=1 TO DD*FF+FQ
2460 PUT SPRITE H+4,(X*10+DX*J+MX,Y*11+DY*J+MY),CO(PP),H+4
2470 NEXT J
2480 PUT SPRITE H+4,(255,211),0,H+4:FI(PP,I,4)=2
2490 IF EX=1 THEN FOR J=0 TO 8 STEP 2:LINE((X+DX*DD)*10-9,(Y+DY*DD)*11-9+J)-((X+DX*DD)*10-1,(Y+DY*DD)*11-9+J),0:NEXT J:FOR J=1 TO 7 STEP 2:LINE((X+DX*DD)*10-9,(Y+DY*DD)*11-9+J)-((X+DX*DD)*10-1,(Y+DY*DD)*11-9+J),0:NEXT J:GOSUB 2760
2500 RETURN
2510 ' WRITE "IMPOSSIBLE"
2520 COPY(56,96)-(136,113),0 TO (56,96),2:COPY(65,158)-(145,175),3 TO (56,96),0:GOSUB 3440:COPY(56,96)-(136,113),2 TO (56,96),0:RETURN
2530 ' CHOOSE BETWEEN MINER AND FIGHTER
2540 LINE(193,73)-(254,208),14,BF:COPY(0,0)-(63,68),2 TO (192,73),0:LINE(193,142)-(254,208),0,BF
2550 A$=INPUT$(1):A=VAL(A$):IF A<1 OR A>2 THEN 2550 ELSE ON A GOSUB 2560,2570:RETURN
2560 M(PP)=M(PP)+2:MI(PP,0,0)=MI(PP,0,0)+1:MC=2:FC=0:RETURN
2570 F(PP)=F(PP)+1:M(PP)=M(PP)+1:FI(PP,0,0)=FI(PP,0,0)+1:MC=1:FC=1:RETURN
2580 ' CORRECT X,Y IF THEY LEAVE THE SCREEN
2590 IF H=1 AND YY=0 THEN XX=XX+1:YY=1:IF XX=20 THEN XX=18
2600 IF H=2 AND XX=20 THEN XX=19:YY=YY+1:IF YY=20 THEN YY=18
2610 IF H=3 AND YY=20 THEN XX=XX+1:YY=19:IF XX=20 THEN XX=18
2620 IF H=4 AND XX=0 THEN XX=1:YY=YY+1:IF YY=20 THEN YY=18
2630 RETURN
2640 ' CORRECT X,Y IF THEY LEAVE THE SCREEN
2650 IF YY=20 THEN XX=XX+1:YY=19:IF XX=20 THEN XX=18
2660 RETURN
2670 ' SET SHIP I.D. NUMBER STRAIGHT
2680 IF MM=1 THEN 2690 ELSE IF FF=1 THEN 2740 ELSE RETURN
2690 L=MI(PP,0,0):FOR J=1 TO MI(PP,0,0)
2700 IF L<>MI(PP,0,0) THEN NEXT J:GOTO 2730
2710 IF MI(PP,J,1)=0 AND MI(PP,J,2)=0 THEN L=J
2720 NEXT J
2730 MI=L:RETURN
2740 FI=FI(PP,0,0):RETURN
2750 ' FIX EXPLOSION VARIABLES
2760 J=X+DX*DD:K=Y+DY*DD:U=C(J,K)
2770 IF U>20 AND U<30 THEN PY=U-20:BL(PY,1)=BL(PY,1)+1:GOSUB 2830:MI(PY,SH,1)=0:MI(PY,SH,2)=0:MI(PY,SH,4)=1:GOSUB 2940:IF MI(PY,SH,3)=1 THEN 2800 ELSE 2810
2780 IF U>10 AND U<20 THEN PY=U-10:BL(PY,2)=BL(PY,2)+1:GOSUB 2880:FI(PY,SH,1)=0:FI(PY,SH,2)=0:FI(PY,SH,3)=0:FI(PY,SH,4)=1:GOSUB 2940:GOTO 2810
2790 GOTO 2810
2800 C(J,K)=1:MI(PY,SH,3)=0:COPY(0,0)-(7,7),1 TO (J*10-9,K*11-8),0:RETURN
2810 C(J,K)=0:RETURN
2820 ' FIND SHIP BY COORDINATES
2830 SH=0:FOR O=1 TO MI(PY,0,0)
2840 IF SH<>0 THEN NEXT O:GOTO 2870
2850 IF MI(PY,O,1)=J AND MI(PY,O,2)=K THEN SH=O
2860 NEXT O
2870 RETURN
2880 SH=0:FOR O=1 TO FI(PY,0,0)
2890 IF SH<>0 THEN NEXT O:GOTO 2920
2900 IF FI(PY,O,1)=J AND FI(PY,O,2)=K THEN SH=O
2910 NEXT O
2920 RETURN
2930 ' PLAYER BUYS IT
2940 IF MI(PY,0,0)-BL(PY,1)=0 AND FI(PY,0,0)-BL(PY,2)=0 THEN 2950 ELSE RETURN
2950 GOSUB 2970:RETURN
2960 ' TELL PLAYER HE'S BOUGHT IT
2970 COPY(11,86)-(179,133),0 TO (11,86),2:LINE(11,86)-(179,133),14,BF:LINE(12,87)-(178,132),0,BF:PRESET((193-LEN(J$(PY))*8)/2,91):PRINT#1,J$(PY)
2980 PRESET(17,101):PRINT#1,"HA PERDIDO SUS NAVES"
2990 PRESET(33,111):PRINT#1,"Y DEBE ABANDONAR"
3000 PRESET(57,121):PRINT#1,"LA GALAXIA"
3010 A$=INPUT$(1)
3020 Q=SB(PY,1):W=SB(PY,2):COPY(11,86)-(179,133),2 TO (11,86),0:LINE(Q*10-9,W*11-9)-(Q*10-2,W*11-1),0,BF:C(Q,W)=0:QU(PY)=1
3030 IF QU(1)+QU(2)+QU(3)+QU(4)=PL-1 THEN 3140
3040 RETURN
3050 ' WHEN PLAYER QUITS
3060 Q=SB(PP,1):W=SB(PP,2):R=0:LINE(Q*10-9,W*11-9)-(Q*10-2,W*11-1),0,BF:C(Q,W)=0
3070 R=R+1:IF MI(PP,0,0)=>R THEN Q=MI(PP,R,1):W=MI(PP,R,2):GOTO 3110
3080 R=0
3090 R=R+1:IF FI(PP,0,0)=>R THEN Q=FI(PP,R,1):W=FI(PP,R,2):GOTO 3120
3100 QU(PP)=1:RETURN
3110 IF Q=0 AND W=0 THEN GOTO 3070 ELSE LINE(Q*10-9,W*11-9)-(Q*10-2,W*11-1),0,BF:C(Q,W)=0:GOTO 3070
3120 IF Q=0 AND W=0 THEN GOTO 3090 ELSE LINE(Q*10-9,W*11-9)-(Q*10-2,W*11-1),0,BF:C(Q,W)=0:GOTO 3090
3130 ' END
3140 FOR I=1 TO PL
3150 IF QU(I)=1 THEN NEXT I:GOTO 3170
3160 PY=I:NEXT I
3170 LINE(15,77)-(175,133),14,BF:LINE(16,78)-(174,132),0,BF
3180 PRESET((194-LEN(J$(PY))*8)/2,81):PRINT#1,J$(PY)
3190 PRESET(49,91):PRINT#1,"HA ELIMINADO"
3200 PRESET(21,101):PRINT#1,"A TODOS SUS RIVALES"
3210 PRESET(33,111):PRINT#1,"Y HA CONQUISTADO"
3220 PRESET(57,121):PRINT#1,"LA GALAXIA"
3230 FOR I=1 TO 2000:NEXT I
3240 A=1:B=2:C=3:D=4:E=5:F=6:FOR I=6 TO 0 STEP -1
3250 IF I>5 THEN A=A-1
3260 IF I>4 THEN B=B-1
3270 IF I>3 THEN C=C-1
3280 IF I>2 THEN D=D-1
3290 IF I>1 THEN E=E-1
3300 IF I>0 THEN F=F-1
3310 COLOR=(0,A,A,A):COLOR=(2,I,A,A):COLOR=(3,I,C,A):COLOR=(4,A,A,I):COLOR=(5,B,C,I):COLOR=(6,F,0,0):COLOR=(7,A,C,A):COLOR=(8,A,D,A):COLOR=(9,F,I,A):COLOR=(10,D,I,A):COLOR=(12,0,0,D):COLOR=(13,D,C,E):COLOR=(14,D,D,D):COLOR=(15,E,0,0)
3320 NEXT I
3330 SCREEN8:SET PAGE 0,1:CLS:BLOAD"ARGENTA.BIN":DEFUSR=&HC001:PRINTUSR("ARGENTA4.SCR")
3340 FOR I=0 TO 127:J=INT(I/(127/106))
3350 COPY(127-I,106-J)-(127+I,106-J),1 TO (127-I,106-J),0
3360 COPY(127-I,106+J)-(127+I,106+J),1 TO (127-I,106+J),0
3370 COPY(127-I,106-J)-(127-I,106+J),1 TO (127-I,106-J),0
3380 COPY(127+I,106-J)-(127+I,106+J),1 TO (127+I,106-J),0
3390 NEXT I
3400 A$=INPUT$(1)
3410 SET PAGE 0,0:FOR I=0 TO 127:LINE(I,I)-(255-I,211-I),0,B:NEXT I
3420 RUN"ARGENTA2.LDR"
3430 ' PUSH A KEY
3440 IF STRIG(0)=0 AND STRIG(1)=0 AND STICK(0)=0 AND STICK(1)=0 THEN 3440 ELSE RETURN
3450 ' DATA
3460 DATA FF,80,80,80,80,80,80,80,80,80,80,FF,00,00,00,00,E0,20,20,20,20,20,20,20,20,20,20,E0,00,00,00,00
3470 DATA 18,3C,7E,18,18,18,18,18
3480 DATA 0,4,6,FF,FF,6,4,0
3490 DATA 18,18,18,18,18,7E,3C,18
3500 DATA 0,20,60,FF,FF,60,20,0
3510 DATA 6,4,8,10
